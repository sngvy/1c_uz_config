// Установить или снять блокировку информационной базы,
// исходя из значений реквизитов обработки.
//
Процедура ВыполнитьУстановку() Экспорт
	
	Блокировка = Новый БлокировкаСеансов;
	Блокировка.Начало           = НачалоБлокировки;
	Блокировка.Конец            = ОкончаниеБлокировки;
	Блокировка.Сообщение        = СоединенияИБ.СформироватьСообщениеБлокировки(Сообщение, КодРазрешения); 
	Блокировка.Установлена      = УстановитьБлокировкуСоединений;
	Блокировка.КодРазрешения    = КодРазрешения;
	
	УстановитьБлокировкуСеансов(Блокировка);
	
КонецПроцедуры

// Зачитать параметры блокировки информационной базы 
// в реквизиты обработки.
//
Процедура ПолучитьПараметрыБлокировки() Экспорт
	
	ТекущийРежим = ПолучитьБлокировкуСеансов();
	
	УстановитьБлокировкуСоединений = ТекущийРежим.Установлена;
	Сообщение                      = СоединенияИБКлиентСервер.ИзвлечьСообщениеБлокировки(ТекущийРежим.Сообщение);
	Если Сообщение = "" Тогда
		Сообщение = "Для администратора:
				    |Чтобы принудительно разблокировать информационную базу, воспользуйтесь консолью кластера серверов или 
	                |запустите ""1С:Предприятие"" с параметрами:
	                |ENTERPRISE /S""NK_TM:1541\udz_test"" /CРазрешитьРаботуПользователей /UC<код разрешения>";
	КонецЕсли;
	КодРазрешения                  = ТекущийРежим.КодРазрешения;
	Если КодРазрешения = "" Тогда
		КодРазрешения = 1;	
	КонецЕсли;
	Если УстановитьБлокировкуСоединений Тогда
		НачалоБлокировки           = ТекущийРежим.Начало;
		ОкончаниеБлокировки        = ТекущийРежим.Конец;
	Иначе	
		// Если блокировка не установлена, можно предположить, что
		// пользователь открыл форму для установки блокировки.
		// Поэтому устанавливаем дату блокировки равной текущей дате + 2 и + 5 минут соответственно.
		НачалоБлокировки           = ТекущаяДатаСеанса() + 60 * 2;
		ОкончаниеБлокировки        = ТекущаяДатаСеанса() + 60 * 5;
	КонецЕсли;
	
КонецПроцедуры

