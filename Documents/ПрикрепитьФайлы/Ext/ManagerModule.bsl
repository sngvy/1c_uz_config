
#Область ПрограммныйИнтерфейс

Функция СохраненныеФайлы(Основание) Экспорт

	Возврат РегистрыСведений.ПрикрепляемыеФайлы
		.СохраненныеФайлы(Основание);

КонецФункции // ()

Функция Создать() Экспорт

	Возврат СоздатьДокумент()
		.ЗаполнитьПоУмолчанию();

КонецФункции // ()

Функция ПрикрепитьНаборФайлов(Набор, Клиент, Автор = Неопределено) Экспорт

	Прикреплятель = Создать()
		.СохранениеФайловПриПроведении()
		.Основание(Клиент);
	Если Автор <> Неопределено Тогда
	
		Прикреплятель.Автор(Автор);
	
	КонецЕсли;
	
	СохранитьФайлы(Прикреплятель, Набор);
	
	Прикреплятель.Записать(РежимЗаписиДокумента.Проведение);
	
	Возврат Прикреплятель.Ссылка;

КонецФункции // ()

Функция СохранитьФайлы(Прикреплятель, Набор) Экспорт

	Пока ЕстьСледующийФайл(Набор) Цикл
	
		Описание = СледующийФайл(Набор);
		
		Прикреплятель.ЗаписьФайла(Описание);
	
	КонецЦикла;

КонецФункции // ()

#Область СохранениеДанных

Функция ОписаниеФайлаПоУмолчанию() Экспорт

	Описание = Новый Структура;
	Описание.Вставить("Путь", Неопределено);
	Описание.Вставить("ТипФайла", Неопределено);
	Описание.Вставить("Данные", Неопределено);
	Описание.Вставить("Идентификатор", Неопределено);
	
	Возврат Описание;

КонецФункции // ()

Функция НаборФайловПоУмолчанию() Экспорт

	Описание = Новый Структура;
	Описание.Вставить("Количество", 0);
	Описание.Вставить("Позиция", 0);
	Описание.Вставить("Коллекция", Новый Массив);
	
	Возврат Описание;

КонецФункции // ()

Процедура ДобавитьФайлВНабор(Набор, Файл) Экспорт

	Если Не ЭтоОписаниеФайла(Файл) Тогда
	
		ВызватьИсключение "Параметр Файл не является Описанием файла";
	
	КонецЕсли;
	
	Коллекция = Набор["Коллекция"];
	
	Коллекция.Добавить(Файл);
	
	Набор["Количество"] = Набор["Количество"] + 1;

КонецПроцедуры

Функция ЕстьСледующийФайл(Набор) Экспорт

	Возврат Набор["Позиция"] < Набор["Количество"];

КонецФункции // ()

Функция СледующийФайл(Набор) Экспорт

	Элемент = Набор["Коллекция"][Набор["Позиция"]];
	
	Набор["Позиция"] = Набор["Позиция"] + 1;
	
	Возврат Элемент;

КонецФункции // ()

Функция ПолноеИмяФайлаНаСервере(ЗаписьФайла, КаталогХранения) Экспорт

	Файл = Новый Файл(ЗаписьФайла.Путь);
	
	Возврат КаталогХранения + ПолучитьРазделительПути() + Строка(ЗаписьФайла.УИД) + Файл.Расширение

КонецФункции // ()

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий



#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс



#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЭтоОписаниеФайла(Файл)

	Описание = ОписаниеФайлаПоУмолчанию();
	
	Для каждого Поле Из Описание Цикл
	
		Если Не Файл.Свойство(Поле.Ключ) Тогда
		
			Возврат Ложь;
		
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат Истина;

КонецФункции // ()

#КонецОбласти