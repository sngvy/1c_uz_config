
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	Мероприятие.Ссылка,
	                      |	Мероприятие.Автор,
	                      |	Мероприятие.Объект,
	                      |	Мероприятие.Организация,
	                      |	Мероприятие.Ответственный,
	                      |	Мероприятие.ПланируемаяДата,
	                      |	Мероприятие.Подразделение,
	                      |	Мероприятие.ТипМероприятия
	                      |ПОМЕСТИТЬ СуществующиеМероприятия
	                      |ИЗ
	                      |	Задача.Мероприятие КАК Мероприятие
	                      |ГДЕ
	                      |	Мероприятие.Автор = &Автор
	                      |	И Мероприятие.ПометкаУдаления = ЛОЖЬ
	                      |	И Мероприятие.Выполнена = ЛОЖЬ
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ПакетноеСозданиеМероприятийОбъекты.Ссылка.Ссылка,
	                      |	ПакетноеСозданиеМероприятийОбъекты.Ссылка.Организация,
	                      |	ПакетноеСозданиеМероприятийОбъекты.Ссылка.Подразделение,
	                      |	ПакетноеСозданиеМероприятийОбъекты.Ссылка.Ответственный,
	                      |	ПакетноеСозданиеМероприятийОбъекты.Ссылка.ТипМероприятия,
	                      |	ПакетноеСозданиеМероприятийОбъекты.Ссылка.ПланируемаяДата,
	                      |	ПакетноеСозданиеМероприятийОбъекты.Объект
	                      |ПОМЕСТИТЬ ТЧ
	                      |ИЗ
	                      |	Документ.ПакетноеСозданиеМероприятий.Объекты КАК ПакетноеСозданиеМероприятийОбъекты
	                      |ГДЕ
	                      |	ПакетноеСозданиеМероприятийОбъекты.Ссылка = &Автор
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ТЧ.Организация,
	                      |	ТЧ.Подразделение,
	                      |	ТЧ.Ответственный,
	                      |	ТЧ.ТипМероприятия,
	                      |	ТЧ.ПланируемаяДата,
	                      |	ТЧ.Объект,
	                      |	СуществующиеМероприятия.Ссылка КАК Мероприятие,
	                      |	ВЫБОР
	                      |		КОГДА СуществующиеМероприятия.Ссылка ЕСТЬ NULL 
	                      |			ТОГДА ""Создать""
	                      |		ИНАЧЕ ВЫБОР
	                      |				КОГДА ТЧ.Объект ЕСТЬ NULL 
	                      |					ТОГДА ""Удалить""
	                      |				ИНАЧЕ ""Обновить""
	                      |			КОНЕЦ
	                      |	КОНЕЦ КАК Статус
	                      |ИЗ
	                      |	ТЧ КАК ТЧ
	                      |		ПОЛНОЕ СОЕДИНЕНИЕ СуществующиеМероприятия КАК СуществующиеМероприятия
	                      |		ПО ТЧ.Объект = СуществующиеМероприятия.Объект");
	Запрос.УстановитьПараметр("Автор", Ссылка);
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		Если Результат.Статус = "Создать" Тогда
			НовоеМероприятие = Задачи.Мероприятие.СоздатьЗадачу();
			НовоеМероприятие.Дата = ЭтотОбъект.Дата;
			Если ТипЗнч(Ответственный)=Тип("СправочникСсылка.Пользователи") И ЗначениеЗаполнено(Ответственный) Тогда
				НовоеМероприятие.Организация = Ответственный.Организация;
				НовоеМероприятие.Подразделение = Ответственный.Подразделение;
			Иначе
				НовоеМероприятие.Организация = Организация;
				НовоеМероприятие.Подразделение = Подразделение;
			КонецЕсли;
			НовоеМероприятие.Ответственный = Ответственный;
			НовоеМероприятие.ТипМероприятия = ТипМероприятия;
			НовоеМероприятие.Объект = Результат.Объект;
			НовоеМероприятие.ПланируемаяДата = ПланируемаяДата;
			НовоеМероприятие.ПланируемоеВремя = ПланируемоеВремя;
			НовоеМероприятие.ПланируемоеВремяКонца = ПланируемоеВремяКонца;
			НовоеМероприятие.Автор = ЭтотОбъект.Ссылка;
			НовоеМероприятие.Комментарий = Комментарий;	
			//++КазанцевЯА-БП
			Если Константы.КонтрольСроковМероприятий.Получить() И НовоеМероприятие.ТипМероприятия.СрокВыполнения > 0 Тогда 
				ВремяКон = ?(ЗначениеЗаполнено(ПланируемоеВремяКонца),ПланируемоеВремяКонца,ПланируемоеВремя);
				НовоеМероприятие.СрокВыполнения = Дата(1,1,1) + (ПланируемаяДата - Дата(1,1,1)) + (ВремяКон - Дата(1,1,1)) + НовоеМероприятие.ТипМероприятия.СрокВыполнения * 60;
				ОсновнойКалендарьПредприятия = Константы.ОсновнойКалендарьПредприятия.Получить();
				Если Константы.ПереноситьВыходныеИПраздникиМероприятий.Получить() И ЗначениеЗаполнено(ОсновнойКалендарьПредприятия) Тогда
					УстановитьПривилегированныйРежим(Истина);
					ПроизводственныйКалендарь = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОсновнойКалендарьПредприятия, "ПроизводственныйКалендарь");
					ПараметрыПолучения = КалендарныеГрафики.ПараметрыПолученияБлижайшихРабочихДат();
					ПараметрыПолучения.ПолучатьПредшествующие = Ложь;
					ПараметрыПолучения.ВызыватьИсключение = Ложь;
					свДата = НачалоДня(НовоеМероприятие.СрокВыполнения);
					свВремя =  НовоеМероприятие.СрокВыполнения - свДата;
					мСрокВыполнения = Новый Массив;
					мСрокВыполнения.Добавить(свДата);
					ДниКалендаря = КалендарныеГрафики.БлижайшиеРабочиеДаты(ПроизводственныйКалендарь, мСрокВыполнения, ПараметрыПолучения);
					НовоеМероприятие.СрокВыполнения = ДниКалендаря.Получить(свДата) + свВремя;
					УстановитьПривилегированныйРежим(Ложь);	
				КонецЕсли;
			КонецЕсли;
			//--КазанцевЯА-БП
			НовоеМероприятие.Записать();
			
		ИначеЕсли Результат.Статус = "Удалить" Тогда                            
			Мероприятие = Результат.Мероприятие.ПолучитьОбъект();
			Мероприятие.ПометкаУдаления = Истина;
			Мероприятие.Записать();
			
		ИначеЕсли Результат.Статус = "Обновить" Тогда
			Мероприятие = Результат.Мероприятие.ПолучитьОбъект();
			Мероприятие.Дата = ЭтотОбъект.Дата;
			Если ТипЗнч(Ответственный)=Тип("СправочникСсылка.Пользователи") и ЗначениеЗаполнено(Ответственный) Тогда
				Мероприятие.Организация = Ответственный.Организация;
				Мероприятие.Подразделение = Ответственный.Подразделение;
			Иначе
				Мероприятие.Организация = Организация;
				Мероприятие.Подразделение = Подразделение;
			КонецЕсли;
			Мероприятие.Ответственный = Ответственный;
			Мероприятие.ТипМероприятия = ТипМероприятия;
			Мероприятие.Объект = Результат.Объект;
			Мероприятие.ПланируемаяДата = ПланируемаяДата;
			Мероприятие.ПланируемоеВремя = ПланируемоеВремя;
			Мероприятие.ПланируемоеВремяКонца = ПланируемоеВремяКонца;
			Мероприятие.Автор = ЭтотОбъект.Ссылка;
			Мероприятие.Комментарий = Комментарий;
			Мероприятие.Записать();
		КонецЕсли;	
	КонецЦикла;	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ) 
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	Мероприятие.Ссылка
	                      |ИЗ
	                      |	Задача.Мероприятие КАК Мероприятие
	                      |ГДЕ
	                      |	Мероприятие.Автор = &Автор
	                      |	И Мероприятие.ПометкаУдаления = ЛОЖЬ
	                      |	И Мероприятие.Выполнена = ЛОЖЬ");
	Запрос.УстановитьПараметр("Автор", Ссылка);
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		Мероприятие = Результат.Ссылка.ПолучитьОбъект();
		Мероприятие.ПометкаУдаления = Истина;
		Мероприятие.Записать();
	КонецЦикла;	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	ОбъектыСервер.ОбработкаЗаполненияДокументов(ЭтотОбъект, ДанныеЗаполнения, СтандартнаяОбработка);
КонецПроцедуры
