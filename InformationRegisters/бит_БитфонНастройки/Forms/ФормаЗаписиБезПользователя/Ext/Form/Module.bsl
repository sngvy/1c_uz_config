
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьДоступность();
	Элементы.ВерсияБитфона.Заголовок = "Версия БИТ.Phone " + бит_ТелефонияСервер.ПолучитьВерсиюПодсистемы();
	
	стрПоУмолчанию = ПолучитьИмяУстройстваПоУмолчанию();
	Элементы.УстройствоВыводаЗвонка.СписокВыбора.Очистить();
	Элементы.УстройствоВыводаЗвонка.СписокВыбора.Добавить(стрПоУмолчанию);
	списокУстройств = бит_ТелефонияКлиентСервер.СтрРазбить(Параметры.СписокУстройствВывода, ";");
	Для Каждого ЗвукУстройствоЭл Из списокУстройств Цикл
		ЗвукУстройство = ЗвукУстройствоЭл.Значение;
		Элементы.УстройствоВыводаЗвонка.СписокВыбора.Добавить(ЗвукУстройство);
	КонецЦикла;
	УстройствоВыводаЗвонка = Запись.УстройствоВыводаЗвукаВходящегоЗвонка;
	Если НЕ ЗначениеЗаполнено(УстройствоВыводаЗвонка) Тогда
		УстройствоВыводаЗвонка = стрПоУмолчанию;
	КонецЕсли;
	
	ОбновитьСписокКодеков();
	
КонецПроцедуры

&НаКлиенте
Процедура ДиректорияЗаписанныхФайловНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	стрКаталогНач = Запись.ДиректорияЗаписанныхФайлов;
	длг = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	длг.Каталог = стрКаталогНач;
	бит_ТелефонияКлиентПереопределяемый.ПоказВыборФайла(длг, "БитФонНастройки_ВыборДиректорииЗаписиЗавершение", стрКаталогНач);
КонецПроцедуры

&НаКлиенте
Процедура ПрофильНастроекПриИзменении(Элемент)
	
	бит_БитфонКлиент.ОбработчикИзмененияПрофиляНастроек(Запись, Истина);
	
	ОбновитьДоступность();
	
	ОбновитьСписокКодеков();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздаватьСобытиеПриВходящемЗвонкеПриИзменении(Элемент)
	ОбновитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура СоздаватьСобытиеПриИсходящемЗвонкеПриИзменении(Элемент)
	ОбновитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ПриниматьНесколькоВходящихПриИзменении(Элемент)
	ОбновитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура СерверЛицензийВерсияПриИзменении(Элемент)
	ОбновитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ТипПереводаЗвонкаПриИзменении(Элемент)
	ОбновитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьПрямойНаборПриИзменении(Элемент)
	ОбновитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьКомандуНеБеспокоитьНаАТСПриИзменении(Элемент)
	ОбновитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ПолучатьЗаписиРазговоровСБИТАТСПриИзменении(Элемент)
	ОбновитьДоступность();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	стрСписокКодеков = Запись.СписокКодеков;
	Если СписокКодеков.Количество() > 0 Тогда
		стрСписокКодеков = "";
		Для Каждого строкаКодек Из СписокКодеков Цикл
			стрКодек = строкаКодек.Кодек;
			стрПриор = Строка(строкаКодек.Приоритет);
			стрСписокКодеков = стрСписокКодеков + стрКодек + "=" + стрПриор + ";";
		КонецЦикла;
	КонецЕсли;
	ТекущийОбъект.СписокКодеков = стрСписокКодеков;
КонецПроцедуры

&НаКлиенте
Процедура УстройствоВыводаЗвонкаПриИзменении(Элемент)
	стрПоУмолчанию = ПолучитьИмяУстройстваПоУмолчанию();
	стрУстройствоВывода = УстройствоВыводаЗвонка;
	Если стрУстройствоВывода = стрПоУмолчанию Тогда
		стрУстройствоВывода = "";
	КонецЕсли;
	Если Запись.УстройствоВыводаЗвукаВходящегоЗвонка <> стрУстройствоВывода Тогда
		Запись.УстройствоВыводаЗвукаВходящегоЗвонка = стрУстройствоВывода;
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокКодековПриоритетПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТипВходящегоЗвонкаПриИзменении(Элемент)
	ОбновитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ФайлВходящегоЗвонкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	стрФайлНач = Запись.ФайлВходящегоЗвонка;
	длг = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	длг.МножественныйВыбор = Ложь;
	длг.ПроверятьСуществованиеФайла = Истина;
	длг.ПолноеИмяФайла = стрФайлНач;
	длг.Фильтр = "Звуковые файлы (*.wav;*.mp3)|*.wav;*.mp3";
	бит_ТелефонияКлиентПереопределяемый.ПоказВыборФайла(длг, "БитФонНастройки_ВыборФайлаВходящегоЗвонкаЗавершение", стрФайлНач);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "БитФонНастройки_ВыборДиректорииЗаписиЗавершение" Тогда
		ДиректорияЗаписанныхФайловЗавершение(Параметр.Результат, Параметр.ПараметрОповещения);
	ИначеЕсли ИмяСобытия = "БитФонНастройки_ВыборФайлаВходящегоЗвонкаЗавершение" Тогда
		ФайлВходящегоЗвонкаЗавершение(Параметр.Результат, Параметр.ПараметрОповещения);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьПапкуСЛогФайлом(Команда)
	бит_ТелефонияКлиентПереопределяемый.ЗапускПрограммы("%APPDATA%\BIT");
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросВТехподдержку(Команда)
	бит_БитфонКлиент.ОтправитьЗапросВТехподдержкуСАрхивомЛога();
КонецПроцедуры

&НаКлиенте
Процедура ТестВходящегоЗвонка(Команда)
	ПараметрОповещ = Новый Структура;
	ПараметрОповещ.Вставить("ТипВходящегоЗвонка", Запись.ТипВходящегоЗвонка);
	ПараметрОповещ.Вставить("ФайлВходящегоЗвонка", Запись.ФайлВходящегоЗвонка);
	Оповестить("БитФонНастройки_ТестВходящегоЗвонка", ПараметрОповещ);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПолучитьИмяУстройстваПоУмолчанию()
	Возврат "(по умолчанию)";
КонецФункции

&НаКлиенте
Процедура ДиректорияЗаписанныхФайловЗавершение(ВыбранныеФайлы, НачальныйКаталог)
	Если ЗначениеЗаполнено(ВыбранныеФайлы) Тогда
		выбранКаталог = ВыбранныеФайлы[0];
		Запись.ДиректорияЗаписанныхФайлов = выбранКаталог;
		Если выбранКаталог <> НачальныйКаталог Тогда
			Модифицированность = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДоступность()
	Элементы.ПрефиксВыходаНаВнешнююЛинию.Доступность = НЕ Запись.ИспользоватьПрямойНабор;
	//
	Элементы.СоздаватьСобытияПриВнутреннихЗвонках.Доступность = (Запись.СоздаватьСобытиеПриВходящемЗвонке ИЛИ Запись.СоздаватьСобытиеПриИсходящемЗвонке);
	//
	Элементы.СохранятьОтклоненныйПриРазговореВходящий.Доступность = НЕ Запись.ПриниматьНесколькоВходящих;
	//
	флагПрофильНастроекБИТАТС = (Запись.ПрофильНастроек = ПредопределенноеЗначение("Перечисление.бит_ПрофилиНастроекБИТPhone.БИТ_АТС")) ИЛИ
								(Запись.ПрофильНастроек = ПредопределенноеЗначение("Перечисление.бит_ПрофилиНастроекБИТPhone.Произвольные"));
	Элементы.ГруппаDND.Доступность = флагПрофильНастроекБИТАТС;
	Элементы.ГруппаБИТАТС.Доступность = флагПрофильНастроекБИТАТС;
	Элементы.ГруппаПереадресация.Доступность = флагПрофильНастроекБИТАТС;
	//
	Элементы.СерверЛицензийНеИспользоватьПрокси.Доступность = НЕ (Запись.СерверЛицензийВерсия = 1);
	Элементы.СерверЛицензийCID.Доступность = (Запись.СерверЛицензийВерсия = 1);
	//
	Элементы.КомандаПереадресации.Доступность = Запись.ТипПереводаЗвонка = ПредопределенноеЗначение("Перечисление.бит_ТипПереводаЗвонка.Условный");
	//
	Элементы.ПроверкаСтатусаНомераПередПереводом.Доступность = ((Запись.ТипПереводаЗвонка = ПредопределенноеЗначение("Перечисление.бит_ТипПереводаЗвонка.Условный"))
															ИЛИ (Запись.ТипПереводаЗвонка = ПредопределенноеЗначение("Перечисление.бит_ТипПереводаЗвонка.Безусловный")));
	//
	Элементы.КомандаНеБеспокоить.Доступность = Запись.ИспользоватьКомандуНеБеспокоитьНаАТС;
	Элементы.КомандаОтменаНеБеспокоить.Доступность = Запись.ИспользоватьКомандуНеБеспокоитьНаАТС;
	//
	Элементы.ФайлВходящегоЗвонка.Доступность = (Запись.ТипВходящегоЗвонка = 4);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокКодеков()
	СписокКодеков.Очистить();
	массКодеков = бит_БитфонКлиент.РазобратьСписокКодеков(Запись.СписокКодеков);
	Для Каждого кодекПриор Из массКодеков Цикл
		строкаКодек = СписокКодеков.Добавить();
		ЗаполнитьЗначенияСвойств(строкаКодек, кодекПриор);
	КонецЦикла;
	СписокКодеков.Сортировать("Приоритет Убыв");
КонецПроцедуры

&НаКлиенте
Процедура ФайлВходящегоЗвонкаЗавершение(ВыбранныеФайлы, НачальныйФайл)
	Если ЗначениеЗаполнено(ВыбранныеФайлы) Тогда
		выбранФайл = ВыбранныеФайлы[0];
		Запись.ФайлВходящегоЗвонка = выбранФайл;
		Если выбранФайл <> НачальныйФайл Тогда
			Модифицированность = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
