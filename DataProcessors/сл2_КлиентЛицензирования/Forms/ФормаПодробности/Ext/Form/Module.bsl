
#Область СЕРВЕРНЫЕ_МЕТОДЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СостояниеЛицензирования = Параметры.СостояниеЛицензирования;
	                                                          
	Для Каждого ЭлементЗн Из СостояниеЛицензирования.ЛицензируемыеПродукты Цикл
		
		// Продукт
		СтрокаДетализацииПродукт = ДетализацияПродукты.Добавить();
		СтрокаДетализацииПродукт.ПрограммныйПродукт = ЭлементЗн.НаименованиеПродукта;
		
		Если ЭлементЗн.ЛицензияПолучена Тогда			
			
			СтрокаДетализацииПродукт.ИндикаторСтатуса = БиблиотекаКартинок.ОформлениеЗнакФлажок; 
			СтрокаДетализацииПродукт.Состояние        = "Лицензия получена (CId: " + ЭлементЗн.ПулЛицензий + ")";			
			
		Иначе
						
			Если ЭлементЗн.Ошибка = Неопределено Тогда			
				СтрокаДетализацииПродукт.ИндикаторСтатуса = БиблиотекаКартинок.ОформлениеКругЖелтый; 
				СтрокаДетализацииПродукт.Состояние        = "Лицензия будет получена при первой необходимости";
			Иначе 			
				СтрокаДетализацииПродукт.ИндикаторСтатуса = БиблиотекаКартинок.ОформлениеЗнакКрест;
				СтрокаДетализацииПродукт.Состояние        = сл2_Сервер.ОписаниеРезультата(ЭлементЗн.Ошибка);
			КонецЕсли;
			
		КонецЕсли;
		
		// Подписка
		Если ЭлементЗн.ПродуктИмеетПодписку И ЗначениеЗаполнено(ЭлементЗн.СвойстваПодписки) Тогда
			
			СтрокаДетализацииПодписка = ДетализацияПодписки.Добавить();
			СтрокаДетализацииПодписка.Подписка = "Технологическое сопровождение " + ЭлементЗн.НаименованиеПродукта;			
			
			Описание = "";
			Если ЭлементЗн.СвойстваПодписки.Отсутствует Тогда
				СтрокаДетализацииПодписка.ИндикаторСтатуса = БиблиотекаКартинок.ОформлениеЗнакКрест;
				Описание = "Отсутствуют данные о подписке, обратитесь к администратору или проверьте веб интерфейс сервера лицензий.";
			ИначеЕсли ЭлементЗн.СвойстваПодписки.СрокДействияИстек Тогда
				СтрокаДетализацииПодписка.ИндикаторСтатуса = БиблиотекаКартинок.ОформлениеЗнакКрест;
				Если ЗначениеЗаполнено(ЭлементЗн.СвойстваПодписки.ДатаОкончания) Тогда
					Описание = "Подписка истекла " + ЭлементЗн.СвойстваПодписки.ДатаОкончания;
					Если ЗначениеЗаполнено(ЭлементЗн.ДатаСборкиПродукта) Тогда
						Описание = Описание + Символы.ПС + "Дата выпуска текущей версии конфигурации: " + Формат(ЭлементЗн.ДатаСборкиПродукта, "ДЛФ=D");				
					КонецЕсли;				
				КонецЕсли; 
			ИначеЕсли ЭлементЗн.СвойстваПодписки.СкороИстекает Тогда
				СтрокаДетализацииПодписка.ИндикаторСтатуса = БиблиотекаКартинок.ОформлениеКругЖелтый;
				Если ЭлементЗн.СвойстваПодписки.ОсталосьДней = 0 Тогда
					Описание = "Срок действия подписки истекает сегодня!";
				Иначе
					Описание = "Срок действия подписки истекает через: " + Формат(ЭлементЗн.СвойстваПодписки.ОсталосьДней, "ЧН=0; ЧГ=0") + " дн.";
				КонецЕсли;
			Иначе
				СтрокаДетализацииПодписка.ИндикаторСтатуса = БиблиотекаКартинок.ОформлениеЗнакФлажок;
				Описание = "Подписка функционирует";
				Если ЗначениеЗаполнено(ЭлементЗн.СвойстваПодписки.ДатаОкончания) Тогда
					Описание = Описание + " до " + Формат(ЭлементЗн.СвойстваПодписки.ДатаОкончания, "ДЛФ=D");				
				КонецЕсли;
			КонецЕсли;
			
			СтрокаДетализацииПодписка.Состояние = Описание;
			
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти


