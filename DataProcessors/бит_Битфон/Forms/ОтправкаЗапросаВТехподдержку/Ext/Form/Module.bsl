
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтправитьЗапрос(Команда)
	
	стрФайлАрхива = "";
	данныеАрхива = Неопределено;
	
#Если НЕ МобильныйКлиент Тогда
	стрЗаголовокОшибки = "Ошибка при архивации лога ";
	стрПрофиль = бит_ТелефонияКлиент.ПолучитьПапкуПриложенийПользователя(стрЗаголовокОшибки);
	Если НЕ ЗначениеЗаполнено(стрПрофиль) Тогда
		// вывод сообщения об ошибке уже есть в функции получения папки
		Возврат;
	КонецЕсли;
	стрЛогин = бит_БитфонСервер.ПолучитьПараметрыСоединения().Логин;
	Если НЕ ЗначениеЗаполнено(стрЛогин) Тогда
		бит_ТелефонияКлиент.ВывестиСообщение(стрЗаголовокОшибки + "Не задан логин подключения");
		Возврат;
	КонецЕсли;
	стрПапкаБИТ = стрПрофиль + "\BIT";
	стрФайлАрхива = стрЛогин + "_sp.zip";
	стрФайлАрхиваПолныйПуть = стрПапкаБИТ + "\" + стрФайлАрхива;
	стрИмяЛогФайла = стрПапкаБИТ + "\" + стрЛогин + "_sp.log";
	// создать архив
	архивZip = Новый ЗаписьZipФайла();
	архивZip.Открыть(стрФайлАрхиваПолныйПуть);
	архивZip.Добавить(стрИмяЛогФайла);
	архивZip.Записать();
	
	данныеАрхива = Новый ДвоичныеДанные(стрФайлАрхиваПолныйПуть);
#КонецЕсли
	
	стрТемаПисьма = "Запрос техподдержки от " + НазваниеОрганизации;
	
	стрТекстПисьма = "Запрос техподдержки (письмо сформировано автоматически)" + Символы.ПС + Символы.ПС +
		"Организация: " + НазваниеОрганизации + Символы.ПС +
		"Контактное лицо: " + КонтактноеЛицо + Символы.ПС +
		"Телефон: " + Телефон + Символы.ПС +
		"Электронная почта: " + ЭлПочта + Символы.ПС + Символы.ПС +
		"Описание проблемы: " + Символы.ПС + ОписаниеПроблемы + Символы.ПС;
	
	сообщение = Новый СообщениеПользователю();
	бит_БитфонСервер.ОтправитьПочтовоеСообщениеВТехподдержку(стрТемаПисьма, стрТекстПисьма, стрФайлАрхива, данныеАрхива, сообщение);
	сообщение.Сообщить();
	
	Закрыть(КодВозвратаДиалога.ОК);
	
КонецПроцедуры

#КонецОбласти

