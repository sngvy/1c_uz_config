#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ПервоначальноеЗаполнениеИОбновлениеИнформационнойБазы

// Процедура заполняет значения ставки рефинансирования.
// Центрального Банка Российской Федерации 
//
Процедура ЗаполнитьЗначенияСтавкиРефинансированияЦБ() Экспорт
	
	КлассификаторXML = РегистрыСведений.АЭ_ЗначенияВидовПроцентныхСтавок.ПолучитьМакет("СтавкаРефинансированияЦБ").ПолучитьТекст();
	
	КлассификаторТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	
	НаборЗаписей = РегистрыСведений.АЭ_ЗначенияВидовПроцентныхСтавок.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ВидСтавки.Установить(Справочники.АЭ_ВидыСтавок.КлючеваяСтавкаЦБ);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	
	НаборЗаписей.ДополнительныеСвойства.Вставить("ЗаписьОбщихДанных");
	
	Для Каждого СтрокаКлассификатора Из КлассификаторТаблица Цикл
		СтрокаНабора = НаборЗаписей.Добавить();
		СтрокаНабора.Период 	= Дата(СтрокаКлассификатора.Date);
		СтрокаНабора.ВидСтавки 	= Справочники.АЭ_ВидыСтавок.КлючеваяСтавкаЦБ;
		СтрокаНабора.Процент	= Число(СтрокаКлассификатора.Rate);
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ЗаписьСтавок

// НоваяСтавка - ТаблицаЗначений,
//		Поля: Дата(Дата), Значение(Число)
Процедура ДобавитьНовоеЗначениеКлючевойСтавки(НоваяСтавка) Экспорт

	Запись = СоздатьМенеджерЗаписи();
	Запись.Период = НоваяСтавка.Дата;
	Запись.ВидСтавки = Справочники.АЭ_ВидыСтавок.КлючеваяСтавкаЦБ;
	Запись.Процент = НоваяСтавка.Значение;
	
	// Если произойдет ошибка записи, то надо отдельно разбираться
	// почему так вышло. Возможно ранее были некоректные данные
	Запись.Записать(Ложь);

КонецПроцедуры

Функция ПолучитьПоследниеДанныеКлючевойСтавки() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КлючеваяСтавка.ВидСтавки КАК Ставка,
		|	КлючеваяСтавка.Процент КАК ЗначениеСтавки,
		|	КлючеваяСтавка.Период КАК Период
		|ИЗ
		|	РегистрСведений.АЭ_ЗначенияВидовПроцентныхСтавок.СрезПоследних(, ВидСтавки = &Ставка) КАК КлючеваяСтавка";
	
	Запрос.УстановитьПараметр("Ставка", Справочники.АЭ_ВидыСтавок.КлючеваяСтавкаЦБ);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ДатаЗначениеСтавки = Новый Структура;
	ДатаЗначениеСтавки.Вставить("Дата", Дата(2016, 1, 1));
	ДатаЗначениеСтавки.Вставить("Значение", 8.25);
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		
		ДатаЗначениеСтавки["Дата"] = ВыборкаДетальныеЗаписи.Период;
		ДатаЗначениеСтавки["Значение"]= ВыборкаДетальныеЗаписи.ЗначениеСтавки;
		
	КонецЕсли;
	
	Возврат ДатаЗначениеСтавки;

КонецФункции // ()

#КонецОбласти