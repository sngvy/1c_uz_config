
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УправлениеФормой();
	Если Объект.Ссылка.Пустая() Или Объект.ПорядокГашения.Количество() = 0 Тогда
		Объект.ВидПроцентнойСтавки = Перечисления.ВидыПроцентнойСтавки.ФиксированнаяПроцентнаяСтавка;
		стр = Объект.ПорядокГашения.Добавить();
		стр.Гашение = ПланыВидовХарактеристик.РасчетныеХарактеристики.ПросроченныеПроценты;
		стр = Объект.ПорядокГашения.Добавить();
		стр.Гашение = ПланыВидовХарактеристик.РасчетныеХарактеристики.Проценты;
		стр = Объект.ПорядокГашения.Добавить();
		стр.Гашение = ПланыВидовХарактеристик.РасчетныеХарактеристики.ОсновнойДолг;	
		стр = Объект.ПорядокГашения.Добавить();
		стр.Гашение = ПланыВидовХарактеристик.РасчетныеХарактеристики.Пени;
		стр = Объект.ПорядокГашения.Добавить();
		стр.Гашение = ПланыВидовХарактеристик.РасчетныеХарактеристики.Штрафы;		
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()
	Элементы.ПрекращатьНачисления_Количество.Видимость				= Объект.ПрекращатьНачисления;
	Элементы.ПрекращатьНачисления_Период.Видимость					= Объект.ПрекращатьНачисления;
	Элементы.ПрекращатьНачисления_ПослеДатыВыдачиЗайма.Видимость   	= Объект.ПрекращатьНачисления;
	Элементы.ПрекращатьНачисления_ДеньПросрочки.Видимость			= Объект.ПрекращатьНачисления;
	Элементы.ПрекращатьНачисления_НаДеньПросрочки.Видимость			= Объект.ПрекращатьНачисления;
	
	Элементы.НачислятьПроценты_Количество.Видимость					= Объект.НачислятьПроценты;
	Элементы.НачислятьПроценты_ОтВыдачиЗайма.Видимость				= Объект.НачислятьПроценты;
	
	Элементы.ПорядокГашения.Видимость 								= Объект.ЗадатьПорядокГашения;
	
	Элементы.ИмяОбщегоМодуля.Видимость 								= Объект.ПользовательскаяПрограмма;
КонецПроцедуры

&НаКлиенте
Процедура ПрекращатьНачисленияПриИзменении(Элемент)
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура ПользовательскаяПрограммаПриИзменении(Элемент)
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура ЗадатьПорядокГашенияПриИзменении(Элемент)
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура НачислятьПроцентыПриИзменении(Элемент)
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура ПотребительскийКредитДоОдногоГодаПриИзменении(Элемент)
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура ПериодичностьСрокаЗаймаПриИзменении(Элемент)
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРедактированияПроцентныхСтавок(ДосрочноеРасторжение)
	
	Если ДосрочноеРасторжение = Неопределено Тогда
		ПоместитьТаблицуВоВременноеХранилище("ИнтервалыПроцентнойСтавкиПослеДатыОкончания");
	ИначеЕсли ДосрочноеРасторжение Тогда
		ПоместитьТаблицуВоВременноеХранилище("ИнтервалыПроцентнойСтавкиПриДосрочномРасторжении");
	Иначе
		ПоместитьТаблицуВоВременноеХранилище("ИнтервалыПроцентнойСтавки");
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресВременногоХранилища",						АдресВременногоХранилища);
	
	Если ДосрочноеРасторжение = Неопределено Тогда
		ПараметрыФормы.Вставить("ПроцентнаяСтавкаПериодичность"					,Объект.ПроцентнаяСтавкаПослеДатыОкончанияПериодичность);
		ПараметрыФормы.Вставить("ПроцентнаяСтавкаПериодичностьИнтервалов" 		,Объект.ПроцентнаяСтавкаПослеДатыОкончанияПериодичностьИнтервалов);
	ИначеЕсли ДосрочноеРасторжение Тогда
		ПараметрыФормы.Вставить("ПроцентнаяСтавкаПериодичность"					,Объект.ПроцентнаяСтавкаПериодичностьПриДосрочномЗакрытии);
		ПараметрыФормы.Вставить("ПроцентнаяСтавкаПериодичностьИнтервалов" 		,Объект.ПроцентнаяСтавкаПериодичностьИнтерваловПриДосрочномЗакрытии);
	Иначе
		ПараметрыФормы.Вставить("ПроцентнаяСтавкаПериодичность"					,Объект.ПериодичностьПроцентнойСтавки);
		ПараметрыФормы.Вставить("ПроцентнаяСтавкаПериодичностьИнтервалов" 		,Объект.ПериодичностьСрокаЗайма);
	КонецЕсли;
	
	ПараметрыФормы.Вставить("ТолькоПросмотр", Ложь);
	
	ДополнительныеПараметры = Новый Структура("ДосрочноеРасторжение", ДосрочноеРасторжение);
	
	ОткрытьФорму("Справочник.ПрограммыРасчетаЗайма.Форма.ФормаРедактированияИнтервалов"
	,ПараметрыФормы
	,ЭтотОбъект,,,,
	Новый ОписаниеОповещения("ОбработкаПослеЗакрытияФормыредактированияПроцентныхСтавок", ЭтотОбъект, ДополнительныеПараметры),
	РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере
Процедура ПоместитьТаблицуВоВременноеХранилище(ИмяТаблицы)
	
	АдресВременногоХранилища = ПоместитьВоВременноеХранилище(Объект[ИмяТаблицы].Выгрузить(), УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияИнтервалыПроцентнойСтавкиНажатие(Элемент)
	
	ОткрытьФормуРедактированияПроцентныхСтавок(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаПослеЗакрытияФормыредактированияПроцентныхСтавок(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	Если Результат.ЕстьИзменияВДанных Тогда 
		
		Модифицированность = Истина;
		
		ДосрочноеРасторжение = ДополнительныеПараметры.ДосрочноеРасторжение;
		
		Если ДосрочноеРасторжение = Неопределено Тогда
			Объект.ПроцентнаяСтавкаПослеДатыОкончанияПериодичность 				= Результат.ПроцентнаяСтавкаПериодичность;
			Объект.ПроцентнаяСтавкаПослеДатыОкончанияПериодичностьИнтервалов 	= Результат.ПроцентнаяСтавкаПериодичностьИнтервалов;
		ИначеЕсли ДосрочноеРасторжение Тогда
			Объект.ПроцентнаяСтавкаПериодичностьПриДосрочномЗакрытии 			= Результат.ПроцентнаяСтавкаПериодичность;
			Объект.ПроцентнаяСтавкаПериодичностьИнтерваловПриДосрочномЗакрытии 	= Результат.ПроцентнаяСтавкаПериодичностьИнтервалов;
		Иначе
			Объект.ПериодичностьПроцентнойСтавки 								= Результат.ПроцентнаяСтавкаПериодичность;
			Объект.ПериодичностьСрокаЗайма				 						= Результат.ПроцентнаяСтавкаПериодичностьИнтервалов;
		КонецЕсли;
		
		ОбновитьРеквизиты(ДосрочноеРасторжение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьРеквизиты(ДосрочноеРасторжение)
	
	ТаблицаДанных = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	Если ТаблицаДанных = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ДосрочноеРасторжение = Неопределено Тогда
		Объект.ИнтервалыПроцентнойСтавкиПослеДатыОкончания.Загрузить(ТаблицаДанных);
	ИначеЕсли ДосрочноеРасторжение Тогда 
		Объект.ИнтервалыПроцентнойСтавкиПриДосрочномРасторжении.Загрузить(ТаблицаДанных);
	Иначе 
		Объект.ИнтервалыПроцентнойСтавки.Загрузить(ТаблицаДанных);
	КонецЕсли;
	
	РасчетЗадолженностиМФО.СформироватьТекстГиперссылкиИнтервалыПроцентнойСтавки(ЭтотОбъект, ДосрочноеРасторжение);
	
КонецПроцедуры


				
				