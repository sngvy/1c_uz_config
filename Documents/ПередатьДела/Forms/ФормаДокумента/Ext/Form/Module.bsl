
&НаСервере
Процедура ПодборВтаблЧасть(Результат)
	Об = РеквизитФормыВЗначение("Объект");		
	РаботаСДокументами.ЗанестиРезультатПодбораВТабличнуюЧасть(Об.Объекты, Результат);
	ЗначениеВРеквизитФормы(Об, "Объект");
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)	
	ОбъектыСервер.ОграничитьТипОбъекта(Элементы.ОбъектыОбъект);
	
	Если Объект.Ссылка.Пустая() Тогда
		Пользователи.ЗаполнитьРеквизитыПоДаннымПользователя(Объект);
	КонецЕсли;
	
	ОбъектыСервер.РасширитьТабличнуюЧасть(Объект.Объекты);
КонецПроцедуры

&НаКлиенте
Процедура Распределить(Команда)
	РаспределитьСервер();
КонецПроцедуры

&НаСервере
Процедура РаспределитьСервер()
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ТаблицаДО.Объект,
	                      |	ТаблицаДО.Сотрудник
	                      |ПОМЕСТИТЬ ТаблицаДО
	                      |ИЗ
	                      |	&ТаблицаДО КАК ТаблицаДО
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ТаблицаДО.Объект,
	                      |	МАКСИМУМ(ТаблицаДО.Сотрудник) КАК Сотрудник
	                      |ПОМЕСТИТЬ ТаблицаДОБезПовт
	                      |ИЗ
	                      |	ТаблицаДО КАК ТаблицаДО
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ТаблицаДО.Объект
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |	ТаблицаС.Сотрудник
	                      |ПОМЕСТИТЬ ТаблицаС
	                      |ИЗ
	                      |	&ТаблицаС КАК ТаблицаС
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ТаблицаС.Сотрудник,
	                      |	ЕСТЬNULL(Таблица.Количество, 0) КАК Количество
	                      |ИЗ
	                      |	ТаблицаС КАК ТаблицаС
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	                      |			ТаблицаДОБезПовт.Сотрудник КАК Сотрудник,
	                      |			КОЛИЧЕСТВО(*) КАК Количество
	                      |		ИЗ
	                      |			ТаблицаДОБезПовт КАК ТаблицаДОБезПовт
	                      |		
	                      |		СГРУППИРОВАТЬ ПО
	                      |			ТаблицаДОБезПовт.Сотрудник) КАК Таблица
	                      |		ПО ТаблицаС.Сотрудник = Таблица.Сотрудник
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ТаблицаДОБезПовт.Объект,
	                      |	ТаблицаДОБезПовт.Сотрудник
	                      |ИЗ
	                      |	ТаблицаДОБезПовт КАК ТаблицаДОБезПовт");
	Запрос.УстановитьПараметр("ТаблицаДО", Объект.Объекты.Выгрузить());
	Запрос.УстановитьПараметр("ТаблицаС", Объект.СотрудникиПринимающие.Выгрузить());
	Результат = Запрос.ВыполнитьПакет();
	КоличествоДел.Загрузить(Результат[3].Выгрузить());
	Объект.Объекты.Загрузить(Результат[4].Выгрузить());					  
	
	Для Каждого Элемент Из Объект.Объекты Цикл
		Если Элемент.Сотрудник.Пустая() Тогда
			КоличествоДел.Сортировать("Количество");
			Элемент.Сотрудник = КоличествоДел[0].Сотрудник;
			КоличествоДел[0].Количество = КоличествоДел[0].Количество + 1;
		КонецЕсли;
	КонецЦикла;
	
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТаблицаДО.Объект,
	               |	ТаблицаДО.Сотрудник
	               |ПОМЕСТИТЬ ТаблицаДО
	               |ИЗ
	               |	&ТаблицаДО КАК ТаблицаДО
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаДО.Объект,
	               |	ОтветственныеСотрудники.ТипСотрудника,
	               |	ТаблицаДО.Сотрудник
	               |ИЗ
	               |	ТаблицаДО КАК ТаблицаДО
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОтветственныеСотрудники КАК ОтветственныеСотрудники
	               |		ПО ТаблицаДО.Объект = ОтветственныеСотрудники.Объект
	               |			И (ОтветственныеСотрудники.Пользователь = &Пользователь)
	               |			И (ОтветственныеСотрудники.ТипСотрудника В (&СписокТипыС))";
	Запрос.УстановитьПараметр("ТаблицаДО", Объект.Объекты.Выгрузить());
	Запрос.УстановитьПараметр("Пользователь", Объект.Ответственный);
	Запрос.УстановитьПараметр("СписокТипыС", Объект.ПередаваемыеТипы.Выгрузить().ВыгрузитьКолонку("ТипСотрудника"));
	Объект.Объекты.Загрузить(Запрос.Выполнить().Выгрузить());
	ОбъектыСервер.РасширитьТабличнуюЧасть(Объект.Объекты);
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)	
	Для Каждого Элемент Из Объект.Объекты Цикл
		Элемент.Сотрудник = Неопределено;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ОбъектыСервер.РасширитьТабличнуюЧасть(Объект.Объекты);
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыОбъектНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Элементы.ОбъектыОбъект.ВыбиратьТип = (Элементы.Объекты.ТекущиеДанные.Объект = Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыОбъектПриИзменении(Элемент)
	ОбъектыКлиент.РасширитьТекущуюСтроку(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыПолеПриИзменении(Элемент)
	ОбъектыКлиент.РасширитьТекущуюСтрокуОбъект(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)
	ОбъектыКлиент.Подбор(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПодборИзФайла(Команда)
	ОбъектыКлиент.ПодборИзДОК(ЭтаФорма);
КонецПроцедуры
