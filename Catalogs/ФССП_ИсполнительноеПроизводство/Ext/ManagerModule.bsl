
#Область ПрограммныйИнтерфейс

// Соответствует n..n/yy/dd/rr или n..n/yy/ddddd-ИП
//
// Параметры:
//  НомерИП  - Строка - Номер исполнительного производства
//
// Возвращаемое значение:
//   Булево   - Соответствует заданной ФССП Схеме
//
Функция Нормализован(НомерИП) Экспорт

	Если Не ЗначениеЗаполнено(НомерИП) Тогда
	
		Возврат Ложь;
	
	КонецЕсли;
	
	БезПробелов = СтрЗаменить(НомерИП, " ", "");
	БезПробелов = СокрЛП(БезПробелов);
	Если СтрСравнить(НомерИП, БезПробелов) <> 0 Тогда
	
		Возврат Ложь;
	
	КонецЕсли;
	
	Приписка = "-ИП";
	
	НомерБезИП = СтрЗаменить(НомерИП, Приписка, "");
	
	Части = СтрРазделить(НомерБезИП, "/");
	
	Длина = Части.Количество();
	Если Длина <> 3
		И Длина <> 4 Тогда
	
		Возврат Ложь;
	
	КонецЕсли;
	
	Для каждого Часть Из Части Цикл
	
		Если Не ЭтоЧисло(Часть) Тогда
		
			Возврат Ложь;
		
		КонецЕсли;
	
	КонецЦикла;
	
	Если Длина = 3
		И СтрНайти(НомерИП, Приписка) = 0 Тогда
	
		Возврат Ложь;
	
	КонецЕсли;
	
	Возврат Истина;

КонецФункции // ()

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьНеНормализованныеИП() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФССП_ИсполнительноеПроизводство.Ссылка КАК Ссылка,
		|	ФССП_ИсполнительноеПроизводство.Представление КАК Представление
		|ИЗ
		|	Справочник.ФССП_ИсполнительноеПроизводство КАК ФССП_ИсполнительноеПроизводство
		|ГДЕ
		|	ФССП_ИсполнительноеПроизводство.ПометкаУдаления = ЛОЖЬ
		|	И ДЛИНАСТРОКИ(ФССП_ИсполнительноеПроизводство.НормализованныйНомер) = 0";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();

КонецФункции // ()

Функция ПолучитьИзНабораНеНормализованныеИП(Набор) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФССП_ИсполнительноеПроизводство.Ссылка КАК Ссылка,
		|	ФССП_ИсполнительноеПроизводство.Представление КАК Представление
		|ИЗ
		|	Справочник.ФССП_ИсполнительноеПроизводство КАК ФССП_ИсполнительноеПроизводство
		|ГДЕ
		|	ФССП_ИсполнительноеПроизводство.Ссылка В(&Ссылки)
		|	И ДЛИНАСТРОКИ(ФССП_ИсполнительноеПроизводство.НормализованныйНомер) = 0";
	
	Запрос.УстановитьПараметр("Ссылки", Набор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();

КонецФункции // ()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЭтоЧисло(ЧастьНомераИП)

	ОписаниеЧисла = Новый ОписаниеТипов("Число");
	
	ЧисловоеЗначение = ОписаниеЧисла.ПривестиЗначение(ЧастьНомераИП);
	
	Возврат ЧисловоеЗначение <> 0;

КонецФункции // ()

#КонецОбласти