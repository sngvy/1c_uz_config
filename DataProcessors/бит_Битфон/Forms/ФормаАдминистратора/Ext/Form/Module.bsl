
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	структОтбор = Новый Структура("Пользователь", бит_ТелефонияСервер.ПолучитьТекущегоПользователя());
	выборка = РегистрыСведений.бит_СпискиОбзвона.Выбрать(структОтбор);
	Если выборка.Следующий() Тогда
		СписокОбзвонаТекущий = выборка.Список;
	КонецЕсли;
	ОбновитьПользователейСписка();
КонецПроцедуры

&НаКлиенте
Процедура СписокОбзвонаТекущийПриИзменении(Элемент)
	Модифицированность = Истина;
	ОбновитьПользователейСписка();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СделатьСписокТекущим(Команда)
	текущ = Элементы.СпискиОбзвона.ТекущаяСтрока;
	Если текущ <> Неопределено Тогда
		СписокОбзвонаТекущий = текущ;
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПользователей(Команда)
	ОбновитьПользователейСписка();
КонецПроцедуры

&НаКлиенте
Процедура Задать(Команда)
	Если Пользователи.Количество() > 0 Тогда
		СохранитьТекущийСписок();
		Элементы.СпискиПоПользователям.Обновить();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьТекущийСписок()
	Для Каждого строкаПользователь Из Пользователи Цикл
		ссылкаПользователь = строкаПользователь.Наименование;
		менедж = РегистрыСведений.бит_СпискиОбзвона.СоздатьМенеджерЗаписи();
		менедж.Пользователь = ссылкаПользователь;
		менедж.Прочитать();
		Если НЕ менедж.Выбран() Тогда
			менедж.Пользователь = ссылкаПользователь;
		КонецЕсли;
		менедж.Список = СписокОбзвонаТекущий;
		менедж.Записать();
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ОбновитьПользователейСписка()
	Если НЕ ЗначениеЗаполнено(СписокОбзвонаТекущий) Тогда
		Возврат;
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Пользователи.Ссылка КАК Наименование
	               |ИЗ
	               |	РегистрСведений.бит_СпискиОбзвона КАК бит_СпискиОбзвона
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	               |		ПО бит_СпискиОбзвона.Пользователь = Пользователи.Ссылка
	               |ГДЕ
	               |	бит_СпискиОбзвона.Список.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", СписокОбзвонаТекущий);
	Пользователи.Загрузить( Запрос.Выполнить().Выгрузить() );
КонецПроцедуры

#КонецОбласти
