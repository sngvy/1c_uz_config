
&НаКлиенте 
Перем ОбработчикПодключен;

///////////////////////////////////////////////////////////////
// СЕРВЕРНЫЕ МЕТОДЫ ///////////////////////////////////////////

&НаСервере
// Получает все необходимые данные для формы
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	бит_сл_сервер.ОбновитьЗначенияРеквизитов(Объект);
	Элементы.ОбщиеНастройки.Доступность = РольДоступна("бит_сл_Администратор") ИЛИ
	                                      РольДоступна("ПолныеПрава") ИЛИ
										  РольДоступна("бит_ПолныеПрава") ИЛИ
										  ПользователиИнформационнойБазы.ТекущийПользователь().Имя = "";
	
КонецПроцедуры

&НаСервере
// Обновляет все реквизиты на форме
//
Процедура ОбновитьДанныеФормы()

	бит_сл_сервер.ОбновитьЗначенияРеквизитов(Объект);
	
	ЧислоИсключений = ?(Объект.общ_СписокИсключения="", 0, СтрЧислоСтрок(Объект.общ_СписокИсключения));
	
КонецПроцедуры // ОбновитьДанныеФормы()

&НаСервере 
// Передает структуру параметров на сервер для сохранения
//
Процедура ПередатьНастройкиНаСервер()

	бит_сл_сервер.СохранитьНастройкиВОбъект(Объект);

КонецПроцедуры // ПередатьНастройкиНаСервер() 

///////////////////////////////////////////////////////////////
// КЛИЕНТСКИЕ МЕТОДЫ //////////////////////////////////////////

&НаКлиенте
// Запускается каждые 4 секунды для обновления данных формы
//
Процедура ОбработчикОбновленияДанныхФормы()

	ОбновитьДанныеФормы();
	
	Элементы.КнопкаОбновитьСоединение.Доступность = Истина;
	
	Если Элементы.ГруппаПредупреждениеАктивностиОбщихНастроек.Видимость <> Объект.общ_ПубликацияНастроек Тогда
		Элементы.ГруппаПредупреждениеАктивностиОбщихНастроек.Видимость = Объект.общ_ПубликацияНастроек;
	КонецЕсли;

	Элементы.ДекорацияФлагПубликации.ЦветТекста = ?(Объект.общ_ПубликацияНастроек, Новый Цвет(53,115,5), Новый Цвет(153,51, 0));
	
КонецПроцедуры // ОбработчикОбновленияДанныхФормы() 

&НаКлиенте 
// Отключает обработчик ожидания при редактировании значений
//
Процедура ОтключитьОбработчикОжиданияПриРедактировании()

	Если ОбработчикПодключен Тогда
		
		ОтключитьОбработчикОжидания( "ОбработчикОбновленияДанныхФормы" );
	    ОбработчикПодключен = Ложь;
		
	КонецЕсли; 	

КонецПроцедуры // ОтключитьОбработчикОжиданияПриРедактировании() 

&НаКлиенте 
// Подключает обработчик ожидания после окончания редактирования значения
//
Процедура ПодключитьОбработчикОжиданияПослеРедактирования()

	Если Не ОбработчикПодключен Тогда
		
		ОбработчикПодключен = Истина;
		ПодключитьОбработчикОжидания( "ОбработчикОбновленияДанныхФормы", 4, Ложь );
	
	КонецЕсли;

КонецПроцедуры // ПодключитьОбработчикОжиданияПослеРедактирования()

&НаКлиенте
// Выполняет первоначальную настройку формы при открытии 
//
Процедура ПриОткрытии(Отказ)
	
	ОбновитьДанныеФормы();	
	ОбработчикПодключен = Истина;
	ПодключитьОбработчикОжидания( "ОбработчикОбновленияДанныхФормы", 4, Ложь );
	Элементы.КнопкаОбновитьСоединение.Доступность = Истина;
	
КонецПроцедуры

&НаКлиенте
// Подготавливает обарботку к выгрузке
//
Процедура ПриЗакрытии()
	
	ОтключитьОбработчикОжидания( "ОбработчикОбновленияДанныхФормы" );

КонецПроцедуры

&НаКлиенте
// Открывает панель настроек
//
Процедура ОткрытьЗакладкуНастроек(Команда)
	
	Элементы.ПанельЗакладок.ТекущаяСтраница = Элементы.Настройки; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСостояние(Команда)
	
	ОбновитьСостояниеДействие();
	
	Элементы.ПанельЗакладок.ТекущаяСтраница = Элементы.ГруппаСообщенияСистемы;	
	Элементы.КнопкаОбновитьСоединение.Доступность = Ложь;
	Объект.ОсновнаяНадпись = "Пожалуйста, подождите...";
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСостояниеДействие()
	
	бит_сл_клиент.ПередОбновлениемСостояния();
	бит_сл_сервер.ОтключитьсяОтСервера();
	бит_сл_сервер.ПолучитьЛицензиюДляТекущегоПродукта();	
	бит_сл_клиент.ПриОбновленииСостояния();	
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройки(Команда)
	
	ПередатьНастройкиНаСервер();	
	
КонецПроцедуры

&НаКлиенте
Процедура АдресСервераИПортПриИзменении(Элемент)
	
	ПередатьНастройкиНаСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ВремяОжиданияДоАктивацииПриИзменении(Элемент)
	
	ПередатьНастройкиНаСервер();	
	
КонецПроцедуры

&НаКлиенте
Процедура общ_АдресСервераИПортПриИзменении(Элемент)
	
	ПередатьНастройкиНаСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура общ_ВремяОжиданияДоАктивацииПриИзменении(Элемент)
	
	ПередатьНастройкиНаСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура общ_ПубликацияНастроекПриИзменении(Элемент)
	
	ПередатьНастройкиНаСервер();
	
	Элементы.ДекорацияФлагПубликации.ЦветТекста = ?(Объект.общ_ПубликацияНастроек, Новый Цвет(53,115,5), Новый Цвет(153, 51, 0));
	
КонецПроцедуры

&НаКлиенте
Процедура АдресСервераИПортАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	ОтключитьОбработчикОжиданияПриРедактировании();
	
КонецПроцедуры

&НаКлиенте
Процедура АдресСервераИПортОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	ПодключитьОбработчикОжиданияПослеРедактирования();
	
КонецПроцедуры

&НаКлиенте
Процедура ПанельЗакладокПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если (ТекущаяСтраница = Элементы.ОбщиеНастройки) Тогда
	
		ЧислоИсключений = ?(Объект.общ_СписокИсключения="", 0, СтрЧислоСтрок(Объект.общ_СписокИсключения));
	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуСписка(Команда)
	
	// Откроем форму списка пользователей
	Форма = ?(бит_сл_клиент.ИспользоватьФормы13Платформы(), 
	          ПолучитьФорму("Обработка.бит_сл_КлиентЛицензирования.Форма.СписокПользователей_упр_13",,ЭтаФорма),
	          ПолучитьФорму("Обработка.бит_сл_КлиентЛицензирования.Форма.СписокПользователей_упр",,ЭтаФорма));  
	Результат = Форма.ОткрытьМодально();
	Если (ТипЗнч(Результат) = Тип("Булево")) И (Результат = Истина) Тогда
		
		// Сохраним настройки
		ПередатьНастройкиНаСервер();
		ЧислоИсключений = ?(Объект.общ_СписокИсключения="", 0, СтрЧислоСтрок(Объект.общ_СписокИсключения));
		
	КонецЕсли;
		
КонецПроцедуры

