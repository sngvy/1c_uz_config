
Процедура ИнициализироватьДанныеДокумента(ЭтотОбъект, ТаблицаДвиженийОбъектыВРаботе, ТаблицаДвиженийСобытия) Экспорт
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("Период",	ЭтотОбъект.Ссылка.МоментВремени());
	Запрос.УстановитьПараметр("Дата",	ЭтотОбъект.Дата);
	Запрос.УстановитьПараметр("Автор",	ЭтотОбъект.Автор);
	Запрос.УстановитьПараметр("Организация", ЭтотОбъект.Организация);
		
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫБОР ТИПЗНАЧЕНИЯ(ПринятиеВРаботуОрганизацииОбъекты.Объект)
	               |		КОГДА ТИП(Справочник.УслугиПоДоговору)
	               |			ТОГДА ВЫРАЗИТЬ(ПринятиеВРаботуОрганизацииОбъекты.Объект КАК Справочник.УслугиПоДоговору).Владелец
	               |		ИНАЧЕ ПринятиеВРаботуОрганизацииОбъекты.Объект
	               |	КОНЕЦ КАК Объект,
	               |	ПринятиеВРаботуОрганизацииОбъекты.Ссылка КАК Регистратор,
	               |	ЕСТЬNULL(ОбъектыВРаботеОстатки.КоличествоОстаток, 0) КАК ОстатокВРаботе
	               |ПОМЕСТИТЬ Таблица
	               |ИЗ
	               |	Документ.ПринятиеВРаботуОрганизации.Объекты КАК ПринятиеВРаботуОрганизацииОбъекты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОбъектыВРаботе.Остатки(
	               |				&Период,
	               |				Организация = &Организация
	               |					И Подразделение = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	               |					И Сотрудник = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)) КАК ОбъектыВРаботеОстатки
	               |		ПО (ПринятиеВРаботуОрганизацииОбъекты.Объект = ОбъектыВРаботеОстатки.Объект
	               |				ИЛИ ВЫРАЗИТЬ(ПринятиеВРаботуОрганизацииОбъекты.Объект КАК Справочник.УслугиПоДоговору).Владелец = ОбъектыВРаботеОстатки.Объект)
	               |ГДЕ
	               |	ПринятиеВРаботуОрганизацииОбъекты.Ссылка = &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВЫБОР ТИПЗНАЧЕНИЯ(ПринятиеВРаботуОрганизацииОбъекты.Объект)
	               |		КОГДА ТИП(Справочник.УслугиПоДоговору)
	               |			ТОГДА ВЫРАЗИТЬ(ПринятиеВРаботуОрганизацииОбъекты.Объект КАК Справочник.УслугиПоДоговору).Владелец
	               |		ИНАЧЕ ПринятиеВРаботуОрганизацииОбъекты.Объект
	               |	КОНЕЦ,
	               |	ПринятиеВРаботуОрганизацииОбъекты.Ссылка,
	               |	ЕСТЬNULL(ОбъектыВРаботеОстатки.КоличествоОстаток, 0)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	&Дата КАК Период,
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	               |	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК Подразделение,
	               |	Таблица.Объект КАК Объект,
	               |	1 КАК Количество,
	               |	Таблица.ОстатокВРаботе КАК ОстатокВРаботе,
	               |	&Организация КАК Организация,
	               |	NULL КАК Сотрудник
	               |ИЗ
	               |	Таблица КАК Таблица
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ОстатокВРаботе УБЫВ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	&Дата КАК Период,
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	               |	ЗНАЧЕНИЕ(Перечисление.ВидыСобытий.ПринятоВРаботуОрганизации) КАК ВидСобытия,
	               |	Таблица.Регистратор КАК ИсточникСобытия,
	               |	Таблица.Объект КАК Объект,
	               |	NULL КАК Подразделение,
	               |	NULL КАК Сотрудник,
	               |	&Автор КАК Автор
	               |ИЗ
	               |	Таблица КАК Таблица
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ Таблица";
				   				   
	Результат = Запрос.ВыполнитьПакет();              
	ТаблицаДвиженийОбъектыВРаботе = Результат[1].Выгрузить();
	ТаблицаДвиженийСобытия = Результат[2].Выгрузить();
КонецПроцедуры
