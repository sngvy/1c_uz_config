// См. ЭлектроннаяПодписьПереопределяемый.ПередНачаломРедактированияСертификатаКлюча
Процедура ПередНачаломРедактированияСертификатаКлюча(Ссылка, Сертификат, ПараметрыРеквизитов) Экспорт
	
	// Локализация
	Свойства = ЭлектроннаяПодпись.СвойстваСубъектаСертификата(Сертификат);
	Если Не ЗначениеЗаполнено(Свойства.Организация)
		Или Не ЗначениеЗаполнено(Свойства.ИНН) И Не ЗначениеЗаполнено(Свойства.ИННЮЛ) Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитОрганизация = ПараметрыРеквизитов.Добавить();
	РеквизитОрганизация.ИмяРеквизита = "Организация";
	РеквизитОрганизация.Видимость = Истина;
	РеквизитОрганизация.ПроверкаЗаполнения = Истина;
	Если ЗначениеЗаполнено(Свойства.ИННЮЛ) Тогда
		ИНН = Свойства.ИННЮЛ;
	Иначе
		ИНН = Свойства.ИНН;
	КонецЕсли;
	
	Если СтрНачинаетсяС(ИНН, "00") Тогда
		ИНН = Сред(ИНН, 3);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИНН", ИНН);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	_ДемоОрганизации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник._ДемоОрганизации КАК _ДемоОрганизации
	|ГДЕ
	|	_ДемоОрганизации.ИНН = &ИНН";
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	Если Выборка.Следующий() Тогда
		РеквизитОрганизация.ЗначениеЗаполнения = Выборка.Ссылка;
	КонецЕсли;
	// Конец Локализация
	
КонецПроцедуры

// См. ЭлектроннаяПодписьПереопределяемый.ПриСозданииФормыПроверкаСертификата
Процедура ПриСозданииФормыПроверкаСертификата(Сертификат, ДополнительныеПроверки, ПараметрыДополнительныхПроверок,
	СтандартныеПроверки, ВводитьПароль) Экспорт
	
	НоваяПроверка = ДополнительныеПроверки.Добавить();
	НоваяПроверка.Имя = "ТестСвязиСОператором";
	НоваяПроверка.Представление = НСтр("ru = 'Дополнительная проверка сертификата (пример)'");
	НоваяПроверка.Подсказка     = НСтр("ru = 'Подсказка к дополнительной проверке сертификата (пример)'");
	
КонецПроцедуры

// См. ЭлектроннаяПодписьПереопределяемый.ПриДополнительнойПроверкеСертификата.
Процедура ПриДополнительнойПроверкеСертификата(Параметры) Экспорт
	
	Если Параметры.Проверка = "ТестСвязиСОператором" Тогда
		Параметры.ОписаниеОшибки = "";
	КонецЕсли;
	
КонецПроцедуры

#Область ЗаполнениеРеквизитовСертификата

// См. ЗаявлениеНаСертификатПереопределяемый.ПриЗаполненииРеквизитовОрганизацииВЗаявленииНаСертификат.
Процедура ПриЗаполненииРеквизитовОрганизацииВЗаявленииНаСертификат(Параметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Параметры.Организация) Тогда
		ОрганизацияПоУмолчанию = Справочники._ДемоОрганизации.ОрганизацияПоУмолчанию();
		Если ЗначениеЗаполнено(ОрганизацияПоУмолчанию) Тогда
			Параметры.Организация = ОрганизацияПоУмолчанию;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры.Организация) Тогда
		Возврат; // Реквизиты организации нельзя заполнить, если организация не выбрана.
	КонецЕсли;
	
	Если ТипЗнч(Параметры.Организация) <> Тип("СправочникСсылка._ДемоОрганизации") Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Организация,
		"НаименованиеСокращенное, НаименованиеПолное,
		|ИндивидуальныйПредприниматель, ИНН, КПП, ОГРН,
		|РасчетныйСчет,БИК,КорреспондентскийСчет");
	
	Параметры.НаименованиеСокращенное = Реквизиты.НаименованиеСокращенное;
	Параметры.НаименованиеПолное      = Реквизиты.НаименованиеПолное;
	Параметры.ЭтоИндивидуальныйПредприниматель = ЗначениеЗаполнено(Реквизиты.ИндивидуальныйПредприниматель);
	Параметры.ИНН  = Реквизиты.ИНН;
	Параметры.КПП  = Реквизиты.КПП;
	Параметры.ОГРН = Реквизиты.ОГРН;
	Параметры.РасчетныйСчет         = Реквизиты.РасчетныйСчет;
	Параметры.БИК                   = Реквизиты.БИК;
	Параметры.КорреспондентскийСчет = Реквизиты.КорреспондентскийСчет;
	
	Объекты = Новый Массив;
	Объекты.Добавить(Параметры.Организация);
	
	ВидыКИ = Новый Массив;
	ВидыКИ.Добавить(УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени("_ДемоЮридическийАдресОрганизации"));
	ВидыКИ.Добавить(УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени("_ДемоТелефонОрганизации"));
	
	КонтактнаяИнформация = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(Объекты, , ВидыКИ, ТекущаяДатаСеанса());
	
	Строка = КонтактнаяИнформация.Найти(УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени("_ДемоЮридическийАдресОрганизации"), "Вид");
	Если Строка <> Неопределено Тогда
		Параметры.ЮридическийАдрес = Строка.Значение;
	КонецЕсли;
	
	Строка = КонтактнаяИнформация.Найти(УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени("_ДемоТелефонОрганизации"), "Вид");
	Если Строка <> Неопределено Тогда
		Параметры.Телефон = Строка.Значение;
	КонецЕсли;
	
КонецПроцедуры

// См. ЗаявлениеНаСертификатПереопределяемый.ПриЗаполненииРеквизитовВладельцаВЗаявленииНаСертификат.
Процедура ПриЗаполненииРеквизитовВладельцаВЗаявленииНаСертификат(Параметры) Экспорт
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникСсылка._ДемоФизическиеЛица"));
	Параметры.ТипВладельца = Новый ОписаниеТипов(МассивТипов);
	
	Если ЗначениеЗаполнено(Параметры.Организация)
	   И ТипЗнч(Параметры.Организация) = Тип("СправочникСсылка._ДемоОрганизации") Тогда
		
		// Владельца сертификата и варианты его выбора можно заполнить, когда организация выбрана.
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Организация,
			"Директор, ГлавныйБухгалтер, ИндивидуальныйПредприниматель");
		
		Если ЗначениеЗаполнено(Реквизиты.ИндивидуальныйПредприниматель) Тогда
			Если Не ЗначениеЗаполнено(Параметры.Сотрудник) Тогда
				Параметры.Сотрудник = Реквизиты.ИндивидуальныйПредприниматель;
			КонецЕсли;
		Иначе
			Параметры.Директор         = Реквизиты.Директор;
			Параметры.ГлавныйБухгалтер = Реквизиты.ГлавныйБухгалтер;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Параметры.Сотрудник) Тогда
			// Начальное значение для владельца сертификата.
			Если ЗначениеЗаполнено(Параметры.Директор) Тогда
				Параметры.Сотрудник = Параметры.Директор;
				
			ИначеЕсли ЗначениеЗаполнено(Параметры.ГлавныйБухгалтер) Тогда
				Параметры.Сотрудник = Параметры.ГлавныйБухгалтер;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры.Сотрудник) Тогда
		Возврат; // Реквизиты сотрудника нельзя заполнить, если сотрудник не выбран.
	КонецЕсли;
	
	Если Параметры.Сотрудник = Параметры.Директор Тогда
		Параметры.Должность = НСтр("ru = 'Генеральный директор'");
		
	ИначеЕсли Параметры.Сотрудник = Параметры.ГлавныйБухгалтер Тогда
		Параметры.Должность = НСтр("ru = 'Главный бухгалтер'");
	Иначе
		Параметры.Должность = НСтр("ru = 'Менеджер'");
	КонецЕсли;
	
	Если ТипЗнч(Параметры.Сотрудник) = Тип("СправочникСсылка._ДемоФизическиеЛица") Тогда
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Сотрудник,
			"ЭтоГруппа, Наименование, ДатаРождения, Пол, МестоРождения, Гражданство, СНИЛС, ИНН, СерияДокумента,
			|НомерДокумента, КемВыданДокумент, КодПодразделенияДокумента, ДатаВыдачиДокумента");
		Если Реквизиты.ЭтоГруппа Тогда
			Возврат;
		КонецЕсли;
	Иначе
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Сотрудник, "Наименование");
	КонецЕсли;
	
	Массив = СтрРазделить(Реквизиты.Наименование, " ", Ложь);
	Если Массив.Количество() > 0 Тогда
		Параметры.Фамилия = Массив[0];
	КонецЕсли;
	Если Массив.Количество() > 1 Тогда
		Параметры.Имя = Массив[1];
	КонецЕсли;
	Если Массив.Количество() > 2 Тогда
		Параметры.Отчество = Массив[2];
	КонецЕсли;
	
	Если ТипЗнч(Параметры.Сотрудник) <> Тип("СправочникСсылка._ДемоФизическиеЛица") Тогда
		Возврат;
	КонецЕсли;
	
	Если Реквизиты.Пол = Перечисления._ДемоПолФизическогоЛица.Мужской Тогда
		Параметры.Пол = "Мужской";
		
	ИначеЕсли Реквизиты.Пол = Перечисления._ДемоПолФизическогоЛица.Женский Тогда
		Параметры.Пол = "Женский";
	КонецЕсли;
	
	Параметры.ДатаРождения             = Реквизиты.ДатаРождения;
	Параметры.МестоРождения            = Реквизиты.МестоРождения;
	Параметры.Гражданство              = Реквизиты.Гражданство;
	Параметры.СтраховойНомерПФР        = Реквизиты.СНИЛС;
	Параметры.ДокументВид              = "21";
	Параметры.ДокументНомер            = Реквизиты.СерияДокумента + Реквизиты.НомерДокумента;
	Параметры.ДокументКемВыдан         = Реквизиты.КемВыданДокумент;
	Параметры.ДокументКодПодразделения = Реквизиты.КодПодразделенияДокумента;
	Параметры.ДокументДатаВыдачи       = Реквизиты.ДатаВыдачиДокумента;
	
	Объекты = Новый Массив;
	Объекты.Добавить(Параметры.Сотрудник);
	
	ВидыКИ = Новый Массив;
	ВидыКИ.Добавить(УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени("_ДемоEmailФизическогоЛица"));
	
	КонтактнаяИнформация = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(Объекты, , ВидыКИ);
	
	Строка = КонтактнаяИнформация.Найти(УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени("_ДемоEmailФизическогоЛица"), "Вид");
	Если Строка <> Неопределено Тогда
		Параметры.ЭлектроннаяПочта = Строка.Значение;
	КонецЕсли;
	
	Параметры.ИНН = Реквизиты.ИНН;
	
	Если Не Параметры.ЭтоФизическоеЛицо Тогда
		Возврат;
	КонецЕсли;
	
	ВидыКИ = Новый Массив;
	ВидыКИ.Добавить(УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени("_ДемоАдресФизическогоЛица"));
	ВидыКИ.Добавить(УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени("_ДемоТелефонФизическогоЛица"));
	
	КонтактнаяИнформация = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(Объекты, , ВидыКИ);
	
	Строка = КонтактнаяИнформация.Найти(УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени("_ДемоАдресФизическогоЛица"), "Вид");
	Если Строка <> Неопределено Тогда
		Параметры.АдресРегистрации = Строка.Значение;
	КонецЕсли;
	
	Строка = КонтактнаяИнформация.Найти(УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени("_ДемоТелефонФизическогоЛица"), "Вид");
	Если Строка <> Неопределено Тогда
		Параметры.Телефон = Строка.Значение;
	КонецЕсли;
	
КонецПроцедуры

// См. ЗаявлениеНаСертификатПереопределяемый.ПриЗаполненииРеквизитовРуководителяВЗаявленииНаСертификат.
Процедура ПриЗаполненииРеквизитовРуководителяВЗаявленииНаСертификат(Параметры) Экспорт
	
	Параметры.ТипРуководителя = Новый ОписаниеТипов("СправочникСсылка._ДемоФизическиеЛица");
	
	Если ЗначениеЗаполнено(Параметры.Организация)
	   И ТипЗнч(Параметры.Организация) = Тип("СправочникСсылка._ДемоОрганизации") Тогда
		// Руководителя можно заполнить, когда организация выбрана.
		Директор = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Организация, "Директор");
		Если Не ЗначениеЗаполнено(Параметры.Руководитель) Тогда
			Параметры.Руководитель = Директор;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры.Руководитель) Тогда
		Возврат; // Реквизиты руководителя нельзя заполнить, если руководитель не выбран.
	КонецЕсли;
	
	Если Параметры.Руководитель = Директор Тогда
		Параметры.Должность = НСтр("ru = 'Генеральный директор'");
		Параметры.Основание = НСтр("ru = 'Устав'");
	КонецЕсли;
	
КонецПроцедуры

// См. ЗаявлениеНаСертификатПереопределяемый.ПриЗаполненииРеквизитовПартнераВЗаявленииНаСертификат.
Процедура ПриЗаполненииРеквизитовПартнераВЗаявленииНаСертификат(Параметры) Экспорт
	
	Параметры.ТипПартнера = Новый ОписаниеТипов("СправочникСсылка._ДемоКонтрагенты");
	
	Если Параметры.ЭтоФизическоеЛицо Тогда
		Если Не ЗначениеЗаполнено(Параметры.Партнер) Тогда
			Параметры.Партнер = Константы._ДемоОсновнойПартнерВЗаявленииНаСертификат.Получить();
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(Параметры.Организация)
	        И ТипЗнч(Параметры.Организация) = Тип("СправочникСсылка._ДемоОрганизации") Тогда
		// Партнера можно заполнить, когда организация выбрана.
		Если Не ЗначениеЗаполнено(Параметры.Партнер) Тогда
			Параметры.Партнер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Организация,
				"ИнформационноеОбслуживание");
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры.Партнер) Тогда
		Возврат; // Реквизиты партнера нельзя заполнить, если партнер не выбран.
	КонецЕсли;
	
	Если ТипЗнч(Параметры.Партнер) <> Тип("СправочникСсылка._ДемоКонтрагенты") Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Партнер, "ИНН, КПП, ВидКонтрагента");
	
	Параметры.ЭтоИндивидуальныйПредприниматель =
		Реквизиты.ВидКонтрагента <> Перечисления._ДемоЮридическоеФизическоеЛицо.ЮридическоеЛицо;
	
	Параметры.ИНН = Реквизиты.ИНН;
	Параметры.КПП = Реквизиты.КПП;
	
КонецПроцедуры


#КонецОбласти