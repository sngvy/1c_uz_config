
Процедура ОбработатьRejectФайл() Экспорт   
	Если ЭтотОбъект.СтруктураВыгрузки.Количество() = 0 Тогда
		Сообщить("Ранее для корректировки не формировалась структура выгрузки, обработка не возможна!");
		Возврат;
	КонецЕсли;
	
		
	Если ТипЗнч(НастройкаВыгрузки) = Тип("СправочникСсылка.НастройкиВыгрузкиНБКИ_RUTDF") Тогда
		ОбработатьRejectФайлRUTDF();
	Иначе 
		ОбработатьRejectФайлXML();
	КонецЕсли;	 

	 
КонецПроцедуры   

Процедура ОбработатьRejectФайлRUTDF() 	
	ТипФайлаОтказа = ОпределитьПолныйЧастичныйОтказ(ТекстФайлаОтказа);
	Если ТипФайлаОтказа = Перечисления.НБКИ_ТипыОтказов.Полный Тогда
		ПроставитьДокументуНеПринято();
	ИначеЕсли ТипФайлаОтказа =  Перечисления.НБКИ_ТипыОтказов.Частичный Тогда	
		ПроставитьЗаписямНепринято(ТекстФайлаОтказа); 
	Иначе
		Сообщить("Ошибка чтения файла-отказа!");
	КонецЕсли;
КонецПроцедуры 


Функция ОпределитьПолныйЧастичныйОтказ(RejectФайл) Экспорт  
	МассивСтрок = СтрРазделить(Строка(RejectФайл), Символы.ПС, Ложь);
	Для Каждого стр Из МассивСтрок Цикл  
		ТипОтказа =  Перечисления.НБКИ_ТипыОтказов.Частичный; 
		Если СтрНайти(стр, "ERROR	HEADER") > 0 ИЛИ СтрНайти(стр, "ERROR	0_GROUPHEADER	M") > 0 
				ИЛИ СтрНайти(стр, "ERROR	0_GROUPHEADER	NUM") > 0 ИЛИ СтрНайти(стр, "ERROR	TRAILER") > 0 Тогда 
			ТипОтказа = Перечисления.НБКИ_ТипыОтказов.Полный;
			Прервать;
		КонецЕсли; 
	КонецЦикла;     
	
	ЗаполнитьДатуИмяОтказа(МассивСтрок[0]);
	
	Возврат ТипОтказа;
КонецФункции  

Процедура ПроставитьДокументуНеПринято() Экспорт
	Для Каждого Стр Из ЭтотОбъект.СтруктураВыгрузки Цикл
		Стр.Принято = Ложь;
	КонецЦикла;	
КонецПроцедуры 

Процедура ПроставитьЗаписямНепринято(RejectФайл) Экспорт  
	Для Каждого Заголовок Из ЭтотОбъект.СтруктураВыгрузки Цикл
		Заголовок.Принято = Истина;
	КонецЦикла; 
	
	МассивСтрок = СтрРазделить(Строка(RejectФайл), Символы.ПС, Ложь);
	Для Каждого стр Из МассивСтрок Цикл 
		Если СтрНайти(стр, "0_GROUPHEADER") > 0 Тогда
			Для Каждого Заголовок Из ЭтотОбъект.СтруктураВыгрузки Цикл
				Если стр = Заголовок.ЗаголовокGroupHeader Тогда
					Заголовок.Принято = Ложь; 
					Сообщить("Не принято: " + Строка(Заголовок.ОбъектУчета) + " " + Строка(Заголовок.ЗаголовокGroupHeader));
				КонецЕсли;	
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры 
 	

Функция СформироватьФайлыКорректировкиRUTDF(ТекстСообщения) Экспорт  
	
	Если ЭтотОбъект.Ссылка.Пустая() Тогда
		ДокОтчетСсылка = Документы.НБКИДокументКорректировкаRUTDF.ПустаяСсылка(); 
		ЭтотОбъект.УстановитьСсылкуНового(ДокОтчетСсылка);   
		ЭтотОбъект.УстановитьНовыйНомер();
		ЭтотОбъект.Записать();
	КонецЕсли;	    
	
	СсылкаНаДокумент = ЭтотОбъект.Ссылка;

	ДатаОтчета = Дата; 
	
	ТекстовыйДок = Новый ТекстовыйДокумент;
	СтрокаЗаголовок = КредитныеИстории.СформироватьТекстЗаголовкаHEADER(НастройкаВыгрузки, ИмяФайла, ДатаОтчета, НепринятыйДокумент);
	ТекстовыйДок.ДобавитьСтроку(СтрокаЗаголовок);
	
	КоличествоОбъектовУчета = 0;
	КоличествоГруппБлоков = 0; 
		
	МассивТитульныеЗаписи = КредитныеИстории.СформироватьТитульныеЗаписиПоДолжникам(тчКорректировка.ВыгрузитьКолонку("Должник"));
	МассивПоручители = КредитныеИстории.СформироватьТитульныеЗаписиПоПоручителям(тчКорректировка.ВыгрузитьКолонку("ОбъектУчета"));
	МассивНаследники = КредитныеИстории.СформироватьТитульныеЗаписиПоНаследникам(тчКорректировка.ВыгрузитьКолонку("ОбъектУчета"));
	
	ПараметрыТитульники = новый Структура;
	ПараметрыТитульники.Вставить("ТитульныеЗаписиДолжника", МассивТитульныеЗаписи);
	ПараметрыТитульники.Вставить("ТитульныеЗаписиПоручителя", МассивПоручители);
	ПараметрыТитульники.Вставить("ТитульныеЗаписиНаследника", МассивНаследники);
	
	ОснованиеДляКорректировки = тчКорректировка.Выгрузить(); 
	
	КредитныеИсторииИзменения.ДобавитьСобытияДляПрисвоенныхУникальныхЗначений(ОснованиеДляКорректировки);

    ТаблицаСтрок = КредитныеИсторииИзменения.СформироватьТелоОтчетаКорректировка(КоличествоОбъектовУчета, КоличествоГруппБлоков, НастройкаВыгрузки, ОснованиеДляКорректировки, ПараметрыТитульники, ДатаОтчета, СсылкаНаДокумент); 
	
	Для каждого стр из ТаблицаСтрок Цикл 
		ТекстовыйДок.ДобавитьСтроку(стр.Строка);	
	КонецЦикла;	
	КоличествоГруппБлоков = КоличествоГруппБлоков - 1; 
		
	ТекстСообщения = ТекстСообщения + " Скорректированных записей: " + Строка(КоличествоГруппБлоков) + Символы.ПС; 
	
	СтрокаПодвал = "TRAILER" + Символы.Таб + СтрЗаменить(Строка(КоличествоОбъектовУчета), Символы.НПП, "") + Символы.Таб + СтрЗаменить(Строка(КоличествоГруппБлоков), Символы.НПП, "");
	ТекстовыйДок.ДобавитьСтроку(СтрокаПодвал);    
	
		
	Возврат ТекстовыйДок;

КонецФункции  


Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.НБКИДокументКорректировкаRUTDF") Тогда
		// Заполнение шапки
		НастройкаВыгрузки = ДанныеЗаполнения.НастройкаВыгрузки;
		Принято = Перечисления.СтатусыОтчетаНБКИ.ВОбработке;
		НепринятыйДокумент = ДанныеЗаполнения.Ссылка;
		ЭтоЕдиныйФормат = ТипЗнч(НастройкаВыгрузки) = Тип("СправочникСсылка.НастройкиВыгрузкиКредитныхИсторий");
		ИмяПользователя = ?(ЭтоЕдиныйФормат, НастройкаВыгрузки.ИдентификаторИсточника, НастройкаВыгрузки.ИмяПользователяНБКИ);
		ИмяФайла = ЭтотОбъект.СформироватьИмяФайлаПоБКИ();
		ЭтоКорректировкаРанееОтправленогоДокумента = Истина;
		Для Каждого ТекСтрокаОбъекты Из ДанныеЗаполнения.Объекты Цикл
			НоваяСтрока = Объекты.Добавить();
			НоваяСтрока.Объект = ТекСтрокаОбъекты.Объект;
		КонецЦикла;
		Для Каждого текСтрокаКорректировка Из ДанныеЗаполнения.тчКорректировка Цикл
			стрНепринятое = ДанныеЗаполнения.СтруктураВыгрузки.Найти(текСтрокаКорректировка.ОбъектУчета);
			Если стрНепринятое <> Неопределено Тогда
				Если стрНепринятое.Принято Тогда
					Продолжить;
				КонецЕсли;	
			КонецЕсли;	
			НоваяСтрока = тчКорректировка.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, текСтрокаКорректировка); 	
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Процедура ЗаполнитьДатуИмяОтказа(СтрокаЗаголовокОтказа) Экспорт 
	
	МассивСловЗаголовка = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаЗаголовокОтказа, Символы.Таб, Ложь, Ложь);
	
	Если МассивСловЗаголовка.Количество() > 0 Тогда
		ДатаФайлаОтказа = МассивСловЗаголовка[4];
		ИмяФайлаОтказа =  МассивСловЗаголовка[3];
	КонецЕсли;
КонецПроцедуры 

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Если ЭтотОбъект.Принято = Перечисления.СтатусыОтчетаНБКИ.Принято Тогда 
		Для Каждого Заголовок Из ЭтотОбъект.СтруктураВыгрузки Цикл 
			Заголовок.Принято = Истина;
		КонецЦикла;	
	КонецЕсли;	
	
КонецПроцедуры

Функция СформироватьФайлКорректировкиXML(ТекстСообщения) Экспорт  
	
	Если ЭтотОбъект.Ссылка.Пустая() Тогда
		ДокОтчетСсылка = Документы.НБКИДокументКорректировкаRUTDF.ПустаяСсылка(); 
		ЭтотОбъект.УстановитьСсылкуНового(ДокОтчетСсылка);   
		ЭтотОбъект.УстановитьНовыйНомер();
		ЭтотОбъект.Записать();
	КонецЕсли;	    
	
	СсылкаНаДокумент = ЭтотОбъект.Ссылка;

	ДатаОтчета = Дата;  
	
	ДокументXML = Новый ТекстовыйДокумент; 
		
	КоличествоОбъектовУчета = 0;
	КоличествоГруппБлоков = 0; 
		
	МассивТитульныеЗаписи = КредитныеИстории.СформироватьТитульныеЗаписиПоДолжникам(тчКорректировка.ВыгрузитьКолонку("Должник"));
	МассивПоручители = КредитныеИстории.СформироватьТитульныеЗаписиПоПоручителям(тчКорректировка.ВыгрузитьКолонку("ОбъектУчета"));
	МассивНаследники = КредитныеИстории.СформироватьТитульныеЗаписиПоНаследникам(тчКорректировка.ВыгрузитьКолонку("ОбъектУчета"));
	
	ПараметрыТитульники = новый Структура;
	ПараметрыТитульники.Вставить("ТитульныеЗаписиДолжника", МассивТитульныеЗаписи);
	ПараметрыТитульники.Вставить("ТитульныеЗаписиПоручителя", МассивПоручители);
	ПараметрыТитульники.Вставить("ТитульныеЗаписиНаследника", МассивНаследники);
	
	ОснованиеДляКорректировки = тчКорректировка.Выгрузить(); 
	
	КредитныеИсторииИзменения.ДобавитьСобытияДляПрисвоенныхУникальныхЗначений(ОснованиеДляКорректировки);

    КредитныеИсторииXml.СформироватьXMLКорректировок(ДокументXML, КоличествоОбъектовУчета, КоличествоГруппБлоков, НастройкаВыгрузки, ОснованиеДляКорректировки, ПараметрыТитульники, ДатаОтчета, СсылкаНаДокумент);  
		
	ТекстСообщения = ТекстСообщения + " Скорректированных записей: " + Строка(КоличествоГруппБлоков) + Символы.ПС; 
		
	Возврат ДокументXML;

КонецФункции  

&НаСервере
Функция СформироватьИмяФайлаПоБКИ()Экспорт
	
	Если ЗначениеЗаполнено(НастройкаВыгрузки) Тогда
				
		ЭтоЕдиныйФормат = ТипЗнч(НастройкаВыгрузки) = Тип("СправочникСсылка.НастройкиВыгрузкиКредитныхИсторий");
		
		ИмяПользователя = ?(ЭтоЕдиныйФормат, НастройкаВыгрузки.ИдентификаторИсточника, НастройкаВыгрузки.ИмяПользователяНБКИ);  
		
		ДатаОтчета = ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса());
		
		Если Не ЭтоЕдиныйформат Тогда
			сИмяФайла = Строка(ИмяПользователя) + "_" + КредитныеИстории.СформироватьСтрокуДаты(ДатаОтчета);
		Иначе
			Если НастройкаВыгрузки.ВерсияФормата.БКИ = Перечисления.КредитныеИстории_БКИ.НБКИ Тогда
				сИмяФайла = Строка(ИмяПользователя) + "_" + КредитныеИстории.СформироватьСтрокуДаты(ДатаОтчета);
			ИначеЕсли НастройкаВыгрузки.ВерсияФормата.БКИ = Перечисления.КредитныеИстории_БКИ.ОБКИ Тогда
				сИмяФайла = "CHP_" + Строка(ИмяПользователя) + "_UCH_" + СформироватьИмяФайлаОБКИ(ДатаОтчета);
			ИначеЕсли НастройкаВыгрузки.ВерсияФормата.БКИ = Перечисления.КредитныеИстории_БКИ.СкорингБюро Тогда
				сИмяФайла = Строка(ИмяПользователя) + "_FCH_" + КредитныеИстории.СформироватьСтрокуДаты(ДатаОтчета); 
			Иначе  
				сИмяФайла = "НЕ УДАЛОСЬ ОПРЕДЕЛИТЬ БКИ ИЗ НАСТРОЕК!";
			КонецЕсли; 
		КонецЕсли;  
	Иначе   
		сИмяФайла = "НЕ УДАЛОСЬ ОПРЕДЕЛИТЬ БКИ ИЗ НАСТРОЕК!";
	КонецЕсли;
	
	Возврат сИмяФайла;
	
КонецФункции  

&НаСервере
Функция СформироватьИмяФайлаОБКИ(ДатаОтчета) Экспорт 
	
	ВерсияОБКИ = Формат(НастройкаВыгрузки.ВерсияФормата.НомерВерсии, "ЧЦ=4; ЧДЦ=2; ЧРД=-; ЧН=Ноль; ЧВН=");
		
	ДатаСтрока = Формат(ДатаОтчета, "ДФ=ггггММддЧЧммсс");  
	ДатаСтрока = ДатаСтрока + "000";
		
	Возврат ВерсияОБКИ + "_" + ДатаСтрока;
	
КонецФункции 

Процедура ОбработатьRejectФайлXML() 
	
	ТипФайлаОтказа = ОпределитьПолныйЧастичныйОтказXML(ТекстФайлаОтказа);
	Если ТипФайлаОтказа = Перечисления.НБКИ_ТипыОтказов.Полный Тогда
		ПроставитьДокументуНеПринято();
	ИначеЕсли ТипФайлаОтказа = Перечисления.НБКИ_ТипыОтказов.Частичный Тогда
		ПроставитьЗаписямНепринятоПоЗаголовкамИвентов(ТекстФайлаОтказа); 
	Иначе
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Ошибка чтения файла-отказа!"; 
		Сообщение.Сообщить();
	КонецЕсли;
		
КонецПроцедуры        

Функция ОпределитьПолныйЧастичныйОтказXML(RejectФайл) Экспорт
	
	ТипОтказа = Перечисления.НБКИ_ТипыОтказов.Полный;

	ЯкорьЗаписи = "orderNum";
	
	Если СтрНайти(RejectФайл, ЯкорьЗаписи) <> 0 Тогда
		ТипОтказа =  Перечисления.НБКИ_ТипыОтказов.Частичный;
	КонецЕсли;
			
	Возврат ТипОтказа;
КонецФункции       

Процедура ПроставитьЗаписямНепринятоПоЗаголовкамИвентов(RejectФайл) Экспорт  
	Для Каждого Заголовок Из ЭтотОбъект.СтруктураВыгрузки Цикл
		Заголовок.Принято = Истина;
	КонецЦикла; 
	
	МассивСтрок = СтрРазделить(Строка(RejectФайл), Символы.ПС, Ложь);
	Для Каждого стр Из МассивСтрок Цикл 
		Если СтрНайти(стр, "errorNum") > 0 Тогда
			Для Каждого Заголовок Из ЭтотОбъект.СтруктураВыгрузки Цикл
				Если СтрНайти(стр, Заголовок.ЗаголовокGroupHeader) > 0 Тогда
					Заголовок.Принято = Ложь; 
					Сообщить("Не принято: " + Строка(Заголовок.ОбъектУчета) + " " + Строка(Заголовок.ЗаголовокGroupHeader));
					Прервать;
				КонецЕсли;	
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры 
