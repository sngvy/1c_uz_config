
&НаКлиенте
Асинх Процедура ЗагрузитьСправочник(Команда)
	
	Если Не ЗначениеЗаполнено(ФайлСправочника) Тогда
		Сообщить("Сначала нужно выбрать файл справочника!");
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ФайлРегионов)
		И ПолеИсходнойТаблицы.ВысотаТаблицы = 0 Тогда
		
		Сообщить("Нужно выбрать файл регионов или Заполнить табличную часть");
		Возврат;
		
	КонецЕсли;
	
	Адрес = Неопределено;
	Если ПрочитатьНаКлиенте Тогда
	
		Адрес = Ждать ЗагрузитьСправочникИндексовНаКлиенте(ФайлСправочника);
		ЗагрузитьСправочникНаСервереСКлиента(Адрес, ПолеИсходнойТаблицы);
		Возврат;
		
	КонецЕсли;

	ЗагрузитьСправочникНаСервере(ФайлСправочника, ПолеИсходнойТаблицы);

КонецПроцедуры

&НаКлиенте
Асинх Функция ЗагрузитьСправочникИндексовНаКлиенте(ПутьКФайлу)

	Описание = Ждать ПоместитьФайлНаСерверАсинх(
		,
		,
		,
		ПутьКФайлу
	);
	
	Возврат Описание.Адрес;

КонецФункции

&НаСервере
Процедура ЗагрузитьСправочникНаСервереСКлиента(Адрес, ПолеИсходнойТаблицы);

	Если Не ЭтоАдресВременногоХранилища(Адрес) Тогда
	
		ВызватьИсключение "Не удалось прочитать файл";
	
	КонецЕсли;
	
	ДвоичныйФайл = ПолучитьИзВременногоХранилища(Адрес);
	
	Каталог = КаталогВременныхФайлов();
	ВременныйФайл = Каталог + "base.dbf";
	ДвоичныйФайл.Записать(ВременныйФайл);
	
	ЗагрузитьСправочникНаСервере(ВременныйФайл, ПолеИсходнойТаблицы)

КонецПроцедуры


&НаСервере
Процедура ЗагрузитьСправочникНаСервере(ФайлСправочника, ПолеИсходнойТаблицы)
	
	НачатьТранзакцию();
	Попытка
		ЗначениеОбъект = РеквизитФормыВЗначение("Объект");
		ЗначениеОбъект.ЗагрузитьСправочникИндексов(ФайлСправочника, ПолеИсходнойТаблицы);
		ЗначениеВРеквизитФормы(ЗначениеОбъект, "Объект");
		ТекстСообщения = "Загрузка успешно выполнена!";
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстСообщения = "При загрузке данных произошла ошибка!"
			+ Символы.ПС
			+ ОписаниеОшибки();
	КонецПопытки;
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = ТекстСообщения;
	Сообщение.Сообщить();

КонецПроцедуры	

&НаКлиенте
Процедура ПутьФайлаСправочникаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок = "Выберите файл Эталонного справочника индексов...";
	Диалог.МножественныйВыбор = Ложь;
	Диалог.Фильтр = "Файл базы данных(*.dbf)|*.dbf|";
	Если Диалог.Выбрать() Тогда
		ФайлСправочника = Диалог.ПолноеИмяФайла;
	КонецЕсли;	
КонецПроцедуры

#Область СчитатьФайл

&НаКлиенте
Асинх Процедура ПрочитатьФайлСКлиента()

	ДанныеПрочитанногоФайла = Неопределено;
	Попытка
	
		ДанныеПрочитанногоФайла = Ждать УправлениеЗагрузкойКлиент.ИмпортироватьФайл(ЭтаФорма.УникальныйИдентификатор);
	
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ОписаниеОшибки();
		Сообщение.Сообщить();
	КонецПопытки;
	
	Если НЕ ЗначениеЗаполнено(ДанныеПрочитанногоФайла) Тогда
	
		Возврат;
	
	КонецЕсли;
	
	ПолеИсходнойТаблицы = ДанныеПрочитанногоФайла["ТабличныйДокумент"];
	ФайлРегионов = ДанныеПрочитанногоФайла["ПутьФайлаНаКлиенте"];

КонецПроцедуры

&НаКлиенте
Процедура ФайлИмпортаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПрочитатьФайлСКлиента();
	
КонецПроцедуры

#КонецОбласти