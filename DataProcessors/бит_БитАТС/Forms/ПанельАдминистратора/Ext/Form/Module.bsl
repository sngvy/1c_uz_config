
#Область ОписаниеПеременных

&НаКлиенте
Перем обКонтроллерАТС;

&НаКлиенте
Перем Хост;

// Тип - Соответствие (Map)
// ключ - внутренний номер, значение - Строка, имя внутреннего абонента.
&НаКлиенте
Перем СоответствиеНомеровХоста;

&НаКлиенте
Перем фильтрТип;		// переменная для проверки изменения фильтра

&НаКлиенте
Перем фильтрВнешние;	// переменная для проверки изменения фильтра

&НаКлиенте
Перем ИнтервалОбновления;

//-----------------------------------------------------------------------------
// Тип - Соответствие (Map)
//  ключ - строка-номер телефона,
//  значение - структура из трех полей (КонтрагентСсылка, КонтактноеЛицоСсылка, Обновлен).
&НаКлиенте
Перем кэшКонтактов;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	кэшКонтактов = Новый Соответствие;
	
	бит_ТелефонияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ДеревоНомеров, "Владелец",
							ПредопределенноеЗначение("Справочник.бит_ТелефонныеСтанцииБитАТС.ПустаяСсылка"));
		
	ОбновитьДоступность();
		
	АвторизацияАдминистратор = бит_АТССервер.ПроверитьПраваАдминистрировать();
	
	Если АвторизацияАдминистратор Тогда
		Заголовок = "Панель управления звонками БИТ.АТС";
	Иначе
		Заголовок = "Панель просмотра звонков БИТ.АТС";
	КонецЕсли;
	
	Элементы.ЗвонкиКонтекстноеМенюПрослушать.Видимость	= АвторизацияАдминистратор;
	Элементы.ЗвонкиКонтекстноеМенюКонференция.Видимость	= АвторизацияАдминистратор;
	
	//
	Для Каждого ПолеНомера Из Элементы.Номера.ПодчиненныеЭлементы Цикл
		ПолеНомераЗаголовок = ПолеНомера.Заголовок;
		Если НЕ ЗначениеЗаполнено(ПолеНомераЗаголовок) Тогда
			ПолеНомераЗаголовок = ПолучитьЗаголовокПоляНомераИзРеквизитаСервер(ПолеНомера.Имя);
		КонецЕсли;
		Элементы.ПолеСортировки.СписокВыбора.Добавить(ПолеНомераЗаголовок);
	КонецЦикла;
	Если Элементы.ПолеСортировки.СписокВыбора.Количество() > 0 Тогда
		ПолеСортировки = Элементы.ПолеСортировки.СписокВыбора[0].Значение;
	КонецЕсли;
	//
	Если Элементы.НаправлениеСортировки.СписокВыбора.Количество() > 0 Тогда
		НаправлениеСортировки = Элементы.НаправлениеСортировки.СписокВыбора[0].Значение;
	КонецЕсли;
	
#Если ВебКлиент Тогда
	ИспользоватьВебСервис = Истина;
#КонецЕсли

	ПодключитьАТС(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	Если Подключен Тогда
		Отключить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОчередиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОткрытьДетальнуюИнформациюОчереди(Элементы.Очереди.ТекущиеДанные.ID,
		Элементы.Очереди.ТекущиеДанные.Наименование,
		Элементы.Очереди.ТекущиеДанные.Номер);
КонецПроцедуры

&НаКлиенте
Процедура КлиентскиеГруппыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОткрытьДетальнуюИнформациюОчереди(Элементы.КлиентскиеГруппы.ТекущиеДанные.ID,
		Элементы.КлиентскиеГруппы.ТекущиеДанные.Наименование,
		Элементы.КлиентскиеГруппы.ТекущиеДанные.Номер);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаТранковВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТранкИмя = Элементы.ЗагрузкаТранков.ТекущиеДанные.Наименование;
	ТранкПирПреф = Элементы.ЗагрузкаТранков.ТекущиеДанные.ПирТранкаСПрефиксом;
	формаИнфТранка = ПолучитьФорму("Обработка.бит_БитАТС.Форма.ИнформацияТранк");
	формаИнфТранка.ИсточникВнешнихСобытий	= ИмяИсточникаСобытий();
	формаИнфТранка.ИмяТранка = ТранкИмя;
	формаИнфТранка.ПирТранка = ТранкПирПреф;
	формаИнфТранка.УстановитьКонтроллерАТС(обКонтроллерАТС);
	формаИнфТранка.УстановитьСоответствиеНомеров(СоответствиеНомеровХоста, кэшКонтактов);
	бит_ТелефонияКлиентПереопределяемый.ОткрытьФормуСБлокировкойВладельца(формаИнфТранка);
	
КонецПроцедуры

&НаКлиенте
Процедура ФильтрНомеровТипПриИзменении(Элемент)
	ОбновитьТаблицы();
КонецПроцедуры

&НаКлиенте
Процедура ФильтрНомеровВнешниеПриИзменении(Элемент)
	ОбновитьТаблицы();
КонецПроцедуры

&НаКлиенте
Процедура ПолеСортировкиПриИзменении(Элемент)
	СортироватьТаблицуНомеровССохранениемТекущейСтроки();
КонецПроцедуры

&НаКлиенте
Процедура НаправлениеСортировкиПриИзменении(Элемент)
	СортироватьТаблицуНомеровССохранениемТекущейСтроки();
КонецПроцедуры

&НаКлиенте
Процедура ДеревоНомеровВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОбработкаВыбораГруппыДереваНомеров();
КонецПроцедуры

&НаКлиенте
Процедура ДеревоНомеровПриАктивизацииСтроки(Элемент)
	ОбработкаВыбораГруппыДереваНомеров();
КонецПроцедуры

&НаКлиенте
Процедура НомераВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СсылкаТекущ = Неопределено;
	Если Поле.Имя = "НомераКонтрагент" Тогда
		СсылкаТекущ = Элементы.Номера.ТекущиеДанные.Контрагент;
	ИначеЕсли Поле.Имя = "НомераКонтактноеЛицо" Тогда
		СсылкаТекущ = Элементы.Номера.ТекущиеДанные.КонтактноеЛицо;
	КонецЕсли;
	Если ЗначениеЗаполнено(СсылкаТекущ) Тогда
		бит_ТелефонияКлиентПереопределяемый.ПоказЗначение(СсылкаТекущ);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОжидающиеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СсылкаТекущ = Неопределено;
	Если Поле.Имя = "ОжидающиеКонтрагент" Тогда
		СсылкаТекущ = Элементы.Ожидающие.ТекущиеДанные.Контрагент;
	ИначеЕсли Поле.Имя = "ОжидающиеКонтактноеЛицо" Тогда
		СсылкаТекущ = Элементы.Ожидающие.ТекущиеДанные.КонтактноеЛицо;
	КонецЕсли;
	Если ЗначениеЗаполнено(СсылкаТекущ) Тогда
		бит_ТелефонияКлиентПереопределяемый.ПоказЗначение(СсылкаТекущ);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ГруппаАТСПриСменеСтраницы(Элемент, ТекущаяСтраница)
	ОбновитьТаблицы();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		НачатьЗвонок(ВыбранноеЗначение);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	Если Источник <> ИмяИсточникаСобытий() Тогда
		Возврат;
	КонецЕсли;
	
	Если Событие = "Ошибка" Тогда
		
		бит_АТСКлиент.ОбработкаОшибки(ТекущаяАТС, Источник, "КомпонентаУправлениеАТС_Ошибка", Данные, ЭтаФорма);
		
		Если обКонтроллерАТС.IsConnected Тогда

			ЗапуститьТаймерОбновления(ИнтервалОбновления);
			
		Иначе
			
			ПроверкаВосстановленияПодключения();
			
		КонецЕсли;
		
	ИначеЕсли Событие = "УправлениеПитанием" Тогда
		
		Если Данные = "СпящийРежимВход" Тогда
			Отключить();
		ИначеЕсли Данные = "СпящийРежимВыход" Тогда
			ПодключитьОбработчикОжидания("Подключить", 5, Истина);
		КонецЕсли;
		
	Иначе
		
		Если НЕ Подключен Тогда
			Возврат;
		КонецЕсли;
		
		бОбновлениеОчередей = (Событие = "ОбновлениеОчередей");
		бОбновлениеНомеров = (Событие = "ОбновлениеНомеров");
		
		Если бОбновлениеОчередей ИЛИ бОбновлениеНомеров Тогда
			
			бит_АТСКлиент.ОчиститьФлагОбновленВсегоКэшаКонтактов(кэшКонтактов);
			
			соотвИнфо = бит_ТелефонияКлиентСервер.ЗаполнитьСоответствиеИзXML(Данные);
			
			Если бОбновлениеОчередей Тогда
				ЗаполнитьТаблицыОчередейИОжидающих(соотвИнфо);
			ИначеЕсли бОбновлениеНомеров Тогда
				ЗаполнитьТаблицыНомеровЗвонковТранков(соотвИнфо);
			КонецЕсли;
		
			бит_АТСКлиент.УдалитьНеобновленныеКонтактыИзКэша(кэшКонтактов);
			
			ЗапуститьТаймерОбновления(ИнтервалОбновления);
			
			Оповестить(Событие, соотвИнфо, Источник);
			
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "БитфонПанельБИТАТС_ПодключениеВнешнейКомпоненты" Тогда
		
		обКонтроллерАТС = Параметр.Результат;
		
		Если обКонтроллерАТС <> Неопределено Тогда
			
			обКонтроллерАТС.SetMessageSourceName( ИмяИсточникаСобытий() );
		
			Подключить();
			
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "БитфонПанельБИТАТС_ПроверкаАдресаПодключения" Тогда
		
		Если Параметр.Результат = КодВозвратаДиалога.Да Тогда
			бит_АТССервер.УстановитьРазрешенныйАдрес(Параметр.ПараметрОповещения);
			ПодключитьРазрешенныйАдрес();
		Иначе
			Отключить();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "БитфонМониторПанель_ОбновлениеНастроек" Тогда

		Если Подключен Тогда
		
			ТекущаяАТСНов				= бит_АТССервер.ПолучитьАТС();
			НомерСвязанногоТелефонаНов 	= бит_АТССервер.ПолучитьНомерСвязанногоТелефона();

			Если ТекущаяАТС <> ТекущаяАТСНов Тогда
				Отключить();
				Подключить();
			КонецЕсли;
			
			Если НомерСвязанногоТелефона <> НомерСвязанногоТелефонаНов Тогда
				НомерСвязанногоТелефона = НомерСвязанногоТелефонаНов;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "БитфонПанельБИТАТС_ПеревестиОжидающийЗвонок" Тогда
		
		Если ЗначениеЗаполнено(Параметр.Результат) Тогда
			номерПеревода = Параметр.Результат;
			бит_АТСКлиент.ПеревестиОжидающийЗвонокНаНомер(ЭтаФорма, "Ожидающие", НомерСвязанногоТелефона, номерПеревода,
				обКонтроллерАТС, ИспользоватьВебСервис, ВебСервисИдКлиента);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодключитьАТС(Команда)
	Если ИспользоватьВебСервис Тогда
		Подключить();
	Иначе
		ПодключитьВК();
	КонецЕсли;
	ПоказатьОповещениеПользователя("БИТ.Phone",,"Подключение к АТС выполнено",БиблиотекаКартинок.NC_Подсистема_бтБИТАТС);
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьАТС(Команда)
	Отключить();
	ПоказатьОповещениеПользователя("БИТ.Phone",,"Отключение от АТС выполнено",БиблиотекаКартинок.NC_Подсистема_бтБИТАТС);
КонецПроцедуры

&НаКлиенте
Процедура Позвонить(Команда)
	Если ПроверкаСтатусаСвободен() Тогда
		НачатьЗвонок(Элементы.Номера.ТекущиеДанные.Номер);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Прослушать(Команда)
	ПодключитьсяКРазговору(0);
КонецПроцедуры

&НаКлиенте
Процедура Подсказать(Команда)
	ПодключитьсяКРазговору(1);
КонецПроцедуры

&НаКлиенте
Процедура Конференция(Команда)
	ПодключитьсяКРазговору(2);
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРазговор(Команда)

	текущ = Элементы.Номера.ТекущиеДанные;
	
	Если текущ = Неопределено Тогда
		бит_ТелефонияКлиентПереопределяемый.ПоказПредупреждение("Не выбран номер");
		Возврат;
	КонецЕсли;
	
	индСтатусаНомера = текущ.ИндексКартинкиСтатуса;
	Если НЕ ((индСтатусаНомера = 2) ИЛИ (индСтатусаНомера = 5) ИЛИ (индСтатусаНомера = 6)) Тогда
		бит_ТелефонияКлиентПереопределяемый.ПоказПредупреждение("На номере нет активного разговора");
		Возврат;
	КонецЕсли;
	
	ГруппаДоступаНомера = Элементы.Номера.ТекущиеДанные.ГруппаДоступа;
	
	УправлениеРазрешено = ПроверитьДоступностьПодключенияКРазговоруСервер(ГруппаДоступаНомера);
	
	Если НЕ УправлениеРазрешено Тогда
		бит_ТелефонияКлиентПереопределяемый.ПоказПредупреждение("Недостаточно прав для завершения разговора");
		Возврат;
	КонецЕсли;
	
	стрКанал = Элементы.Номера.ТекущиеДанные.Канал;
	Если НЕ ЗначениеЗаполнено(стрКанал) Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		Если ИспользоватьВебСервис Тогда
			бит_АТССервер.ВебСервисОтбойРазговора(ТекущаяАТС, ВебСервисИдКлиента, стрКанал);
		Иначе
			обКонтроллерАТС.Hangup(стрКанал);
		КонецЕсли;
	Исключение
		бит_ТелефонияКлиент.ВывестиСообщение( ОписаниеОшибки(), ЭтаФорма );
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура НомераИнформация(Команда)
	формаЛегенда = ПолучитьФорму("Обработка.бит_БитАТС.Форма.ЛегендаСтатусов", , ЭтаФорма);
	бит_ТелефонияКлиентПереопределяемый.ОткрытьФормуСБлокировкойВладельца(формаЛегенда);
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСвойстваКанала(Команда)
	ТекущаяСтрокаНомеров = Элементы.Номера.ТекущиеДанные;
	Если ТекущаяСтрокаНомеров <> Неопределено Тогда
		ИмяКанала			= ТекущаяСтрокаНомеров.Канал;
		ИмяСвязанногоКанала	= ТекущаяСтрокаНомеров.СвязКанал;
		флагВходящий		= ТекущаяСтрокаНомеров.Входящий;
		//
		бит_ТелефонияКлиент.ВывестиСообщение("Имя канала: " + ИмяКанала, ЭтаФорма);
		бит_ТелефонияКлиент.ВывестиСообщение("Имя связанного канала: " + ИмяСвязанногоКанала, ЭтаФорма);
		//
		Попытка
			Если ИспользоватьВебСервис Тогда
				ХэшЗаписи = бит_АТССервер.ВебСервисПолучитьХэшЗаписиРазговора(ТекущаяАТС, ВебСервисИдКлиента, ИмяКанала, флагВходящий, ИмяСвязанногоКанала);
			Иначе
				ХэшЗаписи = обКонтроллерАТС.GetRecordHash(ИмяКанала, флагВходящий, ИмяСвязанногоКанала);
			КонецЕсли;
			бит_ТелефонияКлиент.ВывестиСообщение("Хэш записи: " + ХэшЗаписи, ЭтаФорма);
		Исключение
			бит_ТелефонияКлиент.ВывестиСообщение("Ошибка получения хэша записи - " + ОписаниеОшибки(), ЭтаФорма);
		КонецПопытки;
		//
		Попытка
			Если ИспользоватьВебСервис Тогда
				ТранзитИд = бит_АТССервер.ВебСервисПолучитьТранзитИд(ТекущаяАТС, ВебСервисИдКлиента, ИмяКанала);
			Иначе
				ТранзитИд = обКонтроллерАТС.GetTransitID(ИмяКанала);
			КонецЕсли;
			бит_ТелефонияКлиент.ВывестиСообщение("TransitID: " + ТранзитИд, ЭтаФорма);
		Исключение
			бит_ТелефонияКлиент.ВывестиСообщение("Ошибка получения TransitID - " + ОписаниеОшибки(), ЭтаФорма);
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСвойстваНомера(Команда)
	ТекущаяСтрокаНомеров = Элементы.Номера.ТекущиеДанные;
	Если ТекущаяСтрокаНомеров <> Неопределено Тогда
		НомерТекущ	= ТекущаяСтрокаНомеров.Номер;
		СипЛогин	= ТекущаяСтрокаНомеров.SIPЛогин;
		Если ЗначениеЗаполнено(СипЛогин) Тогда
			Попытка
				Если ИспользоватьВебСервис Тогда
					стрПирИнфо = бит_АТССервер.ВебСервисПолучитьСвойстваНомера(ТекущаяАТС, ВебСервисИдКлиента, СипЛогин);
				Иначе
					стрПирИнфо = обКонтроллерАТС.GetSIPPeerInfo(СипЛогин);
				КонецЕсли;
				бит_ТелефонияКлиент.ВывестиСообщение("Информация номера " + НомерТекущ + " (SIP логин " + СипЛогин + "): " +
					Символы.ВК + Символы.ПС + стрПирИнфо, ЭтаФорма);
			Исключение
				бит_ТелефонияКлиент.ВывестиСообщение( ОписаниеОшибки(), ЭтаФорма );
			КонецПопытки;
		Иначе
			бит_ТелефонияКлиент.ВывестиСообщение("Не найден SIP логин номера", ЭтаФорма);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПозвонитьСоСвязанногоТелефона(Команда)
	формаВводаНомера = бит_ТелефонияКлиент.ПолучитьФормуВводаНомера(ЭтаФорма, Ложь, Неопределено, Ложь, "");
	бит_ТелефонияКлиентПереопределяемый.ОткрытьФормуСБлокировкойВладельца(формаВводаНомера);
КонецПроцедуры

&НаКлиенте
Процедура Настройки(Команда)
	бит_АТСКлиент.ОткрытьФормуНастроек(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПеревестиНаВыбранныйНомер(Команда)
	бит_АТСКлиент.ПеревестиЗвонокВыборНомера(ЭтаФорма, "Ожидающие", обКонтроллерАТС, НомерСвязанногоТелефона,
					"БитфонПанельБИТАТС_ПеревестиОжидающийЗвонок");
КонецПроцедуры

&НаКлиенте
Процедура ПеревестиНаСвойНомер(Команда)
	бит_АТСКлиент.ПеревестиОжидающийЗвонокНаСвойНомер(ЭтаФорма, "Ожидающие", НомерСвязанногоТелефона,
		обКонтроллерАТС, ИспользоватьВебСервис, ВебСервисИдКлиента);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ИмяИсточникаСобытий()
	Возврат "БИТ.УправлениеАТС";
КонецФункции

&НаКлиенте
Процедура ОбновитьДоступность()
	Элементы.ГруппаАТС.Доступность						= Подключен;
	Элементы.ПодключитьАТС.Доступность					= НЕ Подключен;
	Элементы.ОтключитьАТС.Доступность					= Подключен;
	Элементы.ПозвонитьСоСвязанногоТелефона.Доступность	= Подключен;
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьТаймерОбновления(Таймаут)
	ПодключитьОбработчикОжидания("ОбновитьТаблицы", Таймаут, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОстановитьТаймерОбновления()
	ОтключитьОбработчикОжидания("ОбновитьТаблицы");
КонецПроцедуры

&НаСервере
Функция ПроверитьДоступностьПодключенияКРазговоруСервер(ГруппаДоступаСсылка)
	АдминистраторАТС = бит_АТССервер.ПроверитьПраваАдминистрировать();
	Если АдминистраторАТС Тогда
		Возврат Истина;
	КонецЕсли;
	ТекущПольз = бит_ТелефонияСервер.ПолучитьТекущегоПользователя();
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	бит_ГруппыДоступаБитАТСПользователи.Пользователь.Ссылка
	               |ИЗ
	               |	Справочник.бит_ГруппыДоступаБитАТС.Пользователи КАК бит_ГруппыДоступаБитАТСПользователи
	               |ГДЕ
	               |	бит_ГруппыДоступаБитАТСПользователи.Ссылка = &ГруппаДоступа
	               |	И бит_ГруппыДоступаБитАТСПользователи.Пользователь.Ссылка = &Пользователь";
	Запрос.УстановитьПараметр("ГруппаДоступа", ГруппаДоступаСсылка);
	Запрос.УстановитьПараметр("Пользователь", ТекущПольз);
	Возврат НЕ Запрос.Выполнить().Пустой();
КонецФункции

&НаСервере
Функция ПолучитьЗаголовокПоляНомераИзРеквизитаСервер(ИмяЭлемента)
	ЗаголовокРеквизита = "";
	ПутьКДаннымРеквизита = Элементы[ИмяЭлемента].ПутьКДанным;
	массРеквизитов = ПолучитьРеквизиты("Номера");
	Для Каждого реквизит Из массРеквизитов Цикл
		путьРеквизита = реквизит.Путь + "." + реквизит.Имя;
		Если путьРеквизита = ПутьКДаннымРеквизита Тогда
			ЗаголовокРеквизита = реквизит.Заголовок;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Возврат ЗаголовокРеквизита;
КонецФункции

&НаКлиенте
Процедура ПодключитьВК()
	Если обКонтроллерАТС <> Неопределено Тогда
		обКонтроллерАТС = Неопределено;
	КонецЕсли;
	бит_АТСКлиент.ПодключениеКомпонентыУправлениеАТС("БитфонПанельБИТАТС_ПодключениеВнешнейКомпоненты");
КонецПроцедуры

&НаКлиенте
Процедура Подключить()
	
	ТекущаяАТС = бит_АТССервер.ПолучитьАТС();
	
	Если НЕ ЗначениеЗаполнено(ТекущаяАТС) Тогда
		бит_ТелефонияКлиент.ВывестиСообщение("Не задана АТС для подключения в настройках", ЭтаФорма);
		Возврат;
	КонецЕсли;
	
	стрРазрешенныйАдрес = бит_АТССервер.ПолучитьРазрешенныйАдрес();
	стрАдресПодключения = бит_АТССервер.ПолучитьПараметрыПодключенияАТС(ТекущаяАТС).Адрес;
	Если стрРазрешенныйАдрес = стрАдресПодключения Тогда
		ПодключитьРазрешенныйАдрес();
	Иначе
		стрЗапросНовогоПодключения = "Разрешить подключение к БИТ.АТС по адресу '" + стрАдресПодключения + "'?";
		бит_ТелефонияКлиентПереопределяемый.ПоказВопрос(стрЗапросНовогоПодключения,
			РежимДиалогаВопрос.ДаНет, , , , "БитфонПанельБИТАТС_ПроверкаАдресаПодключения", стрАдресПодключения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьРазрешенныйАдрес()
	// Состояние("Ожидание подключения...");
	ПодключитьОбработчикОжидания("ПодключитьРазрешенныйАдресЗавершение", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьРазрешенныйАдресЗавершение()
	
	Попытка

		НомерСвязанногоТелефона = бит_АТССервер.ПолучитьНомерСвязанногоТелефона();
		Если НЕ ЗначениеЗаполнено(НомерСвязанногоТелефона) Тогда
			бит_ТелефонияКлиент.ВывестиСообщение(
				"Не задан номер связанного телефона в настройках! Исходящие звонки, подключения к разговорам и перевод ожидающих звонков себе невозможны",
				ЭтаФорма);
		КонецЕсли;
		
		ИнтервалОбновления = бит_АТССервер.ПолучитьИнтервалОбновления();
		Если ИнтервалОбновления = 0 Тогда
			бит_ТелефонияКлиент.ВывестиСообщение("Не задан интервал обновления в настройках, принимается 5 сек", ЭтаФорма);
			ИнтервалОбновления = 5;
		КонецЕсли;
				
		СписокНомеровХоста = "";
		Хост = бит_АТССервер.ПолучитьПараметрыАТС(ТекущаяАТС, СписокНомеровХоста, СоответствиеНомеровХоста);
		
		Если ИспользоватьВебСервис Тогда
			ВебСервисИдКлиента = бит_АТССервер.ВебСервисОткрытьСоединение(ТекущаяАТС, Хост.Адрес, Хост.Пользователь, Хост.Пароль, СписокНомеровХоста, Ложь);
			Подключен = Истина;
		Иначе
			бит_АТСКлиент.УстановитьПараметрыПроверкиЛицензии(обКонтроллерАТС, Ложь);
			
			обКонтроллерАТС.Connect(Хост.Адрес, Хост.Пользователь, Хост.Пароль,
								СписокНомеровХоста);
			
			Подключен = обКонтроллерАТС.IsConnected;
		КонецЕсли;
		
		ВосстановлениеПодключения = НЕ Подключен;
	
		// установка фильтра дерева иерархии номеров
		бит_ТелефонияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ДеревоНомеров, "Владелец", ТекущаяАТС);
		
		//
		ОбновитьДоступность();
		
		Элементы.ДеревоНомеров.ТекущаяСтрока = ПредопределенноеЗначение("Справочник.бит_НомераБитАТС.ПустаяСсылка");
		ОбработкаВыбораГруппыДереваНомеров();
		
	Исключение
		бит_ТелефонияКлиент.ВывестиСообщение( "Ошибка подключения к АТС '" + Строка(ТекущаяАТС) + "'", ЭтаФорма );
		Если ИспользоватьВебСервис Тогда
			ОбработкаОшибкиВебСервиса(ОписаниеОшибки());
		Иначе
			бит_ТелефонияКлиент.ВывестиСообщение(ОписаниеОшибки(), ЭтаФорма);
		КонецЕсли;
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура Отключить()
	
	ОстановитьТаймерОбновления();
		
	Попытка
		
		Если ИспользоватьВебСервис Тогда
			
			Если Подключен Тогда
				бит_АТССервер.ВебСервисЗакрытьСоединение(ТекущаяАТС, ВебСервисИдКлиента);
			КонецЕсли;
			
			ВебСервисИдКлиента = 0;
			
			Подключен = Ложь;
			
		Иначе
			
			обКонтроллерАТС.Disconnect();
			
			Подключен = обКонтроллерАТС.IsConnected;
			
		КонецЕсли;
		
	Исключение
		
		бит_ТелефонияКлиент.ВывестиСообщение( "Ошибка отключения от АТС", ЭтаФорма );
		бит_ТелефонияКлиент.ВывестиСообщение( ОписаниеОшибки(), ЭтаФорма );
		Подключен = Ложь;
		
	КонецПопытки;
	
	ОбновитьДоступность();
	
	//
	
	НомерСвязанногоТелефона = "";
	ТекущаяАТС = ПредопределенноеЗначение("Справочник.бит_ТелефонныеСтанцииБитАТС.ПустаяСсылка");
	
	Хост = Неопределено;
	
	СоответствиеНомеровХоста = Неопределено;
	
	кэшКонтактов.Очистить();
	
	// сброс фильтра дерева иерархии номеров
	бит_ТелефонияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ДеревоНомеров, "Владелец",
					ПредопределенноеЗначение("Справочник.бит_ТелефонныеСтанцииБитАТС.ПустаяСсылка"));
	
	//
	
	ОчиститьТаблицуНомеров();
	
	ОчиститьТаблицуЗвонков();
	
	ОчиститьТаблицуКлиентскихГрупп();
	
	ОчиститьТаблицуОчередей();
	
	ОчиститьТаблицуОжидающих();
	
	ОчиститьТаблицуТранков();
	
	ВсегоЗвонков = 0;
	
	Элементы.ГруппаАТС.ТекущаяСтраница = Элементы.ГруппаНомера;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
&НаКлиенте
Процедура ОчиститьТаблицуНомеров()
	Номера.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТаблицуЗвонков()
	Звонки.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТаблицуКлиентскихГрупп()
	КлиентскиеГруппы.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТаблицуОчередей()
	Очереди.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТаблицуОжидающих()
	Ожидающие.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТаблицуТранков()
	ЗагрузкаТранков.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТаблицыОчередейИОжидающих(соотвИнфо)
	
	списокОчередей = соотвИнфо.Получить("Queues");
	
	ИзменитьРазмерТаблицы(Очереди, списокОчередей.Количество());
	
	//
	ИндексТекущейСтрокиОжидающих = Ожидающие.Индекс(Элементы.Ожидающие.ТекущиеДанные);
	ОчиститьТаблицуОжидающих();
	
	//
	ИндексТекущейСтрокиКлиентскихГрупп = КлиентскиеГруппы.Индекс(Элементы.КлиентскиеГруппы.ТекущиеДанные);
	ОчиститьТаблицуКлиентскихГрупп();
	
	массОчередей = Новый Массив();
	Для Каждого элСпискаОчередь Из списокОчередей Цикл
		массОчередей.Добавить(элСпискаОчередь.Значение);
	КонецЦикла;
	БыстраяСортировка(массОчередей, "id", "Возр");
	
	//
	счетчикОчередей = 0;
	Для Каждого элСпискаОчередь Из массОчередей Цикл
		соотвОчередь = массОчередей[счетчикОчередей];
		ид					= соотвОчередь.Получить("id");
		ИмяОчереди			= соотвОчередь.Получить("name");
		НомерОчереди		= соотвОчередь.Получить("number");
		текущиеЗвонки		= соотвОчередь.Получить("current_calls");
		стратегия			= соотвОчередь.Получить("strategy");
		времяУдержания		= соотвОчередь.Получить("hold_time");
		времяРазговора		= соотвОчередь.Получить("talk_time");
		вес					= соотвОчередь.Получить("weight");
		отвечено			= соотвОчередь.Получить("answered");
		неотвечено			= соотвОчередь.Получить("unanswered");
		уровеньОбслуживания	= соотвОчередь.Получить("service_level");
		периодОбслуживания	= соотвОчередь.Получить("service_period");
		//
		списокЧленов = соотвОчередь.Получить("Members");
		списокОжидающих = соотвОчередь.Получить("Callers");
		//
		КолвоОжидающих = списокОжидающих.Количество();
		//
		СтрокаТаблОчередей = Очереди[счетчикОчередей];
		СтрокаТаблОчередей.ID					= ид;
		СтрокаТаблОчередей.Наименование			= ИмяОчереди;
		СтрокаТаблОчередей.Номер				= НомерОчереди;
		СтрокаТаблОчередей.Стратегия			= стратегия;
		СтрокаТаблОчередей.ВремяУдержания		= времяУдержания;
		СтрокаТаблОчередей.ВремяРазговора		= времяРазговора;
		СтрокаТаблОчередей.Вес					= вес;
		СтрокаТаблОчередей.Отвечено				= отвечено;
		СтрокаТаблОчередей.Неотвечено			= неотвечено;
		СтрокаТаблОчередей.УровеньОбслуживания	= уровеньОбслуживания;
		СтрокаТаблОчередей.ПериодОбслуживания	= периодОбслуживания;
		СтрокаТаблОчередей.Члены				= Строка(списокЧленов.Количество());
		СтрокаТаблОчередей.Ожидающие			= Строка(КолвоОжидающих);
		//
		очередьСвободно = 0;
		очередьЗанято = 0;
		очередьНеВСети = 0;
		Для Каждого домЧленОчереди ИЗ списокЧленов Цикл
			СтатусЧленаОчередиЧисл = Число(домЧленОчереди.Получить("Status"));
			Если СтатусЧленаОчередиЧисл = 1 Тогда
				очередьСвободно = очередьСвободно + 1;	// "Свободен"
			ИначеЕсли СтатусЧленаОчередиЧисл = 2 Тогда
				очередьЗанято = очередьЗанято + 1;		// "Разговаривает"
			ИначеЕсли СтатусЧленаОчередиЧисл = 3 Тогда
				очередьЗанято = очередьЗанято + 1;		// "Не беспокоить (DND)"
			ИначеЕсли СтатусЧленаОчередиЧисл = 5 Тогда
				очередьНеВСети = очередьНеВСети + 1;	// "Не зарегистирован на АТС"
			ИначеЕсли СтатусЧленаОчередиЧисл = 6 Тогда
				очередьЗанято = очередьЗанято + 1;		// "Входящий звонок"
			ИначеЕсли СтатусЧленаОчередиЧисл = 7 Тогда
				очередьЗанято = очередьЗанято + 1;		// "Разговаривает + входящий звонок"
			ИначеЕсли СтатусЧленаОчередиЧисл = 8 Тогда
				очередьЗанято = очередьЗанято + 1;		// "Удержание"
			Иначе
				очередьНеВСети = очередьНеВСети + 1;	// "Неизвестно"
			КонецЕсли;
		КонецЦикла;
		СтрокаТаблОчередей.ТекущиеЗвонки		= Строка(очередьЗанято);
		//
		Для Каждого домОжидающийОчереди ИЗ списокОжидающих Цикл
			СтрокаТаблОжидающих = Ожидающие.Добавить();
			СтрокаТаблОжидающих.Очередь			= ИмяОчереди;
			//
			стрНомер = домОжидающийОчереди.Получить("CallerIDnum");
			КонтрагентСсылка = Неопределено;
			КонтактноеЛицоСсылка = Неопределено;
			бит_АТСКлиент.СократитьНомерНайтиКонтрагента(кэшКонтактов, СоответствиеНомеровХоста, стрНомер, КонтрагентСсылка, КонтактноеЛицоСсылка);
			СтрокаТаблОжидающих.Номер			= стрНомер;
			СтрокаТаблОжидающих.Контрагент		= КонтрагентСсылка;
			СтрокаТаблОжидающих.КонтактноеЛицо	= КонтактноеЛицоСсылка;
			//
			стрВремяОжидания = домОжидающийОчереди.Получить("Wait");
			числВремяОжидания = Число(стрВремяОжидания);
			СтрокаТаблОжидающих.ВремяОжидания	= бит_ТелефонияКлиентСервер.ФорматироватьДлительностьЗвонкаСЛидНулями(числВремяОжидания);
			//
			СтрокаТаблОжидающих.Канал			= домОжидающийОчереди.Получить("Channel");
		КонецЦикла;
		//
		Если Хост <> Неопределено Тогда
			НаименованиеГр = Хост.КлиентскиеГруппы.Получить(НомерОчереди);
			Если НаименованиеГр <> Неопределено Тогда
				строкаКлиентскиеГр = КлиентскиеГруппы.Добавить();
				строкаКлиентскиеГр.Номер		= НомерОчереди;
				строкаКлиентскиеГр.Наименование	= ИмяОчереди;
				строкаКлиентскиеГр.Всего		= Строка(списокЧленов.Количество());
				строкаКлиентскиеГр.Ожидающие	= Строка(КолвоОжидающих);
				строкаКлиентскиеГр.ID			= ид;
				строкаКлиентскиеГр.Свободно		= очередьСвободно;
				строкаКлиентскиеГр.Занято		= очередьЗанято;
				строкаКлиентскиеГр.НеВСети		= очередьНеВСети;
			КонецЕсли;
		КонецЕсли;
		//
		счетчикОчередей = счетчикОчередей + 1;
	КонецЦикла;
	
	Если (ИндексТекущейСтрокиОжидающих >= 0) И (ИндексТекущейСтрокиОжидающих < Ожидающие.Количество()) Тогда
		ВыделеннаяСтрокаОжид = Ожидающие[ИндексТекущейСтрокиОжидающих];
		Элементы.Ожидающие.ТекущаяСтрока = ВыделеннаяСтрокаОжид.ПолучитьИдентификатор();
	КонецЕсли;
	
	Если (ИндексТекущейСтрокиКлиентскихГрупп >= 0) И (ИндексТекущейСтрокиКлиентскихГрупп < КлиентскиеГруппы.Количество()) Тогда
		ВыделеннаяСтрокаКлиентскихГрупп = КлиентскиеГруппы[ИндексТекущейСтрокиКлиентскихГрупп];
		Элементы.КлиентскиеГруппы.ТекущаяСтрока = ВыделеннаяСтрокаКлиентскихГрупп.ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура б_Сортировка(Массив, НижнийПредел, ВерхнийПредел, стрПоле, флагВозр)
	
	i	= НижнийПредел;
	j	= ВерхнийПредел;
	
	m	= Массив[i][стрПоле];
	
	Пока Истина Цикл
		
		Если флагВозр Тогда
			
			Пока Массив[i][стрПоле] < m Цикл
				i = i + 1;
			КонецЦикла;
			
			Пока Массив[j][стрПоле] > m Цикл
				j = j - 1;
			КонецЦикла;
			
		Иначе
			
			Пока Массив[i][стрПоле] > m Цикл
				i = i + 1;
			КонецЦикла;
			
			Пока Массив[j][стрПоле] < m Цикл
				j = j - 1;
			КонецЦикла;
			
		КонецЕсли;
		
		Если i <= j Тогда
			Замена		= Массив[i];
			Массив[i]	= Массив[j];
			Массив[j]	= Замена;
			i			= i + 1;
			j			= j - 1;
		КонецЕсли;
		
		Если i > j Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НижнийПредел < j Тогда
		б_Сортировка(Массив, НижнийПредел, j, стрПоле, флагВозр);
	КонецЕсли;
	
	Если i < ВерхнийПредел Тогда
		б_Сортировка(Массив, i, ВерхнийПредел, стрПоле, флагВозр);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура БыстраяСортировка(Массив, стрПолеСортировки, стрНаправлениеСортировки)
	Если Массив.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	НижняяГраница = 0;
	ВерхняяГраница = Массив.ВГраница();
	флагПоВозрастанию = (стрНаправлениеСортировки = "Возр");
	б_Сортировка(Массив, НижняяГраница, ВерхняяГраница, стрПолеСортировки, флагПоВозрастанию);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТаблицыНомеровЗвонковТранков(соотвХмл)
	
	/////////////////////////////////////
	// Номера
	/////////////////////////////////////
	
	Если Хост = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	массивЭкстеншенов = Хост.Номера;
	
	соотвИнфо = соотвХмл.Получить("ChannelsInfo");
	
	соотвНомера = соотвИнфо.Получить("Extensions");
	
	// временная таблица, без фильтра
	массНомераВременный = Новый Массив();
	Для Каждого Экстеншен Из массивЭкстеншенов Цикл
		НомерЭкст			= Экстеншен.Номер;
		ИмяЭкст				= Экстеншен.Имя;
		ГруппаДоступаЭкст	= Экстеншен.ГруппаДоступа;
		//
		обЭкстенш = соотвНомера.Получить("Extension_" + НомерЭкст);
		Если обЭкстенш = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		стрСтатусЭкстенш = обЭкстенш.Получить("status");
		статусЭкстенш = Число(стрСтатусЭкстенш);
		
		списокАккаунтовЭкстенш = обЭкстенш.Получить("SIPLogins");
		
		колвоАккаунтовНомера = списокАккаунтовЭкстенш.Количество();
		
		Если колвоАккаунтовНомера = 0 Тогда
			// хинты для экстеншена не включены, статус не известен
			ДобавитьСтатусНомераВМассив(ИмяЭкст, НомерЭкст, стрСтатусЭкстенш, ГруппаДоступаЭкст,
				"", "", "", "", Ложь, Ложь, 0, "", "", "", массНомераВременный);
		КонецЕсли;
		//
		Для йАккаунт=0 По (колвоАккаунтовНомера-1) Цикл
			//
			обАккаунтЭкстенш = списокАккаунтовЭкстенш[йАккаунт];
			//
			СтатусНомера		= статусЭкстенш;
			//
			ИндексКартинкиСтатуса = бит_АТСКлиент.ПолучитьИндексКартинкиСтатуса(СтатусНомера);
			//
			СипУстройствоНомера	= обАккаунтЭкстенш.Получить("value");
			//
			Если колвоАккаунтовНомера > 1 Тогда
				ИмяЭкст = Экстеншен.Имя + " (" + СипУстройствоНомера + ")";
			КонецЕсли;
			//
			списокЗвонковАккаунта = обАккаунтЭкстенш.Получить("Channels");
			колвоЗвонковАккаунта = списокЗвонковАккаунта.Количество();
			//
			Если колвоЗвонковАккаунта > 0 Тогда
				Для йЗвонокАккаунта=0 По (колвоЗвонковАккаунта-1) Цикл
					//
					обЗвонокАккаунта = списокЗвонковАккаунта[йЗвонокАккаунта];
					//
					НаправлениеЗвонка		= "";
					СоединениеНомера		= "";
					ДлительностьРазговора	= "";
					ВнешнийВызов			= Ложь;
					//
					ВходящийЗвонок			= Булево(Число(обЗвонокАккаунта.Получить("Incoming")));
					НаправлениеЗвонка = ? ( ВходящийЗвонок, "<-", "->");
					//
					ДлительностьРазговора	= обЗвонокАккаунта.Получить("Duration");
					//
					СоединениеНомера		= обЗвонокАккаунта.Получить("BridgetNum");
					Если СоединениеНомера = "(connecting)" Тогда
						СоединениеНомера = "соединение...";
					Иначе
						СоединениеНомера = бит_АТСКлиент.СократитьНомер(СоединениеНомера, Ложь, ВнешнийВызов);
					КонецЕсли;
					//
					ИмяКанала				= обЗвонокАккаунта.Получить("Name");
					ИмяСвязанногоКанала		= обЗвонокАккаунта.Получить("BridgetName");
					//
					НабранныйНомер			= "";
					//Если ВходящийЗвонок Тогда
					//	НабранныйНомер = обКонтроллерАТС.GetDestination(ИмяКанала);
					//	НабранныйНомер = бит_ТелефонияСервер.ПолучитьНаименованиеНабранногоНомера(НабранныйНомер);
					//КонецЕсли;
					//
					ДобавитьСтатусНомераВМассив(ИмяЭкст, НомерЭкст, стрСтатусЭкстенш, ГруппаДоступаЭкст,
						СипУстройствоНомера, НаправлениеЗвонка, СоединениеНомера, ДлительностьРазговора,
						ВнешнийВызов, ВходящийЗвонок, ИндексКартинкиСтатуса, ИмяКанала, ИмяСвязанногоКанала,
						НабранныйНомер,
						массНомераВременный);
				КонецЦикла;
			Иначе
				// свободный аккаунт
				ДобавитьСтатусНомераВМассив(ИмяЭкст, НомерЭкст, стрСтатусЭкстенш, ГруппаДоступаЭкст,
					СипУстройствоНомера, "", "", "", Ложь, Ложь, ИндексКартинкиСтатуса, "", "", "",
					массНомераВременный);
			КонецЕсли;
			//
		КонецЦикла;
	КонецЦикла;
	
	ФильтрИзменен = Ложь;
	Если ((фильтрТип <> ФильтрНомеровТип) ИЛИ (фильтрВнешние <> ФильтрНомеровВнешние)) Тогда
		ФильтрИзменен = Истина;
		// сохранение новых значений
		фильтрТип		= ФильтрНомеровТип;
		фильтрВнешние	= ФильтрНомеровВнешние;
	КонецЕсли;
	
	ТекущаяСтрокаНомеровИнф = ПолучитьТекущуюСтрокуНомеровСтрукт();
	ИндексТекущейСтрокиНомеров = Номера.Индекс(Элементы.Номера.ТекущиеДанные);
	
	Если ФильтрИзменен Тогда
		ОчиститьТаблицуНомеров();
	КонецЕсли;
	
	массНомераПослеФильтра = Новый Массив();
	Для Каждого строкаНомераВременный Из массНомераВременный Цикл
		
		Если ФильтрНомеровВнешние ИЛИ (ФильтрНомеровТип <> 0) Тогда
			
			Если НЕ ((строкаНомераВременный.ИндексКартинкиСтатуса = 2) ИЛИ
					(строкаНомераВременный.ИндексКартинкиСтатуса = 5) ИЛИ
					(строкаНомераВременный.ИндексКартинкиСтатуса = 6)) Тогда
				Продолжить; // не разговор, не входящий и не удержание
			КонецЕсли;
			
			Если ФильтрНомеровВнешние Тогда
				Если НЕ строкаНомераВременный.Внешний Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Если ФильтрНомеровТип = 1 Тогда
				Если НЕ строкаНомераВременный.Входящий Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Если ФильтрНомеровТип = 2 Тогда
				Если строкаНомераВременный.Входящий Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		строкаНомераПослеФильтра = Новый Структура();
		Для Каждого полеСтруктуры Из строкаНомераВременный Цикл
			строкаНомераПослеФильтра.Вставить(полеСтруктуры.Ключ, полеСтруктуры.Значение);
		КонецЦикла;
		массНомераПослеФильтра.Добавить(строкаНомераПослеФильтра);
	КонецЦикла;
	
	// изменение размера таблицы
	ИзменитьРазмерТаблицы(Номера, массНомераПослеФильтра.Количество());
	
	// сортировка
	БыстраяСортировка(массНомераПослеФильтра, ПолеСортировки, НаправлениеСортировки);
	
	Для й=1 По Номера.Количество() Цикл
		СтрокаТаблНомеров = Номера[й-1];
		строкаНомераПослеФильтра = массНомераПослеФильтра[й-1];
		ЗаполнитьЗначенияСвойств(СтрокаТаблНомеров, строкаНомераПослеФильтра);
		Если ЗначениеЗаполнено(СтрокаТаблНомеров.Соединен) Тогда
			// поиск контрагента по номеру
			КонтрагентСсылка = Неопределено;
			КонтактноеЛицоСсылка = Неопределено;
			бит_АТСКлиент.НайтиКонтрагентаПоСокращНомеру(кэшКонтактов, СоответствиеНомеровХоста, СтрокаТаблНомеров.Соединен,
														СтрокаТаблНомеров.Внешний, КонтрагентСсылка, КонтактноеЛицоСсылка);
			СтрокаТаблНомеров.Контрагент = КонтрагентСсылка;
			СтрокаТаблНомеров.КонтактноеЛицо = КонтактноеЛицоСсылка;
		Иначе
			СтрокаТаблНомеров.Контрагент = "";
			СтрокаТаблНомеров.КонтактноеЛицо = "";
		КонецЕсли;
	КонецЦикла;
	
	//
	//СортироватьТаблицуНомеров();
	ВосстановитьВыделениеСтрокиТаблицыНомеров(ТекущаяСтрокаНомеровИнф, ИндексТекущейСтрокиНомеров);
	
	/////////////////////////////////////
	// Звонки
	/////////////////////////////////////
	
	ТекущаяСтрокаЗвонков = Элементы.Звонки.ТекущиеДанные;
	ТекущаяСтрокаЗвонковСтрукт = Неопределено;
	Если ТекущаяСтрокаЗвонков <> Неопределено Тогда
		ТекущаяСтрокаЗвонковСтрукт = Новый Структура();
		ТекущаяСтрокаЗвонковСтрукт.Вставить("От");
		ТекущаяСтрокаЗвонковСтрукт.Вставить("Кому");
		ЗаполнитьЗначенияСвойств(ТекущаяСтрокаЗвонковСтрукт, ТекущаяСтрокаЗвонков);
	КонецЕсли;
	
	списокАктивныхЗвонков = соотвИнфо.Получить("ActiveCalls");
	
	КолвоЗвонков = ? (списокАктивныхЗвонков = Неопределено, 0, списокАктивныхЗвонков.Количество());
	
	ИзменитьРазмерТаблицы(Звонки, КолвоЗвонков);
	
	Для й = 0 По (КолвоЗвонков - 1) Цикл
		обАктивныйЗвонок = списокАктивныхЗвонков[й];
		//
		НомерЗвОт = обАктивныйЗвонок.Получить("From");
		КонтрагентОтСсылка = Неопределено;
		КонтактноеЛицоОтСсылка = Неопределено;
		бит_АТСКлиент.СократитьНомерНайтиКонтрагента(кэшКонтактов, СоответствиеНомеровХоста, НомерЗвОт, КонтрагентОтСсылка, КонтактноеЛицоОтСсылка);
		//
		НомерЗвКому = обАктивныйЗвонок.Получить("To");
		КонтрагентКомуСсылка = Неопределено;
		КонтактноеЛицоКомуСсылка = Неопределено;
		бит_АТСКлиент.СократитьНомерНайтиКонтрагента(кэшКонтактов, СоответствиеНомеровХоста, НомерЗвКому, КонтрагентКомуСсылка, КонтактноеЛицоКомуСсылка);
		//
		ЗвонокДлительность	= обАктивныйЗвонок.Получить("Duration");
		ЗвонокSIPЛогин		= обАктивныйЗвонок.Получить("SIPLogin");
		ЗвонокSpyНомер		= обАктивныйЗвонок.Получить("SpyNumber");
		//
		СтрокаТаблЗвонков = Звонки[й];
		СтрокаТаблЗвонков.От				= НомерЗвОт;
		СтрокаТаблЗвонков.ОтКонтрагент		= КонтрагентОтСсылка;
		СтрокаТаблЗвонков.ОтКонтактноеЛицо	= КонтактноеЛицоОтСсылка;
		СтрокаТаблЗвонков.Кому				= НомерЗвКому;
		СтрокаТаблЗвонков.КомуКонтрагент	= КонтрагентКомуСсылка;
		СтрокаТаблЗвонков.КомуКонтактноеЛицо= КонтактноеЛицоКомуСсылка;
		СтрокаТаблЗвонков.Длительность		= ЗвонокДлительность;
		СтрокаТаблЗвонков.SIPЛогин			= ЗвонокSIPЛогин;
		СтрокаТаблЗвонков.SpyНомер			= ЗвонокSpyНомер;
	КонецЦикла;
	
	ВсегоЗвонков = КолвоЗвонков;
	
	Звонки.Сортировать("Длительность Убыв");
	
	Если ТекущаяСтрокаЗвонковСтрукт <> Неопределено Тогда
		массЗвонки = Звонки.НайтиСтроки(ТекущаяСтрокаЗвонковСтрукт);
		Если массЗвонки.Количество() > 0 Тогда
			найденныйЗвонок = массЗвонки[0];
			Элементы.Звонки.ТекущаяСтрока = найденныйЗвонок.ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
	/////////////////////////////////////
	// Транки
	/////////////////////////////////////
	
	списокУзловТранки = соотвИнфо.Получить("Trunks");
	КолвоТранков = ? (списокУзловТранки = Неопределено, 0, списокУзловТранки.Количество());
	Если КолвоТранков > 0 Тогда
		ИзменитьРазмерТаблицы(ЗагрузкаТранков, КолвоТранков);
		массТранки = Новый Массив();
		Для Каждого узТранк Из списокУзловТранки Цикл
			стрПирТранкаСПрефиксом = узТранк.Ключ;
			обТранк = узТранк.Значение;
			ИмяТранка = обТранк.Получить("Name");
			внешниеЗвонкиТранка = обТранк.Получить("ExternalCalls");
			структТранк = Новый Структура();
			структТранк.Вставить("Наименование", ИмяТранка);
			структТранк.Вставить("КоличествоЗвонков", Строка(внешниеЗвонкиТранка.Количество()));
			структТранк.Вставить("ПирТранкаСПрефиксом", стрПирТранкаСПрефиксом);
			массТранки.Добавить(структТранк);
		КонецЦикла;
		БыстраяСортировка(массТранки, "Наименование", "Возр");
		й = 0;
		Для Каждого строкаЗагрузкаТранков Из ЗагрузкаТранков Цикл
			ЗаполнитьЗначенияСвойств(строкаЗагрузкаТранков, массТранки[й]);
			й = й + 1;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтатусНомераВМассив(ИмяЭкст, НомерЭкст, СтатусЭкст, ГруппаДоступаЭкст, СипУстройствоНомера,
				НаправлениеЗвонка, СоединениеНомера, ДлительностьРазговора, ВнешнийВызов, ВходящийЗвонок,
				ИндексКартинкиСтатуса, Канал, СвязКанал, НабранныйНомер, массивНомеров)
			
	строкаНомераСтрукт = Новый Структура();
	
	строкаНомераСтрукт.Вставить("Наименование",				ИмяЭкст);
	строкаНомераСтрукт.Вставить("Номер", 					НомерЭкст);
	строкаНомераСтрукт.Вставить("Статус",					СтатусЭкст);
	строкаНомераСтрукт.Вставить("ГруппаДоступа",			ГруппаДоступаЭкст);
	строкаНомераСтрукт.Вставить("SIPЛогин",					СипУстройствоНомера);
	строкаНомераСтрукт.Вставить("Направление",				НаправлениеЗвонка);
	строкаНомераСтрукт.Вставить("Соединен",					СоединениеНомера);
	строкаНомераСтрукт.Вставить("Длительность",				ДлительностьРазговора);
	строкаНомераСтрукт.Вставить("Внешний",					ВнешнийВызов);
	строкаНомераСтрукт.Вставить("Входящий",					ВходящийЗвонок);
	строкаНомераСтрукт.Вставить("ИндексКартинкиСтатуса",	ИндексКартинкиСтатуса);
	строкаНомераСтрукт.Вставить("Канал",					Канал);
	строкаНомераСтрукт.Вставить("СвязКанал",				СвязКанал);
	строкаНомераСтрукт.Вставить("НабранныйНомер",			НабранныйНомер);
	
	массивНомеров.Добавить(строкаНомераСтрукт);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРазмерТаблицы(ТаблицаФормы, НовыйРазмер)
	Если ТаблицаФормы.Количество() <> НовыйРазмер Тогда
		разницаРазмеров = ТаблицаФормы.Количество()- НовыйРазмер;
		разницаРазмеров = ? (разницаРазмеров < 0, -разницаРазмеров, разницаРазмеров);
		Если ТаблицаФормы.Количество() < НовыйРазмер Тогда
			Для й=1 По разницаРазмеров Цикл
				ТаблицаФормы.Добавить();
			КонецЦикла;
		Иначе
			Для й=1 По разницаРазмеров Цикл
				ТаблицаФормы.Удалить(0);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТаблицы()
	
	Попытка
		
		ИмяТекСтраницы = Элементы.ГруппаАТС.ТекущаяСтраница.Имя;
		
		Если ИспользоватьВебСервис Тогда

			Событие = "";
			соотвИнфо = Неопределено;
			бит_АТСКлиент.ОчиститьФлагОбновленВсегоКэшаКонтактов(кэшКонтактов);
			Если ИмяТекСтраницы = "ГруппаНомера" ИЛИ ИмяТекСтраницы = "ГруппаЗвонки" ИЛИ ИмяТекСтраницы = "ГруппаТранки" Тогда
				соотвИнфо = бит_АТССервер.ВебСервисОбновитьНомера(ТекущаяАТС, ВебСервисИдКлиента);
				ЗаполнитьТаблицыНомеровЗвонковТранков(соотвИнфо);
				Событие = "ОбновлениеНомеров";
			ИначеЕсли ИмяТекСтраницы = "ГруппаОчереди" ИЛИ ИмяТекСтраницы = "ГруппаОжидающие" ИЛИ ИмяТекСтраницы = "ГруппаКлиентскиеГруппы" Тогда
				соотвИнфо = бит_АТССервер.ВебСервисОбновитьОчереди(ТекущаяАТС, ВебСервисИдКлиента);
				ЗаполнитьТаблицыОчередейИОжидающих(соотвИнфо);
				Событие = "ОбновлениеОчередей";
			КонецЕсли;
			бит_АТСКлиент.УдалитьНеобновленныеКонтактыИзКэша(кэшКонтактов);
			бит_АТСКлиент.ПроверкаОшибокВебСервиса(ТекущаяАТС, ИмяИсточникаСобытий(), соотвИнфо, ЭтаФорма);
			ЗапуститьТаймерОбновления(ИнтервалОбновления);
			Оповестить(Событие, соотвИнфо, ИмяИсточникаСобытий());
			
		Иначе
		
			Если ИмяТекСтраницы = "ГруппаНомера" ИЛИ ИмяТекСтраницы = "ГруппаЗвонки" ИЛИ ИмяТекСтраницы = "ГруппаТранки" Тогда
				
				обКонтроллерАТС.GetExtensionsStatusesAndChannels();
				
			ИначеЕсли ИмяТекСтраницы = "ГруппаОчереди" ИЛИ ИмяТекСтраницы = "ГруппаОжидающие" ИЛИ ИмяТекСтраницы = "ГруппаКлиентскиеГруппы" Тогда
				
				обКонтроллерАТС.GetQueues();
				
			Иначе
				//
			КонецЕсли;
			
		КонецЕсли;
		
	Исключение
		
		Если ИспользоватьВебСервис Тогда
			ОбработкаОшибкиВебСервиса(ОписаниеОшибки());
		Иначе
			бит_ТелефонияКлиент.ВывестиСообщение(ОписаниеОшибки(), ЭтаФорма);
		КонецЕсли;
		
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьТекущуюСтрокуНомеровСтрукт()
	структураИнф = Неопределено;
	ТекущаяСтрокаНомеров = Элементы.Номера.ТекущиеДанные;
	Если ТекущаяСтрокаНомеров <> Неопределено Тогда
		структураИнф = Новый Структура();
		структураИнф.Вставить("Наименование");
		структураИнф.Вставить("Номер");
		структураИнф.Вставить("SIPЛогин");
		ЗаполнитьЗначенияСвойств(структураИнф, ТекущаяСтрокаНомеров);
	КонецЕсли;
	Возврат структураИнф;
КонецФункции

&НаКлиенте
Процедура СортироватьТаблицуНомеров()
	СтрокаСортировки = ПолеСортировки + " " + НаправлениеСортировки;
	Номера.Сортировать(СтрокаСортировки);
КонецПроцедуры
	
&НаКлиенте
Процедура ВосстановитьВыделениеСтрокиТаблицыНомеров(ТекущаяСтрокаНомеровИнф, ИндексТекущейСтрокиНомеров)
	
	идентВыделСтроки = Неопределено;
	
	Если ПолеСортировки = "Наименование" ИЛИ ПолеСортировки = "Номер" Тогда
		Если ТекущаяСтрокаНомеровИнф <> Неопределено Тогда
			массНомера = Номера.НайтиСтроки(ТекущаяСтрокаНомеровИнф);
			Если массНомера.Количество() > 0 Тогда
				найденныйНомер = массНомера[0];
				идентВыделСтроки = найденныйНомер.ПолучитьИдентификатор();
			Иначе
				Если Номера.Количество() > 0 Тогда
					Строка0 = Номера[0];
					идентВыделСтроки = Строка0.ПолучитьИдентификатор();
				КонецЕсли;
			КонецЕсли;
		Иначе
			Если Номера.Количество() > 0 Тогда
				Строка0 = Номера[0];
				идентВыделСтроки = Строка0.ПолучитьИдентификатор();
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если (ИндексТекущейСтрокиНомеров >= 0) И (ИндексТекущейСтрокиНомеров < Номера.Количество()) Тогда
			ВыделеннаяСтрока = Номера[ИндексТекущейСтрокиНомеров];
			идентВыделСтроки = ВыделеннаяСтрока.ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
	Элементы.Номера.ТекущаяСтрока = идентВыделСтроки;
	
КонецПроцедуры

&НаКлиенте
Процедура СортироватьТаблицуНомеровССохранениемТекущейСтроки()
	ТекущаяСтрокаНомеровИнф = ПолучитьТекущуюСтрокуНомеровСтрукт();
	ИндексТекущейСтрокиНомеров = Номера.Индекс(Элементы.Номера.ТекущиеДанные);
	СортироватьТаблицуНомеров();
	ВосстановитьВыделениеСтрокиТаблицыНомеров(ТекущаяСтрокаНомеровИнф, ИндексТекущейСтрокиНомеров);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДетальнуюИнформациюОчереди(ОчередьИд, ОчередьИмя, ОчередьНомер)
	формаИнфОчереди = ПолучитьФорму("Обработка.бит_БитАТС.Форма.ИнформацияОчередь");
	формаИнфОчереди.ИсточникВнешнихСобытий	= ИмяИсточникаСобытий();
	формаИнфОчереди.ИдентификаторОчереди	= ОчередьИд;
	формаИнфОчереди.ИмяОчереди				= ОчередьИмя;
	формаИнфОчереди.НомерОчереди			= ОчередьНомер;
	формаИнфОчереди.НомерСвязанногоТелефона	= НомерСвязанногоТелефона;
	формаИнфОчереди.ИспользоватьВебСервис	= ИспользоватьВебСервис;
	формаИнфОчереди.ВебСервисИдКлиента		= ВебСервисИдКлиента;
	формаИнфОчереди.УстановитьКонтроллерАТС(обКонтроллерАТС);
	формаИнфОчереди.УстановитьСоответствиеНомеров(СоответствиеНомеровХоста, кэшКонтактов);
	бит_ТелефонияКлиентПереопределяемый.ОткрытьФормуСБлокировкойВладельца(формаИнфОчереди);
КонецПроцедуры

&НаКлиенте
Функция ПроверкаСтатусаСвободен()
	Свободен = Ложь;
	Если Элементы.Номера.ТекущиеДанные = Неопределено Тогда
		бит_ТелефонияКлиентПереопределяемый.ПоказПредупреждение("Не выбран номер");
	Иначе
		Свободен = (Элементы.Номера.ТекущиеДанные.ИндексКартинкиСтатуса = 1);
		Если НЕ Свободен Тогда
			бит_ТелефонияКлиентПереопределяемый.ПоказПредупреждение("Звонок возможен только на свободный номер");
		КонецЕсли;
	КонецЕсли;
	Возврат Свободен;
КонецФункции

&НаКлиенте
Функция ПроверкаСтатусаРазговаривает()
	Разговаривает = Ложь;
	Если Элементы.Номера.ТекущиеДанные = Неопределено Тогда
		бит_ТелефонияКлиентПереопределяемый.ПоказПредупреждение("Не выбран номер");
	Иначе
		ИндексКартинкиСтатуса = Элементы.Номера.ТекущиеДанные.ИндексКартинкиСтатуса;
		Если (ИндексКартинкиСтатуса = 2) ИЛИ (ИндексКартинкиСтатуса = 5) ИЛИ (ИндексКартинкиСтатуса = 6) Тогда
			// статусы разговаривает, входящий звонок, удержание
			Разговаривает = Истина;
		ИначеЕсли ИндексКартинкиСтатуса = 3 Тогда
			// дополнительная проверка для режима DND
			Разговаривает = ЗначениеЗаполнено(Элементы.Номера.ТекущиеДанные.Соединен);
		КонецЕсли;
		Если НЕ Разговаривает Тогда
			бит_ТелефонияКлиентПереопределяемый.ПоказПредупреждение("На выбранном номере нет идущего разговора");
		КонецЕсли;
	КонецЕсли;
	Возврат Разговаривает;
КонецФункции

&НаКлиенте
Процедура НачатьЗвонок(ВызываемыйНомер)
	
	бит_АТСКлиент.НачатьЗвонок(Подключен, НомерСвязанногоТелефона, ВызываемыйНомер, обКонтроллерАТС,
		ИспользоватьВебСервис, ВебСервисИдКлиента, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьПараметрыТекущегоРазговора(SIPЛогинТекущ, НомерТекущ, ГруппаДоступаТекущ)
	ИмяТекСтраницы = Элементы.ГруппаАТС.ТекущаяСтраница.Имя;
	получены = Истина;
	Если ИмяТекСтраницы = "ГруппаНомера" Тогда
		получены = ПроверкаСтатусаРазговаривает();
		Если получены Тогда
			SIPЛогинТекущ		= Элементы.Номера.ТекущиеДанные.SIPЛогин;
			НомерТекущ			= Элементы.Номера.ТекущиеДанные.Номер;
			ГруппаДоступаТекущ	= Элементы.Номера.ТекущиеДанные.ГруппаДоступа;
		КонецЕсли;
	ИначеЕсли ИмяТекСтраницы = "ГруппаЗвонки" Тогда
		Если Элементы.Звонки.ТекущиеДанные = Неопределено Тогда
			бит_ТелефонияКлиентПереопределяемый.ПоказПредупреждение("Не выбран звонок");
			получены = Ложь;
		Иначе
			SIPЛогинТекущ		= Элементы.Звонки.ТекущиеДанные.SIPЛогин;
			НомерТекущ			= Элементы.Звонки.ТекущиеДанные.SpyНомер;
			ГруппаДоступаТекущ	= Неопределено;
			Если Найти(НомерТекущ, "SIP/") > 0 Тогда
				НомерТекущ = НомерСвязанногоТелефона;
			КонецЕсли;
		КонецЕсли;
	Иначе
		получены = Ложь;
	КонецЕсли;
	Возврат получены;
КонецФункции

&НаКлиенте
Процедура ПодключитьсяКРазговору(РежимПодключения)
	
	Если НЕ ЗначениеЗаполнено(НомерСвязанногоТелефона) Тогда
		бит_ТелефонияКлиентПереопределяемый.ПоказПредупреждение("Не задан номер связанного телефона", , "Ошибка");
		Возврат;
	КонецЕсли;
	
	// Подключаться без проверки параметров текущего разговора	
	// ПодключаемыйSIPЛогин = "";
	// ПодключаемыйНомер = "";
	// ГруппаДоступаНомера = Неопределено;
	
	// параметрыТекРазгПолучены = ПолучитьПараметрыТекущегоРазговора(ПодключаемыйSIPЛогин, ПодключаемыйНомер, ГруппаДоступаНомера);
	// Если НЕ параметрыТекРазгПолучены Тогда
		// бит_ТелефонияКлиент.ВывестиСообщение("Не удалось получить параметры текущего разговора", ЭтаФорма);
		// Возврат;
	// КонецЕсли;	
	ПодключаемыйSIPЛогин = Элементы.Номера.ТекущиеДанные.SIPЛогин;
	ПодключаемыйНомер = Элементы.Номера.ТекущиеДанные.Номер;
	ГруппаДоступаНомера = Неопределено;
	
	УправлениеРазрешено = ПроверитьДоступностьПодключенияКРазговоруСервер(ГруппаДоступаНомера);
	
	Если НЕ УправлениеРазрешено Тогда
		бит_ТелефонияКлиентПереопределяемый.ПоказПредупреждение("Недостаточно прав для подключения к разговору");
		Возврат;
	КонецЕсли;
	
	ТаймаутЗвонка = бит_АТСКлиент.ПолучитьТаймаутЗвонка();
	
	РежимВнеОфиса = бит_АТССервер.ПолучитьФлагРежимВнеОфиса();
	
	ЗаголовокАвтоподнятия = бит_АТССервер.ПолучитьФлагЗаголовокАвтоподнятия();
	
	СлушающийНомер = НомерСвязанногоТелефона;
	
	стрТекстОповещ = "Прослушивание номера '" + ПодключаемыйНомер + "'";
	
	Если РежимВнеОфиса Тогда
		стрНомерВнеОфиса = бит_АТСКлиент.ПолучитьНомерВнеОфисаПолный();
		Если НЕ ЗначениеЗаполнено(стрНомерВнеОфиса) Тогда
			Возврат;
		КонецЕсли;
		
		СлушающийНомер = стрНомерВнеОфиса;
		
		стрТекстОповещ = стрТекстОповещ + " с внешнего телефона";
	КонецЕсли;
	
	ПоказатьОповещениеПользователя("Подключение к разговору...", , стрТекстОповещ);
	
	Попытка
		Если ИспользоватьВебСервис Тогда
			бит_АТССервер.ВебСервисПодключитьсяКРазговору(ТекущаяАТС, ВебСервисИдКлиента, НомерСвязанногоТелефона, СлушающийНомер,
				ПодключаемыйSIPЛогин, ПодключаемыйНомер, РежимПодключения, ЗаголовокАвтоподнятия, ТаймаутЗвонка);
		Иначе
			обКонтроллерАТС.Spy2(СлушающийНомер, НомерСвязанногоТелефона, ПодключаемыйSIPЛогин, ПодключаемыйНомер, РежимПодключения,
				ЗаголовокАвтоподнятия, ТаймаутЗвонка);
		КонецЕсли;
	Исключение
		бит_ТелефонияКлиент.ВывестиСообщение( ОписаниеОшибки(), ЭтаФорма );
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораГруппыДереваНомеров()
	Если Хост <> Неопределено Тогда 
		Хост.Номера = бит_АТССервер.ПолучитьНомераАТС(ТекущаяАТС, Элементы.ДеревоНомеров.ТекущаяСтрока);
		ОбновитьТаблицы();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОшибкиВебСервиса(стрОписаниеИсключения)
	бит_АТСКлиент.ОбработкаОшибки(ТекущаяАТС, "ВебСервисУправлениеАТС", "ВебСервисУправлениеАТС_Ошибка", стрОписаниеИсключения, ЭтаФорма);
	ПроверкаВосстановленияПодключения();
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаВосстановленияПодключения()
	
	Если Подключен Тогда
		ВосстановлениеПодключения = Истина;
	КонецЕсли;
	
	Отключить();
	
	Если ВосстановлениеПодключения Тогда
		бит_ТелефонияКлиент.ВывестиСообщение("Соединение с АТС потеряно, попытка переподключения...", ЭтаФорма);
		ПодключитьОбработчикОжидания("Подключить", 10, Истина);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти
