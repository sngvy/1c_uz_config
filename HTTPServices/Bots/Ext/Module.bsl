
Функция ПолучениеСообщенийТелеграмНовыеСообщения(Запрос)
	
	Попытка
	
		Ответ = ОбработкаОтвета(Запрос);
	
	Исключение
		Ответ = ОтветОшибкаСервера();
	КонецПопытки;

	Возврат Ответ;
	
КонецФункции

Функция ОбработкаОтвета(Запрос)

	Логер = БЛогер_Логирование.Создать();
	
	ТекстЗапроса = Запрос.ПолучитьТелоКакДвоичныеДанные();
	
	Попытка
	
		Элемент = Справочники.НастройкиЧатовТелеграм.Телеграм;
	
	Исключение
		Ошибка = ОбработкаОшибок
					.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		БЛогер_Логирование.Добавить(
			Логер,
			"Нет настройки для обработки телеграм сообщений: " + Ошибка,
			Перечисления.БЛогер_Уровни.Ошибка);
		ВызватьИсключение Ошибка;
	КонецПопытки;
	
	Попытка
	
		Данные = Справочники.НастройкиЧатовТелеграм
			.ОбработатьПолучениеСообщений(ТекстЗапроса);
	
	Исключение
		Ошибка = ОбработкаОшибок
					.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		БЛогер_Логирование.Добавить(
			Логер,
			"Ошибка обработки данных: " + Ошибка,
			Перечисления.БЛогер_Уровни.Ошибка);
		ВызватьИсключение Ошибка;
	КонецПопытки;

	МодульХранения = КонфигурацияИсторияЧатов.МодульХраненияИсторииЧатовПоУмолчанию();
	
	Попытка
	
		Обработки.СохранениеИсторииЧатов.ОбработкаПолученияРезультата(
			Данные,
			Элемент,
			МодульХранения);
	
	Исключение
		Ошибка = ОбработкаОшибок
					.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		БЛогер_Логирование.Добавить(
			Логер,
			"Ошибка сохранения истории: " + Ошибка,
			Перечисления.БЛогер_Уровни.Ошибка);
		ВызватьИсключение Ошибка;
	КонецПопытки;
	
	Возврат ОтветУспех();

КонецФункции // ()


#Область ОбработкаОтветов

Функция ОтветУспех()

	Возврат Новый HTTPСервисОтвет(200);

КонецФункции // ()

Функция ОтветОшибкаСервера()

	Возврат Новый HTTPСервисОтвет(500);

КонецФункции // ()

#КонецОбласти