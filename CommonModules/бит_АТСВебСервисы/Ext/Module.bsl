///////////////////////////////////////////////////////////////////////////////
// Общий серверный модуль работы с веб-сервисами
///////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьВебСервисУправлениеАТС(ссылкаАТС) ЭКСПОРТ
	
	Если НЕ ЗначениеЗаполнено(ссылкаАТС) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	стрВебСервис = ссылкаАТС.ВебСервис;
	Если Не ЗначениеЗаполнено(стрВебСервис) Тогда
		стрАдресАМИ = ссылкаАТС.Адрес;
	Иначе
		стрАдресАМИ = "";
	КонецЕсли;
	
	Возврат ПолучитьВебСервисУправлениеЗвонками(стрВебСервис, стрАдресАМИ);
	
КонецФункции

Функция ПолучитьВебСервисУправлениеЗвонками(стрВебСервис, стрАдресАМИ) ЭКСПОРТ
	
	стрАдресАТС = стрВебСервис;
	
	Если Не ЗначениеЗаполнено(стрАдресАТС) И ЗначениеЗаполнено(стрАдресАМИ) Тогда
		стрХостАМИ = стрАдресАМИ;
		индПорт = СтрНайти(стрАдресАМИ, ":");
		Если индПорт > 0 Тогда
			стрХостАМИ = Лев(стрХостАМИ, индПорт-1);
		КонецЕсли;
		стрАдресАТС = СформироватьПолныйАдресАТС(стрХостАМИ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(стрАдресАТС) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ПолучитьВебСервисАТС(стрАдресАТС, "PbxControlWebService");
	
КонецФункции

Функция ПолучитьВебСервисАТС(стрАдресАТС, стрВебСервис) ЭКСПОРТ
	
	Если НЕ ЗначениеЗаполнено(стрАдресАТС) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ссл = Новый ЗащищенноеСоединениеOpenSSL;
	Определения = Новый WSОпределения(стрАдресАТС + "/" + стрВебСервис + "?wsdl", , , , 3, ссл);
	Сервис = Определения.Сервисы[0];
	вс = Новый WSПрокси(Определения, Сервис.URIПространстваИмен, Сервис.Имя, Сервис.ТочкиПодключения[0].Имя, , 3, ссл);
	Возврат вс;
	
КонецФункции

Функция ПолучитьВебСервисКлиентМенеджер(стрХост) ЭКСПОРТ
	
	Если НЕ ЗначениеЗаполнено(стрХост) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	стрАдресАТС = СформироватьПолныйАдресАТС(стрХост);
	
	Возврат ПолучитьВебСервисАТС(стрАдресАТС, "ClientManagerControlWebService");
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьПортВебСервисаПоУмолчанию()
	Возврат "2021";
КонецФункции

Функция СформироватьПолныйАдресАТС(стрХост)
	Возврат "https://" + стрХост + ":" + ПолучитьПортВебСервисаПоУмолчанию();
КонецФункции

#КонецОбласти
