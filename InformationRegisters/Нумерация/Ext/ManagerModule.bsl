
#Область ПрограммныйИнтерфейс

Функция ЗаписьНаСегодня(Нумератор, Организация) Экспорт

	Периодичность = Перечисления.ПериодичностьНумераторов.Непериодический;
	Если Нумератор <> Неопределено
		И Нумератор <> Справочники.Нумераторы.ПустаяСсылка() Тогда
	
		Периодичность = Нумератор.Периодичность;
	
	КонецЕсли;
	ПериодНумерации = НачалоПериодаНумерации(
		Периодичность,
		ТекущаяДатаСеанса()
	);
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Нумератор = Нумератор;
	МенеджерЗаписи.Организация = Организация;
	МенеджерЗаписи.ПериодНумерации = ПериодНумерации;
	МенеджерЗаписи.Прочитать();
	
	МенеджерЗаписи.Нумератор = Нумератор;
	МенеджерЗаписи.Организация = Организация;
	МенеджерЗаписи.ПериодНумерации = ПериодНумерации;
	
	Возврат МенеджерЗаписи;

КонецФункции // ()

Функция НовыйНомер(Нумератор, Организация) Экспорт

	МенеджерЗаписи = ЗаписьНаСегодня(Нумератор, Организация);
	
	МенеджерЗаписи.ТекущийНомер = МенеджерЗаписи.ТекущийНомер + 1;
	МенеджерЗаписи.Записать(Истина);
	
	Возврат МенеджерЗаписи.ТекущийНомер;

КонецФункции // ()

Функция Составитель(Нумератор) Экспорт

	Если Не ЗначениеЗаполнено(Нумератор) Тогда
	
		ВызватьИсключение УправленияСообщениямиКлиентСервер.НеКорректныйПараметр(
			"Передан пустой нумератор"
		);
	
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Нумератор.ФункцияНумерации) Тогда
	
		ВызватьИсключение УправленияСообщениямиКлиентСервер.НеКорректныйПараметр(
			"У нумератора не задана функция нумерации"
		);
	
	КонецЕсли;
	
	Возврат Нумератор.ФункцияНумерации.Функция;

КонецФункции // ()


Функция СформироватьНомерЕСИА(Элемент, Организация) Экспорт

	Возврат СформироватьНомер(Элемент, Организация, Организация);

КонецФункции // ()

Функция СформироватьНомер(Элемент, Организация, Объект) Экспорт

	НовыйНомер = НовыйНомер(Элемент, Организация);
	СоставительНомера = Составитель(Элемент);
	
	Возврат Справочники.ШаблоныТекстаДляАвтоинформирования.ВычислитьФункцию(
		СоставительНомера,
		Объект,
		НовыйНомер
	);

КонецФункции // ()



#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция НачалоПериодаНумерации(Периодичность, ДатаНомера) Экспорт
	
	Если Периодичность = Перечисления.ПериодичностьНумераторов.День Тогда
		ПериодНумерации = НачалоДня(ДатаНомера);
		
	ИначеЕсли Периодичность = Перечисления.ПериодичностьНумераторов.Месяц Тогда
		ПериодНумерации = НачалоМесяца(ДатаНомера);
		
	ИначеЕсли Периодичность = Перечисления.ПериодичностьНумераторов.Квартал Тогда
		ПериодНумерации = НачалоКвартала(ДатаНомера);
		
	ИначеЕсли Периодичность = Перечисления.ПериодичностьНумераторов.Год Тогда
		ПериодНумерации = НачалоГода(ДатаНомера);
		
	ИначеЕсли Периодичность = Перечисления.ПериодичностьНумераторов.Непериодический Тогда 
		ПериодНумерации = Дата(1, 1, 1);
		
	Иначе
		ПериодНумерации = Дата(1, 1, 1);
	КонецЕсли;
	
	Возврат ПериодНумерации;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции



#КонецОбласти