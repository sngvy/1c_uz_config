
Процедура ЗагрузкаКлючевойСтавки() Экспорт

	ХранениеСтавок = РегистрыСведений.АЭ_ЗначенияВидовПроцентныхСтавок;
	ДатаЗначениеСтавки = ХранениеСтавок.ПолучитьПоследниеДанныеКлючевойСтавки();
	
	доДаты = НачалоМесяца(ТекущаяДата());
	отДаты = Макс(ДатаЗначениеСтавки.Дата, Дата(2016, 1, 1));
	
	ЗначенияСтавки = ОбращениеСЦентробанкомНаСервере.ПолучитьКлючевыеСтавкиЗаПериод(отДаты, доДаты);
	тзЗначениеСтавки = ПеревестиВТаблицуЗначенийИзСписокаXDTO_ОтсортироватьПоДате(ЗначенияСтавки);
	
	ПоследнееЗначениеСтавки = ДатаЗначениеСтавки.Значение;

	Для каждого СтрокаСтавка Из тзЗначениеСтавки Цикл
	
		Если СтрокаСтавка.Значение <> ПоследнееЗначениеСтавки Тогда
		
			ХранениеСтавок.ДобавитьНовоеЗначениеКлючевойСтавки(СтрокаСтавка);
			ПоследнееЗначениеСтавки = СтрокаСтавка.Значение;
		
		КонецЕсли;
	
	КонецЦикла;

КонецПроцедуры

#Область ВспомогательныеКлючеваяСтавка

Функция ПеревестиВТаблицуЗначенийИзСписокаXDTO_ОтсортироватьПоДате(ЗначенияСтавки)

	тзЗначенияСтавки = Новый ТаблицаЗначений;
	тзЗначенияСтавки.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
	тзЗначенияСтавки.Колонки.Добавить("Значение", Новый ОписаниеТипов("Число"));
	
	Для каждого Ставка Из ЗначенияСтавки Цикл
	
		СтрокаСтавка = тзЗначенияСтавки.Добавить();
		СтрокаСтавка.Дата = ОбращениеСЦентробанкомНаСервере.ПолучитьДатуИзСтрокиCBR(Ставка.DT);
		СтрокаСтавка.Значение = ОбращениеСЦентробанкомНаСервере.ПолучитьЧислоИзСтрокиCBR(Ставка.Rate);
	
	КонецЦикла;
	
	тзЗначенияСтавки.Сортировать("Дата Возр");
	
	Возврат тзЗначенияСтавки;

КонецФункции

#КонецОбласти