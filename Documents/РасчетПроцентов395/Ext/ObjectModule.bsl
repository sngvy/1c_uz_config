
Процедура ЗагрузитьГрафикНачислений() Экспорт

	Остатки = Неопределено; 
	ЭтотОбъект.РасчетПроцентов.Очистить();
	
	СписокПлатежей = ЭтотОбъект.ИсторияПлатежей.Выгрузить();
	СписокПлатежей.Сортировать("ДатаПлатежа");
	РассчитатьГрафикИндексации(ЭтотОбъект, СписокПлатежей); 

КонецПроцедуры

Процедура ЗаполнитьДанные() Экспорт
	//ЭтотОбъект.ИсторияПлатежей.Очистить();
	ЭтотОбъект.Территория = "Свердловская область";
	ЭтотОбъект.ДатаРешения = ОбъектыСервер.ПолучитьЗначениеСвойства(ЭтотОбъект.Займ, "0498     ");
	ЭтотОбъект.СуммаЗадолженности = ЭтотОбъект.Займ.Замена_СуммаПоСудАкту;
	
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	НАЧАЛОПЕРИОДА(ЗадолженностьПоОбъектам.Период, ДЕНЬ) КАК ДатаПлатежа,
	                      |	ЗадолженностьПоОбъектам.СуммаДО КАК СуммаПлатежа
	                      |ИЗ
	                      |	РегистрНакопления.ЗадолженностьПоОбъектам КАК ЗадолженностьПоОбъектам
	                      |ГДЕ
	                      |	ЗадолженностьПоОбъектам.Объект = &Объект
	                      |	И ТИПЗНАЧЕНИЯ(ЗадолженностьПоОбъектам.Регистратор) = ТИП(документ.ПоступлениеПлатежей)");
	Запрос.УстановитьПараметр("Объект", ЭтотОбъект.Займ);
	ЭтотОбъект.ИсторияПлатежей.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура РассчитатьГрафикИндексации(стрДанные, СписокПлатежей)

	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	СписокПлатежей.СуммаПлатежа КАК СуммаПлатежа,
	                      |	СписокПлатежей.ДатаПлатежа КАК ДатаПлатежа
	                      |ПОМЕСТИТЬ вт_Платежи
	                      |ИЗ
	                      |	&СписокПлатежей КАК СписокПлатежей
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	вт_Платежи.ДатаПлатежа КАК ДатаПлатежа,
	                      |	вт_Платежи.СуммаПлатежа КАК СуммаПлатежа,
	                      |	НАЧАЛОПЕРИОДА(вт_Платежи.ДатаПлатежа, МЕСЯЦ) КАК МесяцСписания
	                      |ИЗ
	                      |	вт_Платежи КАК вт_Платежи
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	МесяцСписания,
	                      |	ДатаПлатежа");
	Запрос.УстановитьПараметр("СписокПлатежей", СписокПлатежей);
	СписокПлатежей = Запрос.Выполнить().Выгрузить();
	
	БазаНачисления = стрДанные.СуммаЗадолженности;
	КУплате = стрДанные.СуммаЗадолженности;
	ОстаткиОсновнойДолг = стрДанные.СуммаЗадолженности;
	ОстаткиПроценты = 0;
		
	Капитализация = стрДанные.КапитализацияПроцентов;
	
	Если стрДанные.ИсключитьКрайниеМесяцы Тогда
		стрДатаРасчета = НачалоМесяца(стрДанные.ДатаРасчета) - 24 * 3600;
		НачалоПериода = КонецМесяца(стрДанные.ДатаРешения) + 1;
	Иначе
		стрДатаРасчета = стрДанные.ДатаРасчета;
		НачалоПериода = стрДанные.ДатаРешения;
	КонецЕсли;
	
	Пока НачалоПериода <= стрДатаРасчета Цикл
		Платежи = СписокПлатежей.НайтиСтроки(Новый Структура("МесяцСписания", НачалоПериода));
		Для Каждого Платеж Из Платежи Цикл
			стр = ЭтотОбъект.РасчетПроцентов.Добавить();
			стр.НачалоПериода = Платеж.ДатаПлатежа;
			стр.СуммаПлатежа = Платеж.СуммаПлатежа;
		КонецЦикла;
		
		стр = ЭтотОбъект.РасчетПроцентов.Добавить();
		
		стр.НачалоПериода = НачалоПериода;
		
		КонецПериода = мин(стрДатаРасчета, НачалоДня(КонецМесяца(НачалоПериода)));
		
		стр.КонецПериода = КонецПериода;
		
		НачалоПериода = КонецПериода + 24 * 3600;
	КонецЦикла;
	
	Для Каждого стр Из ЭтотОбъект.РасчетПроцентов Цикл
		Если Капитализация Тогда
			БазаНачисления = КУплате;
		Иначе
			БазаНачисления = ОстаткиОсновнойДолг;
		КонецЕсли;
		стр.СуммаЗадолженности = БазаНачисления;
		СуммаПлатежа = стр.СуммаПлатежа;

		Если СуммаПлатежа = 0 Тогда
			стр.Дни = (стр.КонецПериода - стр.НачалоПериода) / 24 / 3600 + 1;
			стр.ИПЦ = ПолучитьЗначениеИПЦ(НачалоМесяца(стр.НачалоПериода));
			
			КоличествоДнейВМесяце = КоличествоДнейВМесяце(стр.НачалоПериода);

			стр.Проценты = ?(стр.ИПЦ > 0, РасчетЗадолженностиМФО.ОкруглитьЧисло((стр.ИПЦ - 100) * БазаНачисления * стр.Дни / КоличествоДнейВМесяце / 100, Перечисления.ПорядкиОкругления.Окр0_01), 0);
		КонецЕсли;
		
		стр.СуммаЗадолженности = ОстаткиОсновнойДолг;
		стр.ОстаткиПроценты = ОстаткиПроценты + стр.Проценты;
		
		Если СуммаПлатежа > 0 И Капитализация Тогда
			стр.ОплатаПроценты = мин(стр.ОстаткиПроценты, СуммаПлатежа);
			стр.ОстаткиПроценты = стр.ОстаткиПроценты - стр.ОплатаПроценты;
			СуммаПлатежа = СуммаПлатежа - стр.ОплатаПроценты;
		КонецЕсли;
		
		Если СуммаПлатежа > 0 Тогда
			стр.ОплатаОсновнойДолг = мин(стр.СуммаЗадолженности, СуммаПлатежа);
			стр.СуммаЗадолженности = стр.СуммаЗадолженности - стр.ОплатаОсновнойДолг;
			СуммаПлатежа = СуммаПлатежа - стр.ОплатаОсновнойДолг;
		КонецЕсли; 
		стр.Переплата = СуммаПлатежа;
		стр.КУплате = стр.СуммаЗадолженности + стр.ОстаткиПроценты;
		
		КУплате = стр.КУплате;
		ОстаткиПроценты = стр.ОстаткиПроценты;
		ОстаткиОсновнойДолг = стр.СуммаЗадолженности;

	КонецЦикла;

КонецПроцедуры

Функция ПолучитьЗначениеИПЦ(Период)
	Менеджер = РегистрыСведений.ЗначенияИПЦ.СоздатьМенеджерЗаписи();
	Менеджер.ПериодИПЦ = Период;
	Менеджер.Территория = ЭтотОбъект.Территория;
	
	Менеджер.Прочитать();
	Возврат Менеджер.Значение;
КонецФункции

Функция КоличествоДнейВМесяце(Период)
	Возврат (НачалоДня(КонецМесяца(Период)) - НачалоМесяца(Период))/3600/24 + 1;
КонецФункции