
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Объект.Проведен Тогда
	
		Элементы.ИсполнительноеПроизводство.Доступность = Ложь;
		Элементы.ГруппаВсе.Доступность = Ложь;
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВсеПриИзменении(Элемент)
	
	Если Объект.Все Тогда
	
		Объект.Объекты.Очистить();
	
	КонецЕсли;
	
	ТаблицаИП = Элементы.ИсполнительноеПроизводство;
	
	ТаблицаИП.Доступность = Не ТаблицаИП.Доступность;
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	ЕстьНеНормализованныеИП = ПроверитьНаНормализациюИП();
	
	Если ЕстьНеНормализованныеИП Тогда
	
		Режим = РежимДиалогаВопрос.ДаНетОтмена;
		
		Оповещение = Новый ОписаниеОповещения(
			"ПослеЗакрытияВопроса",
			ЭтотОбъект);
			
		ПоказатьВопрос(
			Оповещение,
			"Есть ИП с не нормализованными номерами,
			|Такие ИП будут игнорироваться при запросе
			|
			|Номер исполнительного производства должен быть в формате n..n/yy/dd/rr или n..n/yy/ddddd-ИП
			|(Проверялись не заполненные поля - Нормализованный номер)
			|
			|Хотите запустить обработку по нормализации?",
			Режим,
			0);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьИП(Команда)
	
	ПроверитьИПНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		
		Возврат;
		
	КонецЕсли;

	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ЗапуститьПолучениеНормализованныхНомеровИП();
		
	КонецЕсли;

	Записать();

КонецПроцедуры

&НаСервере
Функция ПроверитьНаНормализациюИП()

	Запрос = РеквизитФормыВЗначение("Объект");
	
	Возврат Запрос.ЕстьНеНормализованныеИП();

КонецФункции // ()

&НаСервере
Процедура ЗапуститьПолучениеНормализованныхНомеровИП()

	МодульЗапросовФССП = Документы.ИсполнительноеПроизводствоДляЗапросовФССП;
	
	ИП = Неопределено;
	Если Не Объект.Все Тогда
	
		ИП = МодульЗапросовФССП.ПодготовитьКНормализации(
			Объект.Объекты.Выгрузить().ВыгрузитьКолонку("Значение")
		);
	
	КонецЕсли;
	
	МодульЗапросовФССП.НормализоватьИП(
		ИП,
		Объект.Все);

КонецПроцедуры

&НаСервере
Процедура ПроверитьИПНаСервере()

	КонтролерНормализации = Справочники.ФССП_ИсполнительноеПроизводство;

	Для каждого Запись Из Объект.Объекты Цикл
	
		Производство = Запись.Значение;
		Если Не КонтролерНормализации
				.Нормализован(Производство.НормализованныйНомер) Тогда
		
			Запись.ПлохойНомер = Истина;
		
		КонецЕсли;
	
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура Подобрать(Команда)
	ОбъектыКлиент.Подбор(ЭтаФорма, "Значение");
КонецПроцедуры


#КонецОбласти







