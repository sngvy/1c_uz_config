#Область ПрограммныйИнтерфейс

Процедура ЗаписьОшибкаСохраненияВБазе(Сообщение) Экспорт

	Сообщение["Отправитель"] = Неопределено;
	
	Форматер = Справочники.НастройкиИсторииЧатовФайловаяСистема;
	Сообщение["Текст"] = СтрШаблон(
		"Сообщение за дату %1 (%2) не удалось сохренить в базе",
		Сообщение["Дата"],
		Формат(Сообщение["Дата"], Форматер.ФорматДатыВремяСообщения()));
	
	ОписаниеОтправителя = Новый Структура;
	ОписаниеОтправителя.Вставить("ИмяАкаунта", "");
	Если ЗначениеЗаполнено(Сообщение["ОписаниеОтправителя"]) Тогда
	
		ОписаниеОтправителя = Сообщение["ОписаниеОтправителя"];
	
	КонецЕсли;
	
	ОписаниеОтправителя.Вставить("Идентификатор", "Error");

	
	Сообщение["ОписаниеОтправителя"] = ОписаниеОтправителя;
	
	ЗаписьТекстаСообщения(Сообщение);

КонецПроцедуры

Процедура ЗаписьТекстаСообщения(Сообщение) Экспорт
	
	КаталогСообщения = ФайлКаталогаСообщений(
		Сообщение["Мессенджер"],
		Сообщение["Клиент"],
		Сообщение["ОписаниеОтправителя"]);
	Если Не КаталогСообщения.Существует() Тогда
	
		СоздатьКаталог(КаталогСообщения.ПолноеИмя);
	
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("КаталогСообщения", КаталогСообщения);
	
	Справочники.НастройкиИсторииЧатовФайловаяСистема
		.ЗаписатьТекст(
			КаталогСообщения,
			Сообщение["Отправитель"],
			Сообщение["ОписаниеОтправителя"],
			Сообщение["Дата"],
			Сообщение["Текст"]);

КонецПроцедуры

Процедура ЗаписьИдентификатораДокумента(ОписаниеОтправителя, ХранительИстории) Экспорт

	Если Не ЗначениеЗаполнено(ОписаниеОтправителя) Тогда
	
		Возврат;
	
	КонецЕсли;
	
	КаталогСообщения = Неопределено;
	Если Не ДополнительныеСвойства.Свойство("КаталогСообщения", КаталогСообщения) Тогда
	
		ВызватьИсключение
			"Каталог не определен
			|Запись идентификатора возможна только при создании документа";
	
	КонецЕсли;
	
	Справочники.НастройкиИсторииЧатовФайловаяСистема
		.ЗаписатьДанныеАкаунта(
			КаталогСообщения,
			ХранительИстории,
			ОписаниеОтправителя);

КонецПроцедуры

Процедура ЗаписьФайлыСообщения(Сообщение) Экспорт

	КаталогСообщения = Неопределено;
	Если Не ДополнительныеСвойства.Свойство("КаталогСообщения", КаталогСообщения) Тогда
	
		КаталогСообщения = ФайлКаталогаСообщений(
			Сообщение["Мессенджер"],
			Сообщение["Клиент"],
			Сообщение["ОписаниеОтправителя"]);
	
	КонецЕсли;
	
	ИдентификаторыФайлов = ПолучитьДанныеФайлов(Сообщение);
	
	Справочники.НастройкиИсторииЧатовФайловаяСистема
		.ЗаписатьФайлы(
			КаталогСообщения,
			ИдентификаторыФайлов);

КонецПроцедуры

Процедура УбратьОтметкуНовый(Мессенджер, ОписаниеОтправителя) Экспорт

	КаталогСообщения = ФайлКаталогаСообщений(
		Мессенджер,
		Неопределено,
		ОписаниеОтправителя);
	Если Не КаталогСообщения.Существует() Тогда
	
		ВызватьИсключение "Каталога не существует";
	
	КонецЕсли;
	
	НовоеИмя = СтрЗаменить(
		КаталогСообщения.ПолноеИмя,
		Справочники.НастройкиИсторииЧатовФайловаяСистема.ЭтоНеопознаный() + "_",
		"");
	
	СоздатьКаталог(НовоеИмя);
	
	Файлы = НайтиФайлы(КаталогСообщения.ПолноеИмя, "*");
	Для каждого Файл Из Файлы Цикл

		ПереместитьФайл(
			Файл.ПолноеИмя,
			НовоеИмя + ПолучитьРазделительПути() + Файл.Имя);
	
	КонецЦикла;
	
	УдалитьФайлы(КаталогСообщения.ПолноеИмя);
	
КонецПроцедуры

Функция ПолучитьДанныеСообщения(Мессенджер, Клиент, ДатаСообщения) Экспорт

	КаталогСообщения = ФайлКаталогаСообщений(Мессенджер, Клиент);
	Если Не КаталогСообщения.Существует() Тогда
	
		Возврат Новый Структура;
	
	КонецЕсли;
	
	Результат = Справочники.НастройкиИсторииЧатовФайловаяСистема
		.ПолучитьДанныеСообщения(КаталогСообщения, ДатаСообщения);

	ТекущееСообщение = СообщениеИзЧата.НовоеСообщение();
	ЗаполнитьЗначенияСвойств(ТекущееСообщение, Результат);
	
	ТекущееСообщение["Клиент"] = Клиент;
	Если ТекущееСообщение["Отправитель"] <> ТекущееСообщение["Клиент"] Тогда
	
		ТекущееСообщение["Сотрудник"] = ТекущееСообщение["Отправитель"];
	
	КонецЕсли;
	ТекущееСообщение["Дата"] = ДатаСообщения;
	ТекущееСообщение["Мессенджер"] = Мессенджер;
	
	Возврат ТекущееСообщение;

КонецФункции // ()


#КонецОбласти

#Область ОбработчикиСобытий



#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс



#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ФайлКаталогаСообщений(Мессенджер, Клиент, ОписаниеОтправителя = Неопределено)

	Каталог = Новый Файл(ХранилищеФайлов);
	Если Не Каталог.Существует() Тогда
	
		ВызватьИсключение "Не существует каталог хранения файлов: " + ХранилищеФайлов;
	
	КонецЕсли;
	
	Путь = Справочники.НастройкиИсторииЧатовФайловаяСистема
		.ПутьВХранилище(Мессенджер, Клиент, ОписаниеОтправителя);
	
	Возврат Новый Файл(Каталог.ПолноеИмя + ПолучитьРазделительПути() + Путь);

КонецФункции // ()

Функция ПолучитьДанныеФайлов(Сообщение)

	Набор = Новый Массив;
	
	Если Сообщение.Свойство("Файл") Тогда
	
		Файл = Сообщение["Файл"];
		ДобавитьОписаниеФайла(Набор, Файл, "Файл");
	
	КонецЕсли;
	Если Сообщение.Свойство("Видео") Тогда
	
		Видео = Сообщение["Видео"];
		ДобавитьОписаниеФайла(Набор, Видео, "Видео");
	
	КонецЕсли;
	Если Сообщение.Свойство("Звук") Тогда
	
		Звук = Сообщение["Звук"];
		ДобавитьОписаниеФайла(Набор, Звук, "Звук");
	
	КонецЕсли;
	Если Сообщение.Свойство("Изображение") Тогда
	
		Изображение = Сообщение["Изображение"];
		Если ЗначениеЗаполнено(Изображение) Тогда
		
			ДобавитьОписаниеФайла(Набор, Изображение[Изображение.Количество() - 1], "Изображение");
		
		КонецЕсли;
	
	КонецЕсли;

	Возврат Набор;

КонецФункции // ()

Процедура ДобавитьОписаниеФайла(Набор, Файл, ТипФайла)

	Если ЗначениеЗаполнено(Файл) Тогда
	
		ОписаниеФайла = Новый Структура;
		ОписаниеФайла.Вставить("ИД", Файл.Получить("file_id"));
		ОписаниеФайла.Вставить("Имя", Файл.Получить("file_name"));
		ОписаниеФайла.Вставить("Тип", ТипФайла);
		
		Набор.Добавить(ОписаниеФайла);
	
	КонецЕсли;

КонецПроцедуры


#КонецОбласти