
&НаКлиенте
Процедура РаспространитьНаВсеСделкиПриИзменении(Элемент)
	Элементы.Сделка.Доступность = НЕ Объект.РаспространитьНаВсеСделки;
	Если Объект.РаспространитьНаВсеСделки Тогда
		Объект.Сделка = "";     
	КонецЕсли;
КонецПроцедуры    

&НаКлиенте
Процедура ПубликацииПередУдалением(Элемент, Отказ)
	Отказ = Истина;    
	ИдентификаторУдаляемойСтроки = Элементы.Публикации.ТекущиеДанные.ПолучитьИдентификатор();    
	ПараметрыОповещения = Новый Структура("ИдентификаторУдаляемойСтроки", ИдентификаторУдаляемойСтроки);
	
	ОповещениеПриОтветеНаВопрос = Новый ОписаниеОповещения("РезультатОтветаНаВопросОбУдаленииСтроки", ЭтаФорма, ПараметрыОповещения);
	
	ПоказатьВопрос(ОповещениеПриОтветеНаВопрос, "Подтвердите удаление строки для удаления информации из Кредитной истории Должника", РежимДиалогаВопрос.ДаНет, , ,"Вопрос об удалении строки");
КонецПроцедуры


&НаКлиенте
Процедура РезультатОтветаНаВопросОбУдаленииСтроки(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ИдентификаторСтроки = Параметры.ИдентификаторУдаляемойСтроки; 
		УдалениеИзКредитныхИсторий(ИдентификаторСтроки);    
		
	КонецЕсли;
	
КонецПроцедуры // ()

&НаСервере
Процедура УдалениеИзКредитныхИсторий(ИдентификаторСтроки)

    НайденнаяУдаляемаяСтрока = Объект.Публикации.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	Событие =  Справочники.КредитныеИсторииСобытияСубъекта.НайтиПоРеквизиту("КодНБКИ", "1.12");
		
	НаборЗаписей = РегистрыСведений.КредитныеИсторииДанныеФизЛица.СоздатьНаборЗаписей();

	НаборЗаписей.Отбор.Должник.Установить(Объект.Субъект); 
	НаборЗаписей.Отбор.Событие.Установить(Событие);
	НаборЗаписей.Отбор.ДокументСобытие.Установить(Объект.Ссылка);
	НаборЗаписей.Отбор.ДатаСобытия.Установить(НайденнаяУдаляемаяСтрока.ДатаСобытия);
		
	НаборЗаписей.Записать();	
     	
    Объект.Публикации.Удалить(НайденнаяУдаляемаяСтрока);  
      
        
КонецПроцедуры // УдалениеИзКредитныхИсторий()

