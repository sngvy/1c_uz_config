
#Область ПрограммныйИнтерфейс

// Создает обработчик для получения данных,
//   получает документы по которым необходимо сделать запросы
//
// Возвращаемое значение:
//   ДокументСсылка.ИсполнительноеПроизводствоДляЗапросовФССП   - Обработчик
//                          !!! Не записываемый, при попытке выдает исключение
//
Функция Инициализировать() Экспорт

	Обработчик = СоздатьДокумент()
		.ИнициализироватьДокументыЗапросов();
	
	Возврат Обработчик;

КонецФункции // ()

// Полная цикл получения данных и отметка обработанными
//
// Возвращаемое значение:
//   Структура   - Данные для запроса через Робот ФССП
//
Функция ЗабратьДанныеЗапросов() Экспорт

	Обработчик = Инициализировать();
	
	Обработчик.ОтметитьОбработанными();
	
	Возврат Обработчик.ДанныеДляЗапроса();

КонецФункции // ()

// Полная цикл получения ИП и отметка обработанными
//
// Возвращаемое значение:
//   Массив   - Уникальные ИП заданные для запросов в ФССП
//
Функция ЗабратьИП() Экспорт

	Обработчик = Инициализировать();
	
	Обработчик.ОтметитьОбработанными();
	
	Возврат Обработчик.ИсполнительныеПроизводства();

КонецФункции // ()

Функция ДанныеПроизводства() Экспорт

	Описание = Новый Структура;
	Описание.Вставить("ИсполнительноеПроизводство", Неопределено);
	Описание.Вставить("Контрагент", Неопределено);
	Описание.Вставить("Ресурс", 0);
	
	Возврат Описание;

КонецФункции // ()

#КонецОбласти

#Область ОбработчикиСобытий



#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура НормализоватьИП(Выбранные, ОтметкаВсе = Ложь) Экспорт

	НормализуемыеИП = Выбранные;
	Если ОтметкаВсе Тогда
	
		НормализуемыеИП = ПолучитьВсеИП();
	
	КонецЕсли;
	
	НормализованныеНомера = ОбработатьИП(НормализуемыеИП);
	
	ЗаписатьНормализованныеНомера(НормализованныеНомера);

КонецПроцедуры

Функция ПодготовитьКНормализации(Производства) Экспорт

	Возврат Справочники.ФССП_ИсполнительноеПроизводство
		.ПолучитьИзНабораНеНормализованныеИП(Производства);

КонецФункции // ()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьВсеИП()

	Возврат Справочники.ФССП_ИсполнительноеПроизводство
		.ПолучитьНеНормализованныеИП();

КонецФункции // ()

Функция ОбработатьИП(НормализуемыеИП)

	ВыделенныеНомера = ВыделитьНомерИзНаименования(НормализуемыеИП);
	
	Возврат ВыделенныеНомера;

КонецФункции // ()

Функция ВыделитьНомерИзНаименования(НормализуемыеИП)

	ВыделенныеНомера = Новый Соответствие;
	Для каждого Элемент Из НормализуемыеИП Цикл
	
		Значение = ОтделитьНомерОтДаты(Элемент.Представление);
		
		ВыделенныеНомера.Вставить(Элемент.Ссылка, Значение);
	
	КонецЦикла;
	
	Возврат ВыделенныеНомера;

КонецФункции // ()

Функция ОтделитьНомерОтДаты(НаименованиеИП)

	Номер = ВРег(НаименованиеИП);
	
	Части = СтрРазделить(
		Номер,
		" ОТ ");
	
	Если Части.Количество() > 1 Тогда
	
		Номер = Части[0];
	
	КонецЕсли;
	
	Возврат Номер;

КонецФункции // ()

Процедура ЗаписатьНормализованныеНомера(НормализованныеНомера)

	Для каждого Элемент Из НормализованныеНомера Цикл
	
		Ссылка = Элемент.Ключ;
		
		Объект = Ссылка.ПолучитьОбъект();
		
		Объект.НормализованныйНомер = Элемент.Значение;
		
		Объект.Записать();
	
	КонецЦикла;

КонецПроцедуры

#КонецОбласти