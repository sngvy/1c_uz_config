
// Обработчик события ПриЗаписи регистра сведений КурсыВалют.
// При записи нового элемента(ов) вызывает функциональность записи
// курса подчиненных валют.
//
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Количество() > 0 Тогда
		Для Каждого Запись Из ЭтотОбъект Цикл
			// Найти все зависимые валюты от данной и изменить их курс
			ЗагрузитьКурсДляПодчиненныхВалют(Запись.Валюта,
			                                 Запись.Период,
			                                 Запись.Курс,
			                                 Запись.Кратность);
		КонецЦикла;
	Иначе
		УдалитьКурсДляПодчиненныхВалют(Отбор.Валюта.Значение, Отбор.Период);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗагрузитьКурсДляПодчиненныхВалют(ВалютаБазовая, Период, Курс, Кратность)
	
	ТаблицаВалют = РаботаСКурсамиВалют.ПолучитьСписокЗависимыхВалют(ВалютаБазовая);
	
	РегистрКурсыВалют = РегистрыСведений.КурсыВалют;
		
	Для Каждого ЭлементВалюта Из ТаблицаВалют Цикл
		ЗаписьКурсовВалют = РегистрКурсыВалют.СоздатьМенеджерЗаписи();
		
		ЗаписьКурсовВалют.Валюта    = ЭлементВалюта.Ссылка;
		ЗаписьКурсовВалют.Период    = Период;
		ЗаписьКурсовВалют.Курс      = Курс + Курс * ЭлементВалюта.КоэффициентПодчиненногоКурса / 100;
		ЗаписьКурсовВалют.Кратность = Кратность;
		ЗаписьКурсовВалют.Записать();
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьКурсДляПодчиненныхВалют(ВалютаБазовая, Период)
	
	ТаблицаВалют = РаботаСКурсамиВалют.ПолучитьСписокЗависимыхВалют(ВалютаБазовая);
	
	Для Каждого ЭлементВалюта Из ТаблицаВалют Цикл
		НаборЗаписейКурсовВалют = РегистрыСведений.КурсыВалют.СоздатьНаборЗаписей();
		НаборЗаписейКурсовВалют.Отбор.Валюта.Значение = ЭлементВалюта.Ссылка;
		НаборЗаписейКурсовВалют.Отбор.Период.Значение = Период.Значение;
		НаборЗаписейКурсовВалют.Отбор.Период.ЗначениеПо = Период.ЗначениеПО;
		НаборЗаписейКурсовВалют.Отбор.Период.ЗначениеС = Период.ЗначениеС;
		НаборЗаписейКурсовВалют.Отбор.Период.ВидСравнения = Период.ВидСравнения;
		НаборЗаписейКурсовВалют.Отбор.Валюта.Использование = Истина;
		НаборЗаписейКурсовВалют.Отбор.Период.Использование = Истина;
		
		НаборЗаписейКурсовВалют.Записать();
	КонецЦикла;
	
КонецПроцедуры
