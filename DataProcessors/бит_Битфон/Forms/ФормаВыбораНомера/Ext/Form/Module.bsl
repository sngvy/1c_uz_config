
&НаСервере
Процедура ЗаполнитьСписокКонтактныхЛицКонтрагента(КонтрагентСсылка)
	Если НЕ ЗначениеЗаполнено(КонтрагентСсылка) Тогда
		Возврат;
	КонецЕсли;
	КонтактныеЛицаКонтрагента.Очистить();
	стрИмяСправочникаКонтактныхЛиц = бит_ТелефонияСерверПереопределяемый.ПолучитьИмяСправочникаКонтактныхЛиц();
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
				   |	КонтактныеЛицаКонтрагентов.Ссылка
				   |ИЗ
				   |	Справочник." + стрИмяСправочникаКонтактныхЛиц + " КАК КонтактныеЛицаКонтрагентов
				   |ГДЕ
				   |	КонтактныеЛицаКонтрагентов.Владелец = &Владелец";
	Запрос.УстановитьПараметр("Владелец", КонтрагентСсылка);
	табл = Запрос.Выполнить().Выгрузить();
	Для Каждого запись Из табл Цикл
		контактноеЛицо = КонтактныеЛицаКонтрагента.Добавить();
		контактноеЛицо.Наименование = запись.Ссылка;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокТелефоновКонтрагента(КонтрагентСсылка)
	Если НЕ ЗначениеЗаполнено(КонтрагентСсылка) Тогда
		Возврат;
	КонецЕсли;
	Телефоны.Очистить();
	массивНомераТелефонов = бит_ТелефонияСерверПереопределяемый.НайтиНомераКонтрагента(КонтрагентСсылка);
	Для Каждого тел Из массивНомераТелефонов Цикл
		строкаТелефон = Телефоны.Добавить();
		строкаТелефон.Номер = тел;
		УдалитьУправлениеКонтактнойИнформацией.РазбитьНомерТелефонаНаСоставляющие(СтрокаТелефон, тел);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокТелефоновКонтактногоЛица(КонтактноеЛицоСсылка)
	Если НЕ ЗначениеЗаполнено(КонтактноеЛицоСсылка) Тогда
		Возврат;
	КонецЕсли;
	Телефоны.Очистить();
	массивНомераТелефонов = бит_ТелефонияСерверПереопределяемый.НайтиНомераКонтактногоЛица(КонтактноеЛицоСсылка);
	Для Каждого тел Из массивНомераТелефонов Цикл
		строкаТелефон = Телефоны.Добавить();
		строкаТелефон.Номер = тел;
		УдалитьУправлениеКонтактнойИнформацией.РазбитьНомерТелефонаНаСоставляющие(СтрокаТелефон, тел);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	стрИмяСправочникаКонтрагентов = бит_ТелефонияСерверПереопределяемый.ПолучитьИмяСправочникаКонтрагентов();
	стрИмяТаблицыКонтрагентов = "Справочник." + стрИмяСправочникаКонтрагентов;
	//
	Контрагенты.ПроизвольныйЗапрос = Истина;
	Контрагенты.ДинамическоеСчитываниеДанных = Истина;
	Контрагенты.ОсновнаяТаблица = стрИмяТаблицыКонтрагентов;
	Контрагенты.ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                           |	Контрагенты.Наименование
	                           |ИЗ
	                           |	" + стрИмяТаблицыКонтрагентов + " КАК Контрагенты";
	//
	КолонкаНаименованиеКонтрагента = Элементы.Добавить("Наименование", Тип("ПолеФормы"), Элементы.Контрагенты);    
	КолонкаНаименованиеКонтрагента.ПутьКДанным = "Контрагенты.Наименование";	// тут название реквизита формы - динамического списка, а не таблицы БД
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
&НаКлиенте
Процедура КонтрагентыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ЗаполнитьСписокКонтактныхЛицКонтрагента(ВыбраннаяСтрока);
	ЗаполнитьСписокТелефоновКонтрагента(ВыбраннаяСтрока);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыПриАктивизацииЯчейки(Элемент)
	контрагентСсылка = Элементы.Контрагенты.ТекущаяСтрока;
	ЗаполнитьСписокКонтактныхЛицКонтрагента(контрагентСсылка);
	ЗаполнитьСписокТелефоновКонтрагента(контрагентСсылка);
КонецПроцедуры

&НаКлиенте
Процедура КонтактныеЛицаКонтрагентаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	текущиеДанные = Элементы.КонтактныеЛицаКонтрагента.ТекущиеДанные;
	Если текущиеДанные <> Неопределено Тогда
		контактноеЛицоСсылка = текущиеДанные.Наименование;
		ЗаполнитьСписокТелефоновКонтактногоЛица(контактноеЛицоСсылка);
	КонецЕсли;
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьНомерВТаблицеТелефонов()
	текущиеДанные = Элементы.Телефоны.ТекущиеДанные;
	Если текущиеДанные <> Неопределено Тогда
		ВыбранныйНомер = текущиеДанные.Номер;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТелефоныВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ВыбратьНомерВТаблицеТелефонов();
КонецПроцедуры

&НаКлиенте
Процедура ТелефоныПриАктивизацииЯчейки(Элемент)
	ВыбратьНомерВТаблицеТелефонов();
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьДляЗвонка(Команда)
	ЗакрыватьПриВыборе = Истина;
	ОповеститьОВыборе(ВыбранныйНомер);
КонецПроцедуры
