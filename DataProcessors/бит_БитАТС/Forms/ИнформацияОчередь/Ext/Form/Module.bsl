
&НаКлиенте
Перем обКонтроллерАТС;

&НаКлиенте
Перем ИнтервалОбновления;

&НаКлиенте
Перем СоответствиеНомеровХоста;

&НаКлиенте
Перем кэшКонтактов;


&НаКлиенте
Процедура УстановитьКонтроллерАТС(ссылкаКонтроллерАТС) ЭКСПОРТ
	обКонтроллерАТС = ссылкаКонтроллерАТС;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСоответствиеНомеров(СоответствиеВнутреннихНомеров, СоответствиеКонтактов) ЭКСПОРТ
	СоответствиеНомеровХоста = СоответствиеВнутреннихНомеров;
	кэшКонтактов = СоответствиеКонтактов;
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьТаблицы(ИнформацияХМЛ)
	чтение = Новый ЧтениеXML();
	чтение.УстановитьСтроку(ИнформацияХМЛ);
	
	дом = Новый ПостроительDOM();
	докХмл = дом.Прочитать(чтение);
	
	ИмяУзлаХмлОчереди = "Queue_" + ИдентификаторОчереди;
	списокОчередей = докХмл.ПолучитьЭлементыПоИмени("/Queues", ИмяУзлаХмлОчереди);
	Если списокОчередей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	обОчередь = списокОчередей[0];
	
	Члены.Очистить();
	списокЧленов = обОчередь.ПолучитьЭлементыПоИмени("Members", "Member");
	Для Каждого домЧленОчереди ИЗ списокЧленов Цикл
		строкаТабл = Члены.Добавить();
		ИмяЧлОчереди = домЧленОчереди.ПолучитьАтрибут("Name");
		строкаТабл.Наименование		= бит_АТСКлиент.ПолучитьИмяПоВнутреннемуНомеру(СоответствиеНомеровХоста, ИмяЧлОчереди);
		строкаТабл.Расположение		= домЧленОчереди.ПолучитьАтрибут("Location");
		строкаТабл.Членство			= домЧленОчереди.ПолучитьАтрибут("Membership");
		строкаТабл.ЗвонковПринято	= домЧленОчереди.ПолучитьАтрибут("CallsTaken");
		строкаТабл.ПоследнийЗвонок	= домЧленОчереди.ПолучитьАтрибут("LastCall");
		строкаТабл.Пауза			= домЧленОчереди.ПолучитьАтрибут("Paused");
		строкаТабл.Пенальти			= домЧленОчереди.ПолучитьАтрибут("Penalty");
		//
		СтатусЧленаОчередиЧисл = Число(домЧленОчереди.ПолучитьАтрибут("Status"));
		СтатусЧленаОчереди = "Неизвестно";
		ИндексКартинкиСтатуса = 0;
		Если СтатусЧленаОчередиЧисл = 1 Тогда
			СтатусЧленаОчереди = "Свободен";
			ИндексКартинкиСтатуса = 1;
		ИначеЕсли СтатусЧленаОчередиЧисл = 2 Тогда
			СтатусЧленаОчереди = "Разговаривает";
			ИндексКартинкиСтатуса = 2;
		ИначеЕсли СтатусЧленаОчередиЧисл = 3 Тогда
			СтатусЧленаОчереди = "Не беспокоить (DND)";
			ИндексКартинкиСтатуса = 3;
		ИначеЕсли СтатусЧленаОчередиЧисл = 5 Тогда
			СтатусЧленаОчереди = "Не зарегистирован на АТС";
			ИндексКартинкиСтатуса = 4;
		ИначеЕсли СтатусЧленаОчередиЧисл = 6 Тогда
			СтатусЧленаОчереди = "Входящий звонок";
			ИндексКартинкиСтатуса = 5;
		ИначеЕсли СтатусЧленаОчередиЧисл = 7 Тогда
			СтатусЧленаОчереди = "Разговаривает + входящий звонок";
			ИндексКартинкиСтатуса = 2;
		ИначеЕсли СтатусЧленаОчередиЧисл = 8 Тогда
			СтатусЧленаОчереди = "Удержание";
			ИндексКартинкиСтатуса = 6;
		КонецЕсли;
		строкаТабл.Статус = СтатусЧленаОчереди;
		строкаТабл.ИндексКартинкиСтатуса = ИндексКартинкиСтатуса;
	КонецЦикла;
	
	ИндексТекущейСтрокиОжидающих = Ожидающие.Индекс(Элементы.Ожидающие.ТекущиеДанные);
	Ожидающие.Очистить();
	списокОжидающих = обОчередь.ПолучитьЭлементыПоИмени("Callers", "Caller");
	//
	Для Каждого домОжидающийОчереди ИЗ списокОжидающих Цикл
		строкаТабл = Ожидающие.Добавить();
		строкаТабл.CallerIDИмя		= домОжидающийОчереди.ПолучитьАтрибут("CallerIDname");
		ОжидающийНомер				= домОжидающийОчереди.ПолучитьАтрибут("CallerIDnum");
		строкаТабл.CallerIDНомер	= ОжидающийНомер;
		строкаТабл.Позиция			= домОжидающийОчереди.ПолучитьАтрибут("Position");
		строкаТабл.Канал			= домОжидающийОчереди.ПолучитьАтрибут("Channel");
		//
		стрВремяОжидания			= домОжидающийОчереди.ПолучитьАтрибут("Wait");
		числВремяОжидания			= Число(стрВремяОжидания);
		строкаТабл.ВремяОжидания	= бит_ТелефонияКлиентСервер.ФорматироватьДлительностьЗвонкаСЛидНулями(числВремяОжидания);
		//
		КонтрагентСсылка = Неопределено;
		КонтактноеЛицоСсылка = Неопределено;
		бит_АТСКлиент.СократитьНомерНайтиКонтрагента(кэшКонтактов, СоответствиеНомеровХоста, ОжидающийНомер, КонтрагентСсылка, КонтактноеЛицоСсылка);
		строкаТабл.Контрагент		= КонтрагентСсылка;
		строкаТабл.КонтактноеЛицо	= КонтактноеЛицоСсылка;
	КонецЦикла;
	//
	Если (ИндексТекущейСтрокиОжидающих >= 0) И (ИндексТекущейСтрокиОжидающих < Ожидающие.Количество()) Тогда
		ВыделеннаяСтрокаОжид = Ожидающие[ИндексТекущейСтрокиОжидающих];
		Элементы.Ожидающие.ТекущаяСтрока = ВыделеннаяСтрокаОжид.ПолучитьИдентификатор();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)
	Если Источник = ИсточникВнешнихСобытий И Событие = "ОбновлениеОчередей" Тогда	
		ЗагрузитьТаблицы(Данные);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если обКонтроллерАТС <> Неопределено Тогда
		обКонтроллерАТС.GetQueues();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПеревестиНаВыбранныйНомер(Команда)
	бит_АТСКлиент.ПеревестиОжидающийЗвонокВыборНомера(ЭтаФорма, "Ожидающие", "БитфонОчередьБИТАТС_ПеревестиОжидающийЗвонок");
КонецПроцедуры

&НаКлиенте
Процедура ПеревестиНаСвойНомер(Команда)
	НомерСвязанногоТелефона = бит_АТССервер.ПолучитьНомерСвязанногоТелефона();
	бит_АТСКлиент.ПеревестиОжидающийЗвонокНаНомер(ЭтаФорма, "Ожидающие", НомерСвязанногоТелефона, обКонтроллерАТС);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "БитфонОчередьБИТАТС_ПеревестиОжидающийЗвонок" Тогда
		Если ЗначениеЗаполнено(Параметр.Результат) Тогда
			номерПеревода = Параметр.Результат;
			бит_АТСКлиент.ПеревестиОжидающийЗвонокНаНомер(ЭтаФорма, "Ожидающие", номерПеревода, обКонтроллерАТС);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
