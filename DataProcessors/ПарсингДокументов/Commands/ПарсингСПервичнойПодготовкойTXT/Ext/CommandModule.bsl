////////////////////////////////////////////////////////////////////////////////
// Для вызова команды У источника должна быть реализована
// Экспортная функция с двумя параметрами:
//   Результаты - Структура - с результатами обработки файлов по стадиям
//   НастройкиПарсинга - Структура - данные полученные из НастройкиПарсингаДокументов

#Область ПрограммныйИнтерфейс

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	ОчиститьСообщения();
	Если Не ПарсингРазрешен() Тогда
	
		СообщитьОбЗапретеНаПарсинг();
		Возврат;
	
	КонецЕсли;
	
	НастройкиПарсинга = ПолучитьВсеНастройки(ПараметрКоманды);
	Если НастройкиПарсинга = Неопределено Тогда
	
		СообщитьНеХватаетДанных("Настройка");
		Возврат;
	
	КонецЕсли;

	Источник = ПараметрыВыполненияКоманды.Источник;

	КаталогиПарсинга = НастройкиПарсингаКлиентСервер.ПолучитьКаталогиПарсинга(
		НастройкиПарсинга,
		Источник
	);
	
	ВыполнятьНаСервере = НастройкиПарсингаКлиентСервер
		.ВыполнятьНаСервере(НастройкиПарсинга);
	
	Результаты = ОбработатьСтадииПарсинга(НастройкиПарсинга, КаталогиПарсинга, ВыполнятьНаСервере);
	
	ВозвратРезультата = Новый ОписаниеОповещения(
		"ОбработатьРезультатПарсинга",
		Источник,
		НастройкиПарсинга
	);
	
	ВыполнитьОбработкуОповещения(
		ВозвратРезультата,
		Результаты
	);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбщиеКлиент

&НаКлиенте
Функция ПарсингРазрешен()

	Возврат УправлениеДоступами.ЕстьДоступНаПарсинг();

КонецФункции

&НаКлиенте
Процедура СообщитьОбЗапретеНаПарсинг()

	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Нужны права на парсинг!";
	Сообщение.Сообщить();

КонецПроцедуры

&НаКлиенте
Процедура СообщитьНеХватаетДанных(Знач Поле)

	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Не заполнено поле: " + Поле;
	Сообщение.Сообщить();

КонецПроцедуры

&НаКлиентеНаСервере
Функция НастроеныеСтадииПарсинга(НастройкиПарсинга)

	// Убрал конвертацию из ворда в pdf
	// Надо переделать на вычленение текста сразу в txt
	
	// Обработка текста не реализована
	// Остается старый вариант
	
	Возврат НастройкиПарсингаКлиентСервер.ПолучитьПарсингДокументовСтадии(
		НастройкиПарсинга
	);

КонецФункции // ()

#КонецОбласти

#Область СлужебныеРаботаСКаталогами

&НаКлиенте
Функция ПолучитьОписаниеКаталогов(КаталогОбработки, КаталогИсточник)

	Возврат ПарсингДокументовОбщийКлиентСервер
		.КаталогиДляПарсинга(КаталогОбработки, КаталогИсточник);

КонецФункции // ()

#КонецОбласти

#КонецОбласти

#Область СлужебныеРаботаСНастройкой

&НаКлиенте
Функция ПолучитьВсеНастройки(ВыбраннаяНастройка)

	Если Не ЗначениеЗаполнено(ВыбраннаяНастройка) Тогда
	
		Возврат Неопределено;
	
	КонецЕсли;
	
	Возврат ПолучитьВсеНастройкиНаСервере(ВыбраннаяНастройка);

КонецФункции // ()

&НаСервере
Функция ПолучитьВсеНастройкиНаСервере(Настройка)

	Возврат Справочники.НастройкиПарсингаДокументов.ДанныеНастройки(
		Настройка
	);
	
КонецФункции

#КонецОбласти

#Область СлужебныеСтадии

&НаКлиентеНаСервере
Функция ПодготовитьДанныеПоСтадиям(СтадииПарсинга, НастройкиПарсинга, КаталогиПарсинга)

	Стадии = Новый Массив;
	Для каждого Стадия Из СтадииПарсинга Цикл
	
		МодульСтадии = ПарсингДокументовОбщийКлиентСервер
			.ПолучитьМодульСтадии(Стадия);
		ПараметрыСтадии = МодульСтадии.ПодготовитьПараметры(
			КаталогиПарсинга,
			НастройкиПарсинга
		);
		Данные = Новый Структура;
		Данные.Вставить("Параметры", ПараметрыСтадии);
		Данные.Вставить("Модуль", Стадия);
		
		Стадии.Добавить(Данные);
	
	КонецЦикла;
	
	Возврат Стадии;

КонецФункции // ()

&НаКлиенте
Функция ОбработатьСтадииПарсинга(НастройкиПарсинга, КаталогиПарсинга, ВыполнятьНаСервере)

	Если ВыполнятьНаСервере Тогда
	
		Возврат ОбработатьСтадииПарсингаНаСервере(НастройкиПарсинга, КаталогиПарсинга);
	
	Иначе
	
		Возврат ОбработатьСтадииПарсингаНаКлиенте(НастройкиПарсинга, КаталогиПарсинга);
	
	КонецЕсли;

КонецФункции // ()

&НаСервере
Функция ОбработатьСтадииПарсингаНаСервере(НастройкиПарсинга, КаталогиПарсинга)

	Возврат ОбработатьСтадииПарсингаНаОбщий(НастройкиПарсинга, КаталогиПарсинга)

КонецФункции // ()

&НаКлиенте
Функция ОбработатьСтадииПарсингаНаКлиенте(НастройкиПарсинга, КаталогиПарсинга)

	Возврат ОбработатьСтадииПарсингаНаОбщий(НастройкиПарсинга, КаталогиПарсинга)

КонецФункции // ()

&НаКлиентеНаСервере
Функция ОбработатьСтадииПарсингаНаОбщий(НастройкиПарсинга, КаталогиПарсинга)

	СтадииПарсинга = НастроеныеСтадииПарсинга(НастройкиПарсинга);

	ПараметрыСтадийПарсинга = ПодготовитьДанныеПоСтадиям(
		СтадииПарсинга,
		НастройкиПарсинга,
		КаталогиПарсинга
	);
	
	Возврат ПарсингДокументовОбщийКлиентСервер.
		ОбработатьСтадииПарсингаОбщий(ПараметрыСтадийПарсинга);

КонецФункции // ()

#КонецОбласти



