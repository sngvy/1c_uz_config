
Перем ТекстИзменений;

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Наименование = ?(ПустаяСтрока(СерияНомерИД), НомерДела, НомерДела + " (" + СерияНомерИД + ")");
	
	Если Не ЭтотОбъект.ЭтоГруппа И Не ЗначениеЗаполнено(ЭтотОбъект.ДатаСоздания) Тогда
		ЭтотОбъект.ДатаСоздания = ТекущаяДатаСеанса();
	КонецЕсли;
	
	//
	ТекстИзменений = ОбъектыСервер.ПроверитьИзмененияВОбъекте(ЭтотОбъект);
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Справочники.Пользователи.ОтслеживаниеИзменений(Отказ, Ссылка, ТекстИзменений);
	Если Не Отказ Тогда
		Если ТипЗнч(ЭтотОбъект.Владелец) = Тип("СправочникСсылка.ДолговыеОбязательства") Тогда
			СобытиеЗафиксировано = КредитныеИстории.СобытиеСделкиЗафиксированоИсточником(ЭтотОбъект.Владелец, ЭтотОбъект.Ссылка, "2.6");
			ЭтоОтмененноеДело = (СудебноеРешение.Отменено И ЗначениеЗаполнено(СудебноеРешение.ДатаОтмены));
			Если Не СобытиеЗафиксировано И НЕ ЭтоОтмененноеДело Тогда 
				ДатаСобытия = ?(ЗначениеЗаполнено(ЭтотОбъект.ДатаИД), ЭтотОбъект.ДатаИД, ТекущаяДатаСеанса());
				КредитныеИстории.ЗаписатьСобытиеСделки(ЭтотОбъект.Владелец, ДатаСобытия, ЭтотОбъект.Ссылка, "2.6");   
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;  
	
	Если ЭтотОбъект.ПометкаУдаления Тогда
		СобытиеЗафиксировано = КредитныеИстории.СобытиеСделкиЗафиксированоИсточником(ЭтотОбъект.Владелец, ЭтотОбъект.Ссылка, "2.6"); 
		Если СобытиеЗафиксировано Тогда 
			КредитныеИстории.УдалитьСобытияИсточника(ЭтотОбъект.Владелец, ЭтотОбъект.Ссылка);  
		КонецЕсли;
	КонецЕсли;	

	РасчетЗадолженностиСудопроизводство.ОбновитьСуммыЗадолженностиПоИД(ЭтотОбъект.Ссылка);
КонецПроцедуры


Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ИсполнительныеДокументы") Тогда
		Оригинал = ДанныеЗаполнения;
		СудебноеРешение = ДанныеЗаполнения.СудебноеРешение;
		ЧастьРешения = ДанныеЗаполнения.ЧастьРешения;
		ВидОтветчика = ДанныеЗаполнения.ВидОтветчика;
		Должник = ДанныеЗаполнения.Должник;
		ОтветчикФИО = ДанныеЗаполнения.Должник.ФИО_Действующее;
		ВидИД = ДанныеЗаполнения.ВидИД;		
		ТипИД = ДанныеЗаполнения.ТипИД;
		Взыскатель = ДанныеЗаполнения.Взыскатель;
		Дубликат = Истина;
		Владелец = ДанныеЗаполнения.Владелец;
		СтавкаРефинансирование = ДанныеЗаполнения.СтавкаРефинансирования;		
	ИначеЕсли ТипЗНч(ДанныеЗаполнения) = Тип("СправочникСсылка.ДолговыеОбязательства") Тогда
		ВидОтветчика = Перечисления.ВидыОтветчиков.Должник;
		Должник = ДанныеЗаполнения.Должник;
		ОтветчикФИО = ДанныеЗаполнения.Должник.ФИО_Действующее;
		Взыскатель = ДанныеЗаполнения.Организация;
		Владелец = ДанныеЗаполнения.Ссылка;
	КонецЕсли;
КонецПроцедуры


Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	КредитныеИстории.УдалитьСобытияИсточника(ЭтотОбъект.Владелец, ЭтотОбъект.Ссылка);
КонецПроцедуры

