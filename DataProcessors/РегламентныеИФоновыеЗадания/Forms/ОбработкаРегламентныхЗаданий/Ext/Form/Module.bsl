
////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ МЕТОДЫ

// Процедура ОбработатьРегламентныеЗадания выполняет запуск
// обработки регламентных заданий для эмуляции серверной процедуры их выполнения.
//  Подключается, как обработчик ожидания ПриОткрытии.
// 
&НаКлиенте
Процедура ОбработатьРегламентныеЗадания()

	Перезапустить = Ложь;
	Если НужноПрекратитьОбработку(ПараметрЗапуска, Перезапустить) Тогда
		Закрыть(?(Перезапустить, "Перезапустить", Неопределено));
	Иначе
		ОсталосьДоНачалаОбработки = ОсталосьДоНачалаОбработки - 1;
		Если ОсталосьДоНачалаОбработки <= 0 Тогда

			ОсталосьДоНачалаОбработки = ПериодОбработки;

			СтрокаСостояния = НСтр("ru = 'Выполняется обработка, дождитесь завершения... '");
			ОбновитьОтображениеДанных();

			УдалитьРегламентныеЗаданияСервер.ОбработатьРегламентныеЗадания();

		КонецЕсли;
	КонецЕсли;
	
	// Если пользователь нажал CTRL+BREAK тогда прекратим обработку
	ПодключитьОбработчикОжидания("ПрекратитьОбработкуИЗавершитьСеансВыполнить", 1, Истина);
	ОбработкаПрерыванияПользователя();
	ОтключитьОбработчикОжидания("ПрекратитьОбработкуИЗавершитьСеансВыполнить");
	
	ПодключитьОбработчикОжидания("ОбработатьРегламентныеЗадания", 1, Истина);
	СтрокаСостояния = УдалитьСтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	                    НСтр("ru = 'До начала обработки осталось секунд: %1'"), Строка(ОсталосьДоНачалаОбработки) );
	
КонецПроцедуры // ОбработатьРегламентныеЗадания()

// Функция объединяет три серверных вызова в один.
&НаСервере
Функция НужноПрекратитьОбработку(ПараметрЗапуска, Перезапустить)
	
	Перезапустить = КонфигурацияБазыДанныхИзмененаДинамически();
	
	Возврат Перезапустить ИЛИ
	        УдалитьРегламентныеЗаданияСервер.РодительскийСеансЗаданИЗавершен(ПараметрЗапуска) ИЛИ
	        НЕ УдалитьРегламентныеЗаданияСервер.ТекущийСеансОбрабатываетЗадания();
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБРАБОТЧИКИ СОБЫТИЙ
//
// Источники: форма, панели, команды

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбработатьРегламентныеЗадания();

КонецПроцедуры // ПриОткрытии()

&НаКлиенте
Процедура ПрекратитьОбработкуИЗавершитьСеансВыполнить()
	
	Закрыть();
	
КонецПроцедуры // ПрекратитьОбработкуИЗавершитьСеансВыполнить()


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ПериодОбработки = 3;                              // Секунды.
	ОсталосьДоНачалаОбработки = ПериодОбработки + 1;  // Секунды.
	
КонецПроцедуры

