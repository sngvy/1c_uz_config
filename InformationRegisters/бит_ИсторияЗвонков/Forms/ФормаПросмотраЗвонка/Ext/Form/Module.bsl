
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ДлительностьЗвонка = бит_ТелефонияКлиентСервер.ФорматироватьДлительностьЗвонкаСЛидНулями(Запись.ДлительностьЗвонка);
КонецПроцедуры

&НаКлиенте
Процедура ЗаписьРазговораОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	стрПутьКЗаписи = Запись.ЗаписьРазговора;
	Если ЗначениеЗаполнено(стрПутьКЗаписи) Тогда
		бит_БитфонКлиент.ВоспроизвестиЗаписьРазговора(стрПутьКЗаписи);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "БитИсторияЗвонков_СохранениеФайлаЗаписиРазговораЗавершение" Тогда
		Если ЗначениеЗаполнено(Параметр.Результат) Тогда
			данныеЗапись = Параметр.ПараметрОповещения;
			имяФайла = Параметр.Результат[0];
			данныеЗапись.Записать(имяФайла);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СкачатьЗапись(Команда)
	
	Если НЕ ЗначениеЗаполнено(Запись.ЗаписьРазговора) Тогда
		Возврат;
	КонецЕсли;
	
	стрСервисЗаписейНаАТС = бит_АТСКлиент.ПутьСервисаЗаписейРазговоровНаАТС();
	индЗаголовокЗап = Найти(Запись.ЗаписьРазговора, стрСервисЗаписейНаАТС);
	Если индЗаголовокЗап = 0 Тогда
		бит_ТелефонияКлиент.ВывестиСообщение("Скачивание записи разговора возможно только для записей, хранящихся на БИТ.АТС", ЭтаФорма);
		Возврат;
	КонецЕсли;
	
	данныеЗапись = Неопределено;
	стрОшибкаHTTP = "";
	загружено = бит_ТелефонияКлиентСервер.СкачатьПоHTTPS(Запись.ЗаписьРазговора, 900, Ложь, данныеЗапись, стрОшибкаHTTP);
	
	Если загружено Тогда
		длг = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
		длг.ПолноеИмяФайла = бит_ТелефонияКлиент.ПолучитьИмяФайлаЗаписиСДатой(Запись.Дата) + ".mp3"; 
		длг.Фильтр = "Файл mp3 (*.mp3)|*.mp3";
		бит_ТелефонияКлиентПереопределяемый.ПоказВыборФайла(длг, "БитИсторияЗвонков_СохранениеФайлаЗаписиРазговораЗавершение", данныеЗапись);
	Иначе
		бит_ТелефонияКлиент.ВывестиСообщение(стрОшибкаHTTP, ЭтаФорма);
		бит_ТелефонияКлиент.ВывестиСообщение("Ошибка загрузки записи разговора", ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВоспроизвестиЗаписьЧерезМонитор(Команда)
	
	Если НЕ ЗначениеЗаполнено(Запись.ЗаписьРазговора) Тогда
		Возврат;
	КонецЕсли;
	
	стрСервисЗаписейНаАТС = бит_АТСКлиент.ПутьСервисаЗаписейРазговоровНаАТС();
	индЗаголовокЗап = Найти(Запись.ЗаписьРазговора, стрСервисЗаписейНаАТС);
	Если индЗаголовокЗап = 0 Тогда
		бит_ТелефонияКлиент.ВывестиСообщение("Прослушивание записи разговора на стационарном телефоне возможно только для записей, хранящихся на БИТ.АТС", ЭтаФорма);
		Возврат;
	КонецЕсли;
	
	бит_АТСКлиент.ОткрытьМониторВоспроизвестиЗаписьРазговора(Запись.ЗаписьРазговора);
	
КонецПроцедуры

#КонецОбласти

