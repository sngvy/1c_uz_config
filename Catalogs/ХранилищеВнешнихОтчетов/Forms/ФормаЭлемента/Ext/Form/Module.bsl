
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Для Каждого Элемент Из Метаданные.Отчеты Цикл		
		Элементы.ПрикрепляемыйФайл.СписокВыбора.Добавить(Элемент.Имя, Элемент.Синоним);			
	КонецЦикла;	
	
	Элементы.ПрикрепляемыйФайл.КнопкаОткрытия = Не (Объект.Ссылка.Пустая() ИЛИ Объект.Внутренний);
КонецПроцедуры

&НаКлиенте
Процедура ПрикрепляемыйФайлОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Объект.ИмяОтчета = ВыбранноеЗначение;	
	Объект.Внутренний = Истина;
	Элементы.ПрикрепляемыйФайл.КнопкаОткрытия = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПрикрепляемыйФайлНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок = "Выберите файл";
	Диалог.Фильтр = "Отчет (*.erf)|*.erf";  
	Диалог.МножественныйВыбор = Ложь;
	Если Диалог.Выбрать() Тогда		
		Объект.ПрикрепляемыйФайл = Диалог.ПолноеИмяФайла;
		Объект.Внутренний = Ложь;
		Элементы.ПрикрепляемыйФайл.КнопкаОткрытия = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если Не Объект.Внутренний Тогда	
		ФайлОтчета = Новый Файл(Объект.ПрикрепляемыйФайл);
		Если ФайлОтчета.Существует() Тогда
			Попытка
				Данные = Новый ДвоичныеДанные(Объект.ПрикрепляемыйФайл);		
			Исключение
				Сообщить(ОписаниеОшибки());
				Отказ = Истина;
				Возврат;
			КонецПопытки;	
			АдресХранилища = ПоместитьВоВременноеХранилище(Данные, УникальныйИдентификатор);

		Иначе
			//Чуров
			ПоказатьПредупреждение(,"Файл " + ФайлОтчета.ПолноеИмя + " не существует!",, "Ошибка");
			//Предупреждение("Файл " + ФайлОтчета.ПолноеИмя + " не существует!",, "Ошибка");
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ТекущийОбъект.Внутренний Тогда 
		ТекущийОбъект.Хранилище = Неопределено;
		
	Иначе
		ТекущийОбъект.Хранилище = Новый ХранилищеЗначения(
				ПолучитьИзВременногоХранилища(АдресХранилища), Новый СжатиеДанных(9));
		Попытка                         
			ТекущийОбъект.ИмяОтчета = ВнешниеОтчеты.Подключить(АдресХранилища,, Ложь);
		Исключение
			Сообщить(ОписаниеОшибки());
			Отказ = Истина;
			Возврат;
		КонецПопытки;
		
		Элементы.ПрикрепляемыйФайл.КнопкаОткрытия = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПрикрепляемыйФайлОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если Не Объект.Ссылка.Пустая() Тогда
		Данные = ОбъектыСервер.РазыменоватьСсылку(Объект.Ссылка, "Хранилище.Получить()");
		Если Данные <> Неопределено Тогда 
			Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
			Диалог.Заголовок = "Сохранить файл";
			Диалог.Фильтр = "Отчет (*.erf)|*.erf";  
			Диалог.МножественныйВыбор = Ложь;
			Если Диалог.Выбрать() Тогда
				Данные.Записать(Диалог.ПолноеИмяФайла);				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
