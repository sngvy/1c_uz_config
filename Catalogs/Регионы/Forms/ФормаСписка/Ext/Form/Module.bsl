
&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьСервер();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСервер()
	КлассификаторАдресныхОбъектовXML = РегистрыСведений.АдресныйКлассификатор.
			ПолучитьМакет("КлассификаторАдресныхОбъектовРоссии").ПолучитьТекст();
	КлассификаторТаблица = УдалитьОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторАдресныхОбъектовXML).Данные;
	
	// 1.
	ТЗ = Новый ТаблицаЗначений;
	// Колонки:
	// Код
	КЧ = Новый КвалификаторыЧисла(2, 0);
	Массив = Новый Массив();
	Массив.Добавить(Тип("Число"));
	ОписаниеТиповЧ = Новый ОписаниеТипов(Массив,,, КЧ);
	ТЗ.Колонки.Добавить("Код", ОписаниеТиповЧ);
	// Регион
	КС = Новый КвалификаторыСтроки(75);
	Массив = Новый Массив();
	Массив.Добавить(Тип("Строка"));
    ОписаниеТиповС = Новый ОписаниеТипов(Массив,, КС);
	ТЗ.Колонки.Добавить("Регион", ОписаниеТиповС);
	// Столица
	КС = Новый КвалификаторыСтроки(50);
	Массив = Новый Массив();
	Массив.Добавить(Тип("Строка"));
    ОписаниеТиповС = Новый ОписаниеТипов(Массив,, КС);
	ТЗ.Колонки.Добавить("Столица", ОписаниеТиповС);
	// Округ
	КС = Новый КвалификаторыСтроки(30);
	Массив = Новый Массив();
	Массив.Добавить(Тип("Строка"));
    ОписаниеТиповС = Новый ОписаниеТипов(Массив,, КС);
	ТЗ.Колонки.Добавить("Округ", ОписаниеТиповС);
	// Сокращение
	КС = Новый КвалификаторыСтроки(10);
	Массив = Новый Массив();
	Массив.Добавить(Тип("Строка"));
    ОписаниеТиповС = Новый ОписаниеТипов(Массив,, КС);
	ТЗ.Колонки.Добавить("Сокращение", ОписаниеТиповС);
	
	Для Каждого АдресныйОбъект Из КлассификаторТаблица Цикл
		Попытка
			КодЧисло = Число(Лев(АдресныйОбъект.Code, 2));
		Исключение
			Сообщить(ОписаниеОшибки());
			Продолжить;
		КонецПопытки;
			
		НоваяСтрока = ТЗ.Добавить();
		НоваяСтрока.Код = КодЧисло;
		НоваяСтрока.Регион = АдресныйОбъект.Name;
		НоваяСтрока.Столица = АдресныйОбъект.Metropolis;
		НоваяСтрока.Округ = АдресныйОбъект.Okrug;
		НоваяСтрока.Сокращение = АдресныйОбъект.Socr;
	КонецЦикла;
	
	// 2. Список элементов для пометки на удаление
	Запрос = Новый Запрос("ВЫБРАТЬ
						|	Регионы.Ссылка
						|ИЗ
						|	Справочник.Регионы КАК Регионы
						|ГДЕ
						|	НЕ Регионы.Код В(&Код)");
	Запрос.УстановитьПараметр("Код", ТЗ.ВыгрузитьКолонку("Код"));//М_Код);
	Результат = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Регион_Объект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		Регион_Объект.ПометкаУдаления = Истина;
		Регион_Объект.Записать();
	КонецЦикла;
	
	
	// 3. Подготовка итоговой таблицы
	Запрос.Текст = "ВЫБРАТЬ
					|	ТЧ.Код,
					|	ТЧ.Регион,
					|	ТЧ.Столица,
					|	ТЧ.Округ,
					|	ТЧ.Сокращение
					|ПОМЕСТИТЬ ТЧ
					|ИЗ
					|	&ТЗ КАК ТЧ
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ РАЗЛИЧНЫЕ
					|	ЕСТЬNULL(АдресныеСокращения.Наименование, """") КАК НаименованиеСокращения,
					|	ТЧ.Регион,
					|	ТЧ.Столица,
					|	ТЧ.Округ,
					|	ТЧ.Код,
					|	ЕСТЬNULL(Регионы.Ссылка, """") КАК РегионыСправ
					|ИЗ
					|	ТЧ КАК ТЧ
					|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АдресныеСокращения КАК АдресныеСокращения
					|		ПО ТЧ.Сокращение = АдресныеСокращения.Сокращение
					|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Регионы КАК Регионы
					|		ПО ТЧ.Код = Регионы.Код";
	Запрос.УстановитьПараметр("ТЗ", ТЗ);
	ТЗ_Итоговая = Запрос.Выполнить().Выгрузить();
	Для Каждого Стр Из ТЗ_Итоговая Цикл
		Если ЗначениеЗаполнено(Стр.РегионыСправ) Тогда
			Регион_Объект = Стр.РегионыСправ.ПолучитьОбъект();
		Иначе
			Регион_Объект = Справочники.Регионы.СоздатьЭлемент();
		КонецЕсли;
		
		Регион_Объект.Код = Стр.Код;
		Регион_Объект.Регион = Стр.Регион + " " + СокрЛП(НРег(Стр.НаименованиеСокращения));
		Регион_Объект.Столица = СокрЛП(Стр.Столица);
		Регион_Объект.Округ = СокрЛП(Стр.Округ);
		
		Попытка
			Регион_Объект.Записать();
		Исключение
			Сообщить(ОписаниеОшибки());
		КонецПопытки;
	КонецЦикла;	
КонецПроцедуры
