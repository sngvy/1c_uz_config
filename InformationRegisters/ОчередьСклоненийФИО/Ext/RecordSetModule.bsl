
#Область ПрограммныйИнтерфейс

Функция Контрагент(Значение) Экспорт

	Если Не ЭтоСуществующийКонтрагент(Значение) Тогда
	
		ВызватьИсключение УправленияСообщениями.ПредупреждениеОбПользовательскойОшибке(
			"Должен быть существующий контрагент"
		);
	
	КонецЕсли;
	Контрагенты = Неопределено;
	Если Не ДополнительныеСвойства.Свойство("Контрагенты", Контрагенты) Тогда
	
		Контрагенты = Новый Массив;
	
	КонецЕсли;
	
	Контрагенты.Добавить(Значение);
	
	ДополнительныеСвойства.Вставить("Контрагенты", Контрагенты);
	Возврат ЭтотОбъект;

КонецФункции // ()

Функция Контрагенты(Значение) Экспорт

	Если ТипЗнч(Значение) <> Тип("Массив") Тогда
		
		ВызватьИсключение УправленияСообщениями.ПредупреждениеОбПользовательскойОшибке(
			"Устанавливаемое значение должно быть Массивом"
		);
	
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("Контрагенты", Значение);
	
	Возврат ЭтотОбъект;

КонецФункции // ()

Процедура ОбработатьВФоне() Экспорт

	Если Не ПроверитьЗаполнение() Тогда
	
		Возврат;
	
	КонецЕсли;
	
	ЗафиксироватьВФоне();

КонецПроцедуры

Процедура Зафиксировать() Экспорт

	НовыеКонтрагенты = ДополнительныеСвойства["Контрагенты"];
	
	ДобавленныеКонтрагенты = Новый Соответствие;
	Для каждого Контрагент Из НовыеКонтрагенты Цикл
	
		Если Не ЭтоСуществующийКонтрагент(Контрагент) Тогда
		
			Продолжить;
		
		КонецЕсли;
		
		Запись = Добавить();
		Запись.Контрагент = Контрагент;
		ДобавленныеКонтрагенты.Вставить(Контрагент, Истина);
	
	КонецЦикла;
	
	СтарыеКонтрагенты = РегистрыСведений.ОчередьСклоненийФИО.ПолучитьКонтрагентов();
	Для каждого Контрагент Из СтарыеКонтрагенты Цикл
	
		УжеДобавленный = ДобавленныеКонтрагенты.Получить(Контрагент);
		Если УжеДобавленный = Истина Тогда
		
			Продолжить;
		
		КонецЕсли;
		
		Запись = Добавить();
		Запись.Контрагент = Контрагент;
	
	КонецЦикла;
	
	Записать();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ДополнительныеСвойства.Свойство("Контрагенты") Тогда
	
		Отказ = Истина;
		ТекстОтказа = УправленияСообщениями.ПредупреждениеОбПользовательскойОшибке(
			"Контрагент должен быть заполнен"
		);
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстОтказа;
		Сообщение.Сообщить();
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЭтоСуществующийКонтрагент(Контрагент)

	Возврат ТипЗнч(Контрагент) = Тип("СправочникСсылка.Контрагенты")
		И Не Контрагент.Пустая();

КонецФункции // ()

Процедура ЗафиксироватьВФоне()

	Контрагенты = ДополнительныеСвойства["Контрагенты"];
	
	ОбработкаСклоненияФИО.ДобавитьВОчередьВФоне(
		Контрагенты
	);

КонецПроцедуры

#КонецОбласти