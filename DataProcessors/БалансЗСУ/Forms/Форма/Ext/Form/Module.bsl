
&НаСервере
Процедура ОбновитьНаСервере()
	Соответствие = СервисСудебныеУчастки.ПолучитьБаланс();
	Если ТипЗнч(Соответствие) = Тип("Соответствие") Тогда
		ТЗ = Соответствие.Получить("ТЗ"); 
		Если ТипЗнч(ТЗ) = Тип("ТаблицаЗначений") Тогда
			//ТЗ = ТЗ[0].Значение;
			РеквизитДанныеТаблицы = РеквизитФормыВЗначение("ДанныеТаблицы");
			УдаляемыеРеквизиты = Новый Массив();
			Для Каждого Колонка Из РеквизитДанныеТаблицы.Колонки Цикл
				УдаляемыеРеквизиты.Добавить("ДанныеТаблицы." + Колонка.Имя);       
			КонецЦикла;
			
			// Добавляемые реквизиты: колонки новой таблицы значений
			ДобавляемыеРеквизиты = Новый Массив();
			Для Каждого Колонка Из ТЗ.Колонки Цикл
				МассивТипов = Новый Массив();
				МассивТипов.Добавить(Колонка.ТипЗначения);
				НоваяКолонка = Новый РеквизитФормы(
				Колонка.Имя,
				Новый ОписаниеТипов(МассивТипов),
				"ДанныеТаблицы"
				);
				ДобавляемыеРеквизиты.Добавить(НоваяКолонка);
			КонецЦикла;
			
			// Удаляем старые реквизиты и добавляем новые
			ИзменитьРеквизиты(ДобавляемыеРеквизиты, УдаляемыеРеквизиты);
			
			// Присваиваем новое значение реквизиту формы
			ЗначениеВРеквизитФормы(ТЗ, "ДанныеТаблицы");
			
			// Удаляем таблицу с формы
			ЭлементТаблица = Элементы.Найти("ТЗФормы");
			Если ЭлементТаблица <> Неопределено Тогда
				Элементы.Удалить(ЭлементТаблица);       
			КонецЕсли;    
			// И добавляем ее заново
			ЭлементТаблица = Элементы.Добавить("ТЗФормы", Тип("ТаблицаФормы"));
			ЭлементТаблица.ПутьКДанным = "ДанныеТаблицы";
			ЭлементТаблица.Отображение = ОтображениеТаблицы.Список;
			
			// Выводим на форму колонки таблицы
			ЭлементТаблица = Элементы.ТЗФормы;
			Для Каждого Колонка Из ТЗ.Колонки Цикл
				НовыйЭлементФормы = Элементы.Добавить(
				"ДанныеТаблицы" + Колонка.Имя,
				Тип("ПолеФормы"),
				ЭлементТаблица
				);
				НовыйЭлементФормы.Вид = ВидПоляФормы.ПолеВвода;
				НовыйЭлементФормы.ПутьКДанным = "ДанныеТаблицы." + Колонка.Имя;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбновитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура НастройкиКонстант(Команда)
	Элементы.ГруппаКоманднаяПанель.Видимость = Истина;
	Элементы.ГруппаКонстант.Видимость = Истина;
КонецПроцедуры
