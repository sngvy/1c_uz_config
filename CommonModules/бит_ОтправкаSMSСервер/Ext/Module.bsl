///////////////////////////////////////////////////////////////////////////////
// Общий серверный модуль отправки SMS из БИТ.Phone
///////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

///////////////////////////////////////////////////////////////////////////////
Процедура ЗаполнитьНастройкиПоУмолчаниюТекущегоПользователя(настройки) ЭКСПОРТ
	настройки.Пользователь		= бит_ТелефонияСервер.ПолучитьТекущегоПользователя();
	настройки.Отправитель		= "SMS4B-Test";
КонецПроцедуры

Функция ПолучитьПараметрыОткрытияФормыНастроек() ЭКСПОРТ
	Возврат бит_ТелефонияСервер.ПолучитьПараметрыОткрытияФормыНастроек("бит_ОтправкаSMSНастройки", "бит_ОтправкаSMSСервер");
КонецФункции

Процедура ПолучитьПараметрыОтправкиSMS(стрСМСЛогин, стрСМСПароль) ЭКСПОРТ
	настрСМС = ПолучитьНастройкиОтправкиSMS();
	стрСМСЛогин = настрСМС.Логин;
	стрСМСПароль = настрСМС.Пароль;
КонецПроцедуры

Функция ПолучитьОтправителяSMSПоУмолчанию() ЭКСПОРТ
	ПереносСтарыхНастроек();
	отправитель = ПолучитьНастройкиОтправкиSMS().Отправитель;
	Возврат отправитель;
КонецФункции

Функция ПолучитьШаблонСообщения() ЭКСПОРТ
	шаблон = ПолучитьНастройкиОтправкиSMS().ШаблонСообщения;
	Возврат шаблон;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПереносСтарыхНастроек()
	
	настр = ПолучитьНастройкиОтправкиSMS();
	
	стрСтарЛогин = "";
	стрСтарПароль = "";
	стрСтарОтправитель = "";
	Попытка
		бит_БитфонСервер.ПолучитьПараметрыОтправкиСМС(стрСтарЛогин, стрСтарПароль);
		стрСтарОтправитель = бит_БитфонСервер.ПолучитьОтправителяСМСПоУмолчанию();
	Исключение
		// у текущего пользователя может не быть прав к старым настройкам
	КонецПопытки;
		
	Если Не ЗначениеЗаполнено(настр.Отправитель) Тогда
		Если ЗначениеЗаполнено(стрСтарОтправитель) Тогда
			УстановитьНастройкуОтправкиSMS("Отправитель", стрСтарОтправитель);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(настр.Логин) Тогда
		Если ЗначениеЗаполнено(стрСтарЛогин) Тогда
			УстановитьНастройкуОтправкиSMS("Логин", стрСтарЛогин);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(настр.Пароль) Тогда
		Если ЗначениеЗаполнено(стрСтарПароль) Тогда
			УстановитьНастройкуОтправкиSMS("Пароль", стрСтарПароль);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьНастройкиОтправкиSMS()
	Пользователь = бит_ТелефонияСервер.ПолучитьТекущегоПользователя();
	СтруктураОтбора = Новый Структура("Пользователь", Пользователь);
	Возврат РегистрыСведений.бит_ОтправкаSMSНастройки.Получить(СтруктураОтбора);
КонецФункции

Процедура УстановитьНастройкуОтправкиSMS(стрНастройка, значениеНастройки)
	бит_ТелефонияСервер.УстановитьНастройкуТекущегоПользователя("бит_ОтправкаSMSНастройки", "бит_ОтправкаSMSСервер", стрНастройка, значениеНастройки);
КонецПроцедуры

#КонецОбласти
