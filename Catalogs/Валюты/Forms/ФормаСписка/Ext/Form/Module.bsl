// Обработчик события ПриСоздании формы.
// Устанавливает параметр КонецПериода запроса равным текущей дате.
//
&НаСервере
Процедура ПриСоздании(Отказ, СтандартнаяОбработка)
	
	ДатаКурса = НачалоДня(ТекущаяДата());
	Элементы.Курс.Заголовок = "Курс на " + Формат(ТекущаяДата(), "ДЛФ=DD");
	Элементы.Курс.Подсказка = Элементы.Курс.Заголовок;
	Список.Параметры.УстановитьЗначениеПараметра ("КонецПериода", ДатаКурса);
	
КонецПроцедуры

// Обработчик события "обработка выбора" формы
// Обработка выбора валюты при подборе из ОКВ
//
&НаКлиенте
Процедура ОбработкаВыбора(РезультатВыбора, ИсточникВыбора)
	
	Если ТипЗнч(РезультатВыбора) = Тип("Структура") Тогда
		ОткрытьФорму("Справочник.Валюты.ФормаОбъекта", РезультатВыбора,ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик нажатия кнопки "загрузить курсы".
// Получает и открывает форму загрузки курсов.
//
&НаКлиенте
Процедура КомандаЗагрузитьКурсыВыполнить()
	
	ОткрытьФорму("Обработка.КурсыВалютРБК.Форма.Форма");
	
КонецПроцедуры

// Обработчик нажатия кнопки "Подбор из ОКВ".
// Получает и открывает форму подбора валюты из классификатора ОКВ.
//
&НаКлиенте
Процедура КомандаПодборИзОКВВыполнить()
	
	ОткрытьФорму("Справочник.Валюты.Форма.ФормаПодбораВалютИзОКВ",, ЭтаФорма);
	
КонецПроцедуры

// Обработчик события "обработка оповещения" формы.
// Пересчитывает список валют.
//
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеКурсовВалют" Тогда
		Элементы.Валюты.Обновить();
	КонецЕсли;
	
КонецПроцедуры
