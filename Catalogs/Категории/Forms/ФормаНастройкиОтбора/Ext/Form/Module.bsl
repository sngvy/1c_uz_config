&НаКлиенте
Перем Массив;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВариантОтбора = Неопределено;
	Если Не Параметры.Свойство("Отбор", ВариантОтбора) Тогда
	
		Отказ = Истина;
		Возврат;
	
	КонецЕсли;
	
	Владелец = Неопределено;
	Если Не Параметры.Свойство("Владелец", Владелец) Тогда
	
		Отказ = Истина;
		Возврат;
	
	КонецЕсли;

	НоваяНастройка = Настройка.Добавить();
	Если ВариантОтбора <> Неопределено Тогда
	
		НоваяНастройка.Отбор = ВариантОтбора;
	
	КонецЕсли;
	
	ПеренестиБазовыеНастройки(Владелец);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Отборы = КомпоновщикНастроек.Настройки.Отбор;
	Для каждого Элемент Из Настройка[0].Отбор.Элементы Цикл
	
		Запись = Отборы.Элементы.Добавить(Тип(Элемент));
		ЗаполнитьЗначенияСвойств(Запись, Элемент);
	
	КонецЦикла;
	Настройка[0].Отбор = Отборы;
	
	Элементы.Настройка.ТекущаяСтрока = 0;
	Массив = Новый Массив();
	Для Индекс = 0 По Настройка[0].Отбор.Элементы.Количество() - 1 Цикл
		ЗаписатьЭлементОтбора(Настройка[0].Отбор.Элементы[Индекс], Массив);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	//Чуров
	Если Модифицированность Тогда
		Если Не ВыполняетсяЗакрытие Тогда	
			Отказ = Истина;
			ПоказатьВопрос(
				Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект),
				"Измененные данные будут сохранены!", 
				РежимДиалогаВопрос.ОКОтмена,
				,
				,
				"Изменение данных"
			);
		КонецЕсли;
	КонецЕсли;		
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса;
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ВыполняетсяЗакрытие = Истина;
		ОК(Неопределено);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПеренестиБазовыеНастройки(Владелец)

	Категореовед = ПланыВидовХарактеристик.ВидыСкоринга;
	
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;

	НастройкиВладельца = Категореовед.ПолучитьНастройки(
		Владелец,
		УникальныйИдентификатор
	);
	ИсточникНастроек = НастройкиВладельца["Источник"];
	НастройкиПоУмолчанию = НастройкиВладельца["ПоУмолчанию"];
	
	// TODO - Перенести в Управление СКД
	КомпоновщикНастроек.Инициализировать(ИсточникНастроек);
	КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиПоУмолчанию);

КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	Если Модифицированность Тогда
		Модифицированность = Ложь;
	КонецЕсли;
	
	Данные = Новый Структура;
	Данные.Вставить("Результат", Истина);
	Данные.Вставить("ПредставлениеОтбора", Строка(Настройка[0].Отбор));
	Данные.Вставить("Отбор", Настройка[0].Отбор);
	
	Закрыть(Данные);
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	ВернутьПрежниеНастройки();
	Модифицированность = Ложь;
	
	Данные = Новый Структура;
	Данные.Вставить("Результат", Ложь);
	
	Закрыть(Данные);
КонецПроцедуры

&НаКлиенте
Процедура ОтборПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ВернутьПрежниеНастройки()
	Настройка[0].Отбор.Элементы.Очистить();
	Для Индекс = 0 По Массив.Количество() - 1 Цикл 
		ВосстановитьЭлементОтбора(Настройка[0].Отбор.Элементы, Массив[Индекс]);
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьЭлементОтбора(Элементы, Массив)
	Если Массив.ТипЗначения = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
		НовыйОтбор = Элементы.Добавить(Массив.ТипЗначения);
		НовыйОтбор.Использование = Массив.Использование;
		НовыйОтбор.ЛевоеЗначение = Массив.ЛевоеЗначение;
		НовыйОтбор.ВидСравнения = Массив.ВидСравнения;
		НовыйОтбор.ПравоеЗначение = Массив.ПравоеЗначение;
	Иначе
		НовыйОтбор = Элементы.Добавить(Массив.ТипЗначения);
		НовыйОтбор.Использование = Массив.Использование;
        НовыйОтбор.ТипГруппы = Массив.ТипГруппы;
		Для Индекс = 0 По Массив.Элементы.Количество() - 1 Цикл 
			ВосстановитьЭлементОтбора(НовыйОтбор.Элементы, Массив.Элементы[Индекс]);
		КонецЦикла;	
	КонецЕсли;		
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьЭлементОтбора(Элемент, Массив)
	Если ТипЗнч(Элемент) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
		Массив.Добавить(Новый Структура("ТипЗначения, Использование, ЛевоеЗначение, ВидСравнения, ПравоеЗначение",
				ТипЗнч(Элемент), Элемент.Использование, Элемент.ЛевоеЗначение, Элемент.ВидСравнения,
				Элемент.ПравоеЗначение));
	Иначе
		МассивЭлементов = Новый Массив();		
		Массив.Добавить(Новый Структура("ТипЗначения, Использование, ТипГруппы, Элементы",
				ТипЗнч(Элемент), Элемент.Использование, Элемент.ТипГруппы, МассивЭлементов));
		Для Индекс = 0 По Элемент.Элементы.Количество()-1 Цикл
			ЗаписатьЭлементОтбора(Элемент.Элементы[Индекс], МассивЭлементов);		
		КонецЦикла;		
	КонецЕсли;
КонецПроцедуры
