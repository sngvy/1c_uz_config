// TODO - Структура обработки

Функция ПроверитьОшибки(ПараметрыПроверки) Экспорт

	Результат = ПолучитьСтруктуруТаблицыОтчета(
		ПараметрыПроверки["Менеджер"],
		ПараметрыПроверки["Макет"]
	);
	
	ДанныеОЯчейках = Новый Массив;
	
	НачалоДанных = ПараметрыПроверки["НачалоДанных"];
	НастройкиПроверки = ПараметрыПроверки["Настройки"];
	Для Каждого Элемент Из НастройкиПроверки Цикл
	
		ФункцияПроверки = Элемент["Функция"];
		Попытка
		
			НаборДанных = ФункцияПроверки.Старт(Элемент, НачалоДанных);
		
		Исключение
			ВызватьИсключение "Нет такой функции или задана не корректно: " + Строка(ФункцияПроверки);
		КонецПопытки;
		
		Для каждого ДанныеОбработки Из НаборДанных Цикл
		
			ЗаполнитьОшибку(Результат, Элемент, ДанныеОбработки);
			ЗаполнитьСведенияОЯчейках(ДанныеОЯчейках, Элемент, ДанныеОбработки);
		
		КонецЦикла;

	КонецЦикла;
	
	ПараметрыОшибок = Новый Структура;
	ПараметрыОшибок.Вставить("Ошибки", Результат);
	
	ОписаниеОшибок = УправлениеСКД.ПолучитьТабличныйДокумент(
		ПараметрыПроверки["Менеджер"],
		ПараметрыПроверки["Макет"],
		ПараметрыОшибок
	);
	
	СведенияООшибках = Новый Структура;
	СведенияООшибках.Вставить("ОписаниеОшибок", ОписаниеОшибок);
	СведенияООшибках.Вставить("Ячейки", ДанныеОЯчейках);
	
	Возврат СведенияООшибках;

КонецФункции

Процедура ЗаполнитьОшибку(Результат, ДанныеДляПроверки, ДанныеОбработки)

	Ошибка = Результат.Добавить();
	Ошибка.ИмяКолонки = ДанныеДляПроверки["ИмяКолонки"];
	Ошибка.ИмяСвойства = ДанныеДляПроверки["ИмяСвойства"];
	Ошибка.ТипСвойства = ДанныеДляПроверки["ТипДанных"];
	Ошибка.ЗначениеЯчейки = ДанныеОбработки["ЗначениеЯчейки"];
	Ошибка.НомерСтроки = ДанныеОбработки["НомерСтроки"];
	Ошибка.Значение = ДанныеОбработки["Значение"];

КонецПроцедуры

Функция ПолучитьСтруктуруТаблицыОтчета(Менеджер, Макет)

	Результат = УправлениеСКД.ПолучитьПоляМакета(Менеджер, Макет);

	Возврат Результат;

КонецФункции

Процедура ЗаполнитьСведенияОЯчейках(ДанныеОЯчейках, Элемент, Ошибка)

	Если ТипЗнч(Ошибка["ДанныеСтрок"]) = Тип("Массив") Тогда
	
		Для каждого НомерСтроки Из Ошибка["ДанныеСтрок"] Цикл
		
			ОписаниеЯчейки = ЗаполнитьСведенияОЯчейке(Элемент["ИмяСвойства"], НомерСтроки, Неопределено);
			ДанныеОЯчейках.Добавить(ОписаниеЯчейки);
		
		КонецЦикла;
		
		Возврат;

	КонецЕсли;
	
	ОписаниеЯчейки = ЗаполнитьСведенияОЯчейке(Элемент["ИмяСвойства"], Ошибка["ДанныеСтрок"], Элемент["ИмяКолонки"]);
	ДанныеОЯчейках.Добавить(ОписаниеЯчейки);

КонецПроцедуры // ()

Функция ЗаполнитьСведенияОЯчейке(ИмяСвойства, НомерСтроки, ИмяКолонки)

	ОписаниеЯчейки = Новый Структура;
	ОписаниеЯчейки.Вставить("ИмяСвойства", ИмяСвойства);
	ОписаниеЯчейки.Вставить("ИмяКолонки", ИмяКолонки);
	ОписаниеЯчейки.Вставить("НомерСтроки", НомерСтроки);
	
	Возврат ОписаниеЯчейки;

КонецФункции // ()

