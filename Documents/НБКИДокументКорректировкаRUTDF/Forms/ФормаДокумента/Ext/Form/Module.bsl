
&НаСервере
Функция СформироватьОтчетНаСервере()
	ДокОбъект = РеквизитФормыВЗначение("Объект");
	ТекстСообщенияФормирования = "";
	ТекстовыйДокумент = ДокОбъект.СформироватьФайлыКорректировкиRUTDF(ТекстСообщенияФормирования);
	Сообщить(ТекстСообщенияФормирования);  
	Возврат ТекстовыйДокумент;	
КонецФункции

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Сначала сохраните документ!";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;	
	
	Если НЕ ПроверкаИспользованияДействийКИ() Тогда
		Возврат;
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(Объект.НастройкаВыгрузки) И ЗначениеЗаполнено(Объект.ИмяФайла) И ТипЗнч(Объект.НастройкаВыгрузки) = Тип("СправочникСсылка.НастройкиВыгрузкиНБКИ_RUTDF") Тогда
		КаталогФайловВыгрузки = КаталогВыгрузкиВызовСервер();
		Путь = КаталогФайловВыгрузки + "\" + Объект.ИмяФайла;
		ПутьКФайлу = Путь;
		ТекстовыйДок = СформироватьОтчетНаСервере();  
		Попытка
			ТекстовыйДок.Записать(Путь, КодировкаТекста.ANSI, ,Ложь); 
			ТекстСообщения = "Выгрузка завершена!" + Символы.ПС + ТекстСообщения;    
			ПутьКФайлу = Путь;
		Исключение    
			ТекстСообщения = "Произошла ошибка сохранения файла в папку!" + Символы.ПС + ТекстСообщения;
		КонецПопытки; 
	ИначеЕсли ЗначениеЗаполнено(Объект.НастройкаВыгрузки) И ЗначениеЗаполнено(Объект.ИмяФайла) И ТипЗнч(Объект.НастройкаВыгрузки) = Тип("СправочникСсылка.НастройкиВыгрузкиКредитныхИсторий") Тогда
		КаталогФайловВыгрузки = КаталогВыгрузкиВызовСервер();
		Путь = КаталогФайловВыгрузки + "\" + Объект.ИмяФайла + ".xml";
		ПутьКФайлу = Путь;
		XMLдокумент = СформироватьОтчетНаСервереЕдиный();  
		Попытка
			XMLдокумент.Записать(Путь, КодировкаТекста.UTF8, ,Ложь); 
			ТекстСообщения = "Выгрузка завершена!" + Символы.ПС + ТекстСообщения; 
			ПутьКФайлу = Путь;
		Исключение    
			ТекстСообщения = "Произошла ошибка сохранения файла в папку!" + Символы.ПС + ТекстСообщения;
		КонецПопытки; 		
	Иначе  
		ТекстСообщения = "Не выбрана настройка для формирования отчета!";
	КонецЕсли;  
	
	Сообщение = Новый СообщениеПользователю();
	Сообщение.Текст = ТекстСообщения;
	Сообщение.Сообщить();

			

КонецПроцедуры 

&НаСервере
Функция КаталогВыгрузкиВызовСервер()
	Возврат Объект.НастройкаВыгрузки.КаталогДляВыгрузки;
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда  
		Объект.Дата = ТекущаяДата(); 
		Объект.Принято = Перечисления.СтатусыОтчетаНБКИ.ВОбработке;
	КонецЕсли;  
	
КонецПроцедуры

&НаСервере
Процедура НастройкаВыгрузкиПриИзмененииНаСервере()	
	ДокОбъект = РеквизитФормыВЗначение("Объект");
	Объект.ИмяФайла = ДокОбъект.СформироватьИмяФайлаПоБКИ();
КонецПроцедуры

&НаКлиенте
Процедура НастройкаВыгрузкиПриИзменении(Элемент)
	НастройкаВыгрузкиПриИзмененииНаСервере();
	ПроверкаВерсии();
КонецПроцедуры

&НаСервере
Процедура ОбработатьОтказНаСервере()
	ДокОбъект = РеквизитФормыВЗначение("Объект");
	ДокОбъект.ОбработатьRejectФайл();
	ЗначениеВРеквизитФормы(ДокОбъект, "Объект");
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтказ(Команда)
	ОбработатьОтказНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПутьФайлаОтказаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ДиалогВыбора.Заголовок = "Выберите файл REJET";
	
	Если ДиалогВыбора.Выбрать() Тогда
		ИмяФайлаОтказа = ДиалогВыбора.ПолноеИмяФайла;
		Объект.ПутьФайлаОтказа = ИмяФайлаОтказа; 
		Кодировка = ?(СтрНайти(ИмяФайлаОтказа, ".xml", НаправлениеПоиска.СКонца) > 0, КодировкаТекста.UTF8, КодировкаТекста.ANSI); 	
		Текст = Новый ЧтениеТекста(ИмяФайлаОтказа, Кодировка);	
		Объект.ТекстФайлаОтказа  =  Текст.Прочитать(); 	
	КонецЕсли;
	
КонецПроцедуры     

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеВидимостью();
	ЗапретитьФормироватьОтчетПовторно();
	УправлениеПодписью();
	ДоступностьЭЦП();
	
КонецПроцедуры


&НаСервере
Процедура УправлениеВидимостью()
	
	Если Объект.Проведен Тогда
		Элементы.ГруппаКорректировкаКИ.ТолькоПросмотр = Истина;
		
		Элементы.Подбор.Доступность = Ложь;
		Элементы.ПодборИзФайла.Доступность = Ложь;
		Элементы.ПодборПоНастройкам.Доступность = Ложь; 
		
		Элементы.ЗаполнитьДействияИСобытияКорректировки.Доступность = Ложь;
		Элементы.ПодобратьДатуПоследнегоПринятогоСобытия.Доступность = Ложь;

	КонецЕсли;  
	
	Если Объект.Принято = Перечисления.СтатусыОтчетаНБКИ.НеПринято Тогда
		Элементы.ГруппаКорректировкаКИ.ТолькоПросмотр = Истина;
		
		Элементы.Подбор.Доступность = Ложь;
		Элементы.ПодборИзФайла.Доступность = Ложь;
		Элементы.ПодборПоНастройкам.Доступность = Ложь; 
		
		Элементы.ЗаполнитьДействияИСобытияКорректировки.Доступность = Ложь;  
		Элементы.ПодобратьДатуПоследнегоПринятогоСобытия.Доступность = Ложь;
		
		ЭтаФорма.ТекущийЭлемент = ЭтаФорма.Элементы.ТекстФайлаОтказа;
		Элементы.REJECT.ТолькоПросмотр = Ложь;  
		Элементы.ОбработатьОтказ.Доступность = Истина;
	Иначе  
		Элементы.REJECT.ТолькоПросмотр = Истина; 
		Элементы.ОбработатьОтказ.Доступность = Ложь;
	КонецЕсли;
	
	Элементы.НепринятыйДокумент.ТолькоПросмотр = НЕ Объект.ЭтоКорректировкаРанееОтправленогоДокумента;
		
	
КонецПроцедуры  
  

&НаСервере
Процедура ПроверкаВерсии() 
	
	Если ТипЗнч(Объект.НастройкаВыгрузки) = Тип("СправочникСсылка.НастройкиВыгрузкиКредитныхИсторий") Тогда
		Возврат;
	КонецЕсли;	
	
	Если НЕ КредитныеИсторииИзменения.ЭтоСтаршаяВерсияРУТДФ(Объект.НастройкаВыгрузки.ВерсияRUTDF, Перечисления.ВерсияRUTDF.v4) Тогда 
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Данные операции не совместимы с текущей версией RUTDF. Необходимо выбрать настройку с версией 5.0";
		Сообщение.Сообщить();
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ПринятоПриИзменении(Элемент)
	УправлениеВидимостью(); 
	//ПроставитьЗаписямСтатус();
КонецПроцедуры 

&НаСервере
Процедура ПроставитьЗаписямСтатус()
	ДокОбъект = РеквизитФормыВЗначение("Объект");
	ДокОбъект.ПроставитьЗаписямСтатус();			
КонецПроцедуры  


&НаКлиенте
Процедура ЭтоКорректировкаРанееОтправленогоДокументаПриИзменении(Элемент)
	УправлениеВидимостью();
	Если НЕ Объект.ЭтоКорректировкаРанееОтправленогоДокумента Тогда
		ОчиститьНепринятыйДокумент();
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ОчиститьНепринятыйДокумент()
	Объект.НепринятыйДокумент = "";
КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)
	Объект.Объекты.Очистить();
	ОбъектыКлиент.Подбор(ЭтаФорма);
	ПодключитьОбработчикОжидания("ОбработатьРезультатПодбора", 1, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ПодборПоНастройкам(Команда)
	Объект.Объекты.Очистить();
	ФормаПодбор = ПолучитьФорму("Обработка.ПодборДО.Форма.Форма",, ЭтаФорма); 
	ФормаПодбор.Объект.ТипИсточника = Неопределено;
	ОткрытьФорму(ФормаПодбор); 
	ПодключитьОбработчикОжидания("ОбработатьРезультатПодбора", 1, Ложь);
КонецПроцедуры   

&НаСервере
Процедура ОбработатьРезультатПодбораСервер(КоличествоОбъектов) 
	
	РабочаяТЧ = Объект.тчКорректировка; 	
	РабочаяТЧ.Очистить();
	Для каждого стр из Объект.Объекты Цикл
		Нстр =  РабочаяТЧ.Добавить();
		Нстр.ОбъектУчета = Стр.Объект;
		Попытка
			Нстр.Должник = Стр.Объект.Должник;
		Исключение
			Нстр.Должник = Справочники.Контрагенты.ПустаяСсылка();
		КонецПопытки;	
	КонецЦикла;
	
	КоличествоОбъектов = РабочаяТЧ.Количество();	
КонецПроцедуры  

&НаКлиенте
Процедура ОбработатьРезультатПодбора() Экспорт
	КоличествоОбъектов = 0;
	ОбработатьРезультатПодбораСервер(КоличествоОбъектов);
	Если КоличествоОбъектов > 0 Тогда
		ОтключитьОбработчикОжидания("ОбработатьРезультатПодбора");
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыПриАктивизацииСтроки(Элемент)
	ОбработатьРезультатПодбора();
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыПриИзменении(Элемент)
	ОбработатьРезультатПодбора();
КонецПроцедуры


&НаКлиенте
Процедура ЭтоИсправлениеВсейКредитнойИнформацииПриИзменении(Элемент)
	УправлениеВидимостью();
КонецПроцедуры


&НаКлиенте
Процедура ПодборИзФайла(Команда)
	Объект.Объекты.Очистить();
	ОбъектыКлиент.ПодборИзДОК(ЭтаФорма);
	ПодключитьОбработчикОжидания("ОбработатьРезультатПодбора", 1, Ложь);
КонецПроцедуры    


&НаСервере
Процедура ЗаполнитьДействияИСобытияКорректировкиНаСервере(СтруктураИзРегистра)
	Для Каждого Стр из объект.тчКорректировка Цикл   
		ЗаполнитьЗначенияСвойств(Стр, СтруктураИзРегистра);
		Стр.Событие = СтруктураИзРегистра.СобытиеКорректировки;
		Стр.Операция = СтруктураИзРегистра.ОперацияКорректировки;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДействияИСобытияКорректировки(Команда)
	Если Элементы.тчКорректировка.ТекущаяСтрока = Неопределено Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Необходимо выбрать хотя бы один объект на корректировку!";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ЗакрыватьПриВыборе", Истина);
	ВладелецФормыПодбора = Этаформа;
	ОткрытьФорму("РегистрСведений.КредитныеИсторииДействияКорректировкиКИ.Форма.ФормаСписка", ПараметрыОткрытия, ВладелецФормыПодбора);
КонецПроцедуры 

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	ЗаполнитьДействияИСобытияКорректировкиНаСервере(ВыбранноеЗначение); 
КонецПроцедуры 

&НаСервере
Процедура тчКорректировкаОбъектУчетаПриИзмененииНаСервере(НомерСтроки, ТекОбъектУчета)
	Если ТипЗнч(ТекОбъектУчета) = Тип("СправочникСсылка.ДолговыеОбязательства") Тогда
		Объект.тчКорректировка[НомерСтроки - 1].Должник = ТекОбъектУчета.Должник;
	ИначеЕсли ТипЗнч(ТекОбъектУчета) = Тип("СправочникСсылка.Контрагенты") Тогда
		Объект.тчКорректировка[НомерСтроки - 1].Должник = ТекОбъектУчета;		
	Иначе
		Возврат;
	КонецЕсли;	 
КонецПроцедуры

&НаКлиенте
Процедура тчКорректировкаОбъектУчетаПриИзменении(Элемент)
	тчКорректировкаОбъектУчетаПриИзмененииНаСервере(ЭтаФорма.ТекущийЭлемент.ТекущиеДанные.НомерСтроки, ЭтаФорма.ТекущийЭлемент.ТекущиеДанные.ОбъектУчета);
КонецПроцедуры

&НаСервере
Процедура тчКорректировкаПричинаПриИзмененииНаСервере(ОперацияКорректировкиСерв, Причина, ИндексСтроки)
	Если ОперацияКорректировкиСерв <> Перечисления.ВидыОперацийИсправленияИУдаленияRUTDF.C_2 И ЗначениеЗаполнено(Причина) Тогда   			
		Сообщение = Новый СообщениеПользователю;	
		Сообщение.Поле = "Объект.тчКорректировка[" + ИндексСтроки + "].Операция";
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Текст = "Причину аннулирования можно заполнить только для операции С.2!";
		Сообщение.Сообщить();
		Объект.тчКорректировка[ИндексСтроки].Причина = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура тчКорректировкаПричинаПриИзменении(Элемент) 	
	ОперацияКорректировки = ЭтаФорма.ТекущийЭлемент.ТекущиеДанные.Операция;
	Причина = ЭтаФорма.ТекущийЭлемент.ТекущиеДанные.Причина;
	Индекс =  ЭтаФорма.ТекущийЭлемент.ТекущиеДанные.НомерСтроки - 1; 
	тчКорректировкаПричинаПриИзмененииНаСервере(ОперацияКорректировки, Причина, Индекс);
КонецПроцедуры 

&НаСервере
Процедура тчКорректировкаОперацияПриИзмененииНаСервере(ОперацияКорректировкиСерв, Причина, ИндексСтроки)
	Если ОперацияКорректировкиСерв <> Перечисления.ВидыОперацийИсправленияИУдаленияRUTDF.C_2 И ЗначениеЗаполнено(Причина) Тогда   			
		Сообщение = Новый СообщениеПользователю;	
		Сообщение.Поле = "Объект.тчКорректировка[" + ИндексСтроки + "].Операция";
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Текст = "Причину аннулирования можно заполнить только для операции С.2!";
		Сообщение.Сообщить();
		Объект.тчКорректировка[ИндексСтроки].Причина = "";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура тчКорректировкаОперацияПриИзменении(Элемент)
	ОперацияКорректировки = ЭтаФорма.ТекущийЭлемент.ТекущиеДанные.Операция;
	Причина = ЭтаФорма.ТекущийЭлемент.ТекущиеДанные.Причина;
	Индекс =  ЭтаФорма.ТекущийЭлемент.ТекущиеДанные.НомерСтроки - 1; 
	тчКорректировкаОперацияПриИзмененииНаСервере(ОперацияКорректировки, Причина, Индекс);
КонецПроцедуры

&НаСервере
Процедура тчКорректировкаНомерБлокаПриИзмененииНаСервере(ТекСтрока)
	Если КредитныеИсторииИзменения.ЭтоКорректировкапоказателейНеСвязанныхСЗаписьюКредитнойИстории(ТекСтрока) Тогда
		Если Не ЗначениеЗаполнено(ТекСтрока.ДатаСобытияСодержавшегоКорректируемыйБлок) Тогда
			Сообщение = Новый СообщениеПользователю;
			ИндексСтроки =  ТекСтрока.НомерСтроки - 1;
			Сообщение.Поле = "Объект.тчКорректировка[" + ИндексСтроки + "].ДатаСобытияСодержавшегоКорректируемыйБлок";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Текст = "Для данного вида события корректировки необходимо заполнить в показателе: 0.5 Дата события указать дату события, содержавшего корректируемый блок!";
			Сообщение.Сообщить();
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура тчКорректировкаНомерБлокаПриИзменении(Элемент)
	СтруктураСтроки = Новый Структура();
	СтруктураСтроки.Вставить("Событие",  ЭтаФорма.ТекущийЭлемент.ТекущиеДанные.Событие);
	СтруктураСтроки.Вставить("Операция",  ЭтаФорма.ТекущийЭлемент.ТекущиеДанные.Операция);
	СтруктураСтроки.Вставить("ЧастьКИ",  ЭтаФорма.ТекущийЭлемент.ТекущиеДанные.ЧастьКИ);
	СтруктураСтроки.Вставить("НомерСтроки",  ЭтаФорма.ТекущийЭлемент.ТекущиеДанные.НомерСтроки);
	СтруктураСтроки.Вставить("ДатаСобытияСодержавшегоКорректируемыйБлок",  ЭтаФорма.ТекущийЭлемент.ТекущиеДанные.ДатаСобытияСодержавшегоКорректируемыйБлок);
	тчКорректировкаНомерБлокаПриИзмененииНаСервере(СтруктураСтроки);
КонецПроцедуры  


&НаСервере
Процедура тчКорректировкаДатаСобытияСодержавшегоКорректируемыйБлокПриИзмененииНаСервере(ТекСтрока)
	Если КредитныеИсторииИзменения.ЭтоКорректировкапоказателейНеСвязанныхСЗаписьюКредитнойИстории(ТекСтрока) Тогда
		Если Не ЗначениеЗаполнено(ТекСтрока.ДатаСобытияСодержавшегоКорректируемыйБлок) Тогда
			Сообщение = Новый СообщениеПользователю;
			ИндексСтроки =  ТекСтрока.НомерСтроки - 1;
			Сообщение.Поле = "Объект.тчКорректировка[" + ИндексСтроки + "].ДатаСобытияСодержавшегоКорректируемыйБлок";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Текст = "Для данного вида события корректировки необходимо заполнить в показателе: 0.5 Дата события указать дату события, содержавшего корректируемый блок!";
			Сообщение.Сообщить();
		КонецЕсли;
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура тчКорректировкаДатаСобытияСодержавшегоКорректируемыйБлокПриИзменении(Элемент)
	СтруктураСтроки = Новый Структура();
	СтруктураСтроки.Вставить("Событие",  ЭтаФорма.ТекущийЭлемент.ТекущиеДанные.Событие);
	СтруктураСтроки.Вставить("Операция",  ЭтаФорма.ТекущийЭлемент.ТекущиеДанные.Операция);
	СтруктураСтроки.Вставить("ЧастьКИ",  ЭтаФорма.ТекущийЭлемент.ТекущиеДанные.ЧастьКИ);
	СтруктураСтроки.Вставить("НомерСтроки",  ЭтаФорма.ТекущийЭлемент.ТекущиеДанные.НомерСтроки);
	СтруктураСтроки.Вставить("ДатаСобытияСодержавшегоКорректируемыйБлок",  ЭтаФорма.ТекущийЭлемент.ТекущиеДанные.ДатаСобытияСодержавшегоКорректируемыйБлок);
	тчКорректировкаДатаСобытияСодержавшегоКорректируемыйБлокПриИзмененииНаСервере(СтруктураСтроки);

КонецПроцедуры

&НаСервере
Процедура СкорректироватьАдресаПолностьюПриИзмененииНаСервере() 
	Если Объект.СкорректироватьАдресаПолностью Тогда
		ОчиститьНастройкиУАдресныхБлоков();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СкорректироватьАдресаПолностьюПриИзменении(Элемент)
	СкорректироватьАдресаПолностьюПриИзмененииНаСервере();
КонецПроцедуры  

&НаСервере
Процедура ОчиститьНастройкиУАдресныхБлоков()
	Для Каждого Стр из Объект.ТчКорректировка Цикл
		Если СтрНайти("С8C8C9С9", Стр.НомерБлока) > 0 Тогда
			Стр.ПолеБлока = "";
			Стр.СтрокаНаКорректировку= "";
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры    

&НаСервере
Функция ПроверкаИспользованияДействийКИ()
	ПроверкаПройдена = Истина;    
	Если ТипЗнч(Объект.НастройкаВыгрузки) = Тип("СправочникСсылка.НастройкиВыгрузкиКредитныхИсторий") Тогда
		ЭтоВерсияСтарше6 = Истина;
	Иначе
		ЭтоВерсияСтарше6 = КредитныеИсторииИзменения.ЭтоСтаршаяВерсияРУТДФ(Объект.НастройкаВыгрузки.ВерсияRUTDF, Перечисления.ВерсияRUTDF.v6);  
	КонецЕсли;
	
	Для Каждого Стр ИЗ Объект.тчКорректировка Цикл  
		Если ЭтоВерсияСтарше6 И Стр.Событие.КодНБКИ = "3.2" И Стр.Операция = Перечисления.ВидыОперацийИсправленияИУдаленияRUTDF.C_2 
			И НЕ ЗначениеЗаполнено(Стр.ПараметрStartDate) Тогда
			
			Стр.ПараметрStartDate = ПодобратьДатуСобытия(Стр.ОбъектУчета, Справочники.КредитныеИсторииСобытияСделки.НайтиПоРеквизиту("КодНБКИ", "2.3"));
			Стр.Причина = "0";
			
			Сообщение = Новый СообщениеПользователю(); 
			Текст = "Внимание! Совокупность операций 3.2 С.2 не применяется для корректировки Всей динамической информации сделки. "
				+ "Этими дейтвиями Вы можете исключить только последний отмененный платеж в соответствии с п.22.3 ст.5 ФЗ №353. Для этого: а) "
				+ " отмените проведение оспоренного платежа, б)заполните дату события ПРЕДЫДУЩЕГО платежа в параметре StartDate "
				+ "или подтвердите предложенную дату. Формирование отчета прервано!";		
			Сообщение.Текст = Текст;
			Сообщение.Сообщить(); 
			ПроверкаПройдена = Ложь;
			Прервать;
		КонецЕсли;	
	КонецЦикла;	
	Возврат ПроверкаПройдена;
КонецФункции  


&НаСервере
Функция ПодобратьДатуСобытия(ОбъектУчета, Событие)
	
	НаборЗаписей = РегистрыСведений.КредитныеИсторииДанныеСделки.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Сделка.Установить(ОбъектУчета); 
	НаборЗаписей.Отбор.Событие.Установить(Событие);
	НаборЗаписей.Прочитать(); 
	
		
	Если НаборЗаписей.Количество() > 0 Тогда 
		Если НаборЗаписей.Количество() = 1 Тогда
			Возврат НаборЗаписей[0].ДатаСобытия;
		КонецЕсли;	
		
		Возврат НаборЗаписей[НаборЗаписей.Количество() - 1].ДатаСобытия;
		
	Иначе 
		Возврат Дата(1, 1, 1);
	КонецЕсли;			
	
КонецФункции 

&НаСервере
Процедура ПодобратьДатуПоследнегоПринятогоСобытияНаСервере()
	ВыборкаДат = КредитныеИсторииИзменения.ВернутьПоследниеСобытияПоСделкам(Объект.тчКорректировка.Выгрузить().ВыгрузитьКолонку("ОбъектУчета"), Истина);
	
	Пока ВыборкаДат.Следующий() Цикл
		Стр = Объект.тчКорректировка.НайтиСтроки(Новый Структура("ОбъектУчета", ВыборкаДат.Сделка)); 
		Если стр.Количество() > 0 Тогда
			стр[0].ПараметрStartDate =  ВыборкаДат.ДатаСобытия;
		КонецЕсли;		
	КонецЦикла;			
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьДатуПоследнегоПринятогоСобытия(Команда)
	ПодобратьДатуПоследнегоПринятогоСобытияНаСервере();
КонецПроцедуры


#Область ЭЦП

&НаКлиенте
Процедура ДоступностьЭЦП()

	ВидимостьЭлементовЭЦП(Ложь);
	Если УправлениеДоступами.ЕстьДоступЭЦП() Тогда
	
		ВидимостьЭлементовЭЦП(Истина)
	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВидимостьЭлементовЭЦП(ЗначениеВидимости)

	Элементы.Подпись.Видимость = ЗначениеВидимости;
	Элементы.Архив.Видимость = ЗначениеВидимости;
	
	Элементы.ЭлементФайлы.Видимость = ЗначениеВидимости;
	Элементы.АрхивНБКИ.Видимость = ЗначениеВидимости;

КонецПроцедуры

&НаКлиенте
Процедура Прикрепить(Команда)

	Попытка
	
		ФайлКлиента = УправлениеФайловаяСистема.НайтиФайл(Объект.ИмяФайла, КаталогВыгрузкиВызовСервер());
	
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ОписаниеОшибки();
		Сообщение.Сообщить();
		Возврат;
	КонецПопытки;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Ссылка", "ЭлементФайлы");
	ДополнительныеПараметры.Вставить("КонтрольФормы", Истина);
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеПомещения", ЭтаФорма, ДополнительныеПараметры);
	
	АдресХранилища = "";
	НачатьПомещениеФайлаНаСервер(
		ОповещениеОЗавершении,
		,,
		АдресХранилища,
		ФайлКлиента.ПолноеИмя,
		ЭтаФорма.УникальныйИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура ПослеПомещения(ПомещенныйФайл, ПараметрыПомещения) Экспорт

	Если НЕ ЭтоАдресВременногоХранилища(ПомещенныйФайл.Адрес) Тогда
	
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не удалось поместить файл";
		Сообщение.Сообщить();
		Возврат;
	
	КонецЕсли;

	ДанныеДляПомещения = Новый Структура;
	ДанныеДляПомещения.Вставить("АдресХранилища", ПомещенныйФайл.Адрес);
	ДанныеДляПомещения.Вставить("ИмяБезРасширения", ПомещенныйФайл.СсылкаНаФайл.Файл.ИмяБезРасширения);
	ДанныеДляПомещения.Вставить("Расширение", ПомещенныйФайл.СсылкаНаФайл.Расширение);
	
	Попытка
	
		ЭлементыСправочникаФайлы = ПолучитьСправочникФайл(ДанныеДляПомещения);
		Если ЭлементыСправочникаФайлы.Количество() = 1 Тогда
		
			УстановитьСсылкуНаСправочникФайлы(ЭлементыСправочникаФайлы, ПараметрыПомещения);
		
		КонецЕсли;
	
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не удалось прикрепить файл: " + ОписаниеОшибки();
		Сообщение.Сообщить();
		Возврат;
	КонецПопытки;
	
	ЗанестиВБазу = Ложь;
	Если ПараметрыПомещения.Свойство("Архив", ЗанестиВБазу)
		И ЗанестиВБазу Тогда
		
		ЗашифроватьФайл(Объект.АрхивНБКИ, Истина);
	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьСсылкуНаСправочникФайлы(ЭлементыСправочникаФайлы, ПараметрыПомещения);

	Объект[ПараметрыПомещения["Ссылка"]] = ЭлементыСправочникаФайлы[0];
	
	Если ПараметрыПомещения["КонтрольФормы"] Тогда
	
		ЗапретитьФормироватьОтчетПовторно();
		УправлениеПодписью();
	
	КонецЕсли;

	
	//Объект.ЭлементФайлы = ЭлементыСправочникаФайлы[0];
	//ЗапретитьФормироватьОтчетПовторно();
	//УправлениеПодписью();

КонецПроцедуры


&НаКлиенте
Процедура ЗапретитьФормироватьОтчетПовторно()

	Если ЗначениеЗаполнено(Объект.ЭлементФайлы) Тогда
	
		Элементы.ФормаСформироватьОтчет.Доступность = Ложь;
		Элементы.ФормаПрикрепить.Доступность = Ложь;
	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УправлениеПодписью()

	Элементы.ФормаГруппаПодписание.Доступность = Ложь;
	Если ЗначениеЗаполнено(Объект.ЭлементФайлы) Тогда
	
		Элементы.ФормаГруппаПодписание.Доступность = Истина;
		Элементы.ФормаПрикрепить.Доступность = Ложь;
	
	КонецЕсли;

КонецПроцедуры


&НаСервере
Функция ПолучитьСправочникФайл(ДанныеДляПомещения)

	ДанныеДляПомещения.Вставить("ПапкаВладелец", Справочники.ПапкиФайлов.НБКИ);
	ЭлементыСправочникаФайлы = Обработки.Интерфейс_Файлы.ПолучитьФайлы(ДанныеДляПомещения);
	
	Возврат ЭлементыСправочникаФайлы;

КонецФункции // ()


&НаКлиенте
Процедура Подписать(Команда)

	РаботаСФайламиКлиент.ПодписатьФайл(Объект.ЭлементФайлы, УникальныйИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура Зашифровать(Команда)

	ЗашифроватьФайл(Объект.ЭлементФайлы);
	//ДанныеФайла = РаботаСФайламиСлужебныйВызовСервера.ПолучитьДанныеФайлаИКоличествоВерсий(Объект.ЭлементФайлы);
	//
	//ПараметрыОбработчика = Новый Структура;
	//ПараметрыОбработчика.Вставить("ДанныеФайла", ДанныеФайла);
	//ПараметрыОбработчика.Вставить("ОбъектСсылка", Объект.ЭлементФайлы);
	//
	//Обработчик = Новый ОписаниеОповещения("ПослеШифрования", ЭтаФорма, ПараметрыОбработчика);
	//
	//РаботаСФайламиСлужебныйКлиент.Зашифровать(Обработчик, ДанныеФайла, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеШифрования(Результат, ПараметрыШифрования) Экспорт

	Если Не Результат.Успех Тогда
		Возврат;
	КонецЕсли;
	
	ИмяРабочегоКаталога = РаботаСФайламиСлужебныйКлиент.РабочийКаталогПользователя();
	
	МассивФайловВРабочемКаталогеДляУдаления = Новый Массив;
	
	ЗашифроватьСервер(
		Результат.МассивДанныхДляЗанесенияВБазу,
		Результат.МассивОтпечатков,
		МассивФайловВРабочемКаталогеДляУдаления,
		ИмяРабочегоКаталога,
		ПараметрыШифрования.ОбъектСсылка);
	
	РаботаСФайламиСлужебныйКлиент.ИнформироватьОШифровании(
		МассивФайловВРабочемКаталогеДляУдаления,
		ПараметрыШифрования.ДанныеФайла.Владелец,
		ПараметрыШифрования.ОбъектСсылка);

	Если ПараметрыШифрования["Архив"] Тогда
	
		РаботаСФайламиСлужебныйКлиент.СохранитьФайлВместеСПодписью(
			Объект.АрхивНБКИ,
			УникальныйИдентификатор);
	
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗашифроватьСервер(МассивДанныхДляЗанесенияВБазу, МассивОтпечатков, 
	МассивФайловВРабочемКаталогеДляУдаления,
	ИмяРабочегоКаталога, ОбъектСсылка)
	
	Зашифровать = Истина;
	РаботаСФайламиСлужебный.ЗаписатьИнформациюОШифровании(
		ОбъектСсылка,
		Зашифровать,
		МассивДанныхДляЗанесенияВБазу,
		Неопределено,  // УникальныйИдентификатор
		ИмяРабочегоКаталога,
		МассивФайловВРабочемКаталогеДляУдаления,
		МассивОтпечатков);
	
КонецПроцедуры

&НаКлиенте
Процедура Расшифровать(Команда)

	ДанныеФайла = РаботаСФайламиСлужебныйВызовСервера.ПолучитьДанныеФайлаИКоличествоВерсий(Объект.ЭлементФайлы);
	
	ПараметрыОбработчика = Новый Структура;
	ПараметрыОбработчика.Вставить("ДанныеФайла", ДанныеФайла);
	ПараметрыОбработчика.Вставить("ОбъектСсылка", Объект.ЭлементФайлы);
	Обработчик = Новый ОписаниеОповещения("ПослеРасшифровки", ЭтотОбъект, ПараметрыОбработчика);
	
	РаботаСФайламиСлужебныйКлиент.Расшифровать(
		Обработчик,
		ДанныеФайла.Ссылка,
		УникальныйИдентификатор,
		ДанныеФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеРасшифровки(Результат, ПараметрыВыполнения) Экспорт
	
	Если Результат = Ложь Или Не Результат.Успех Тогда
		Возврат;
	КонецЕсли;
	
	ИмяРабочегоКаталога = РаботаСФайламиСлужебныйКлиент.РабочийКаталогПользователя();
	
	РасшифроватьСервер(
		Результат.МассивДанныхДляЗанесенияВБазу,
		ИмяРабочегоКаталога,
		ПараметрыВыполнения.ОбъектСсылка);
	
	РаботаСФайламиСлужебныйКлиент.ИнформироватьОРасшифровке(
		ПараметрыВыполнения.ДанныеФайла.Владелец,
		ПараметрыВыполнения.ОбъектСсылка);

КонецПроцедуры

&НаСервере
Процедура РасшифроватьСервер(МассивДанныхДляЗанесенияВБазу, 
	ИмяРабочегоКаталога, ОбъектСсылка)
	
	Зашифровать = Ложь;
	МассивОтпечатков = Новый Массив;
	МассивФайловВРабочемКаталогеДляУдаления = Новый Массив;
	
	РаботаСФайламиСлужебный.ЗаписатьИнформациюОШифровании(
		ОбъектСсылка,
		Зашифровать,
		МассивДанныхДляЗанесенияВБазу,
		Неопределено,  // УникальныйИдентификатор
		ИмяРабочегоКаталога,
		МассивФайловВРабочемКаталогеДляУдаления,
		МассивОтпечатков);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВместеСПодписью(Команда)
	
	РаботаСФайламиСлужебныйКлиент.СохранитьФайлВместеСПодписью(
		Объект.ЭлементФайлы,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура СобратьВАрхив(Команда)

	АдресАрхива = АрхивироватьНБКИФайлы();
	Если НЕ ЗначениеЗаполнено(АдресАрхива) Тогда
	
		Возврат;
	
	КонецЕсли;
	
	ЗанестиАрхивВБазу(АдресАрхива);

КонецПроцедуры

&НаКлиенте
Функция АрхивироватьНБКИФайлы()

	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок = "Выберете отчет и подпись";
	Диалог.Фильтр = "Все файлы (*.*)|*.*";
	Диалог.МножественныйВыбор = Истина;
	
	Если НЕ Диалог.Выбрать() Тогда
	
		Возврат "";
	
	КонецЕсли;
	
	ВыбранныеФайлы = Диалог.ВыбранныеФайлы;
	Если ВыбранныеФайлы.Количество() = 0 Тогда
	
		Возврат ""
	
	КонецЕсли;
	
	ПервыйФайл = ВыбранныеФайлы[0];
	ФайлПервыйФайл = Новый Файл(ПервыйФайл);
	Каталог = ФайлПервыйФайл.Путь;
	АдресАрхива = Каталог + ПолучитьРазделительПутиКлиента() + Объект.ИмяФайла + ".zip";
	
	НовыйАрхив = Новый ЗаписьZipФайла(
		АдресАрхива,
		"",
		,
		МетодСжатияZIP.Сжатие,
		УровеньСжатияZIP.Оптимальный);
	
	Для каждого ВыбранныйФайл Из ВыбранныеФайлы Цикл
	
		НовыйАрхив.Добавить(ВыбранныйФайл);
	
	КонецЦикла;
	
	НовыйАрхив.Записать();
	
	Возврат АдресАрхива;

КонецФункции // ()

&НаКлиенте
Функция ЗанестиАрхивВБазу(АдресАрхива)

	Архив = Новый Файл(АдресАрхива);
	
	Попытка
	
		ФайлКлиента = УправлениеФайловаяСистема.НайтиФайл(Архив.Имя, Архив.Путь);
	
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ОписаниеОшибки();
		Сообщение.Сообщить();
		Возврат Ложь;
	КонецПопытки;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Ссылка", "АрхивНБКИ");
	ДополнительныеПараметры.Вставить("КонтрольФормы", Ложь);
	ДополнительныеПараметры.Вставить("Архив", Истина);
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеПомещения", ЭтаФорма, ДополнительныеПараметры);
	
	АдресХранилища = "";

	НачатьПомещениеФайлаНаСервер(
		ОповещениеОЗавершении,
		,,
		АдресХранилища,
		ФайлКлиента.ПолноеИмя,
		ЭтаФорма.УникальныйИдентификатор);

	Возврат Истина;

КонецФункции // ()

&НаКлиенте
Процедура ЗашифроватьФайл(СсылкаНаФайл, Архив = Ложь)

	ДанныеФайла = РаботаСФайламиСлужебныйВызовСервера.ПолучитьДанныеФайлаИКоличествоВерсий(СсылкаНаФайл);
	
	ПараметрыОбработчика = Новый Структура;
	ПараметрыОбработчика.Вставить("ДанныеФайла", ДанныеФайла);
	ПараметрыОбработчика.Вставить("ОбъектСсылка", СсылкаНаФайл);
	ПараметрыОбработчика.Вставить("Архив", Архив);
	
	Обработчик = Новый ОписаниеОповещения("ПослеШифрования", ЭтаФорма, ПараметрыОбработчика);
	
	РаботаСФайламиСлужебныйКлиент.Зашифровать(Обработчик, ДанныеФайла, УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти    

#Область ПроверкаСформированногоФайла

&НаСервере
Функция ПутьКСформированномуXML()

	Возврат Объект.НастройкаВыгрузки.КаталогДляВыгрузки + ПолучитьРазделительПути() + Объект.ИмяФайла + ".xml";

КонецФункции // ()

&НаКлиенте
Процедура ВыполнитьПроверку(Команда)

	Если ПустаяСтрока(Объект.ИмяФайла) Тогда
	
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Файл не сформирован";
		Сообщение.Поле = "ИмяФайла";
		Сообщение.Сообщить();
		
		Возврат;
	
	КонецЕсли;
	
	ПутьКXML = ПутьКСформированномуXML();
	
	Если ВыполнятьНаСервере Тогда
	
		ДвДанные = Новый ДвоичныеДанные(ПутьКXML);
		
		АдресXML = ПоместитьВоВременноеХранилище(ДвДанные);
	
	КонецЕсли;

	Ошибки = Неопределено;
	Если ВыполнятьНаСервере Тогда
	
		Ошибки = ВыполнитьПроверкуНаСервере(АдресXML, ПутьКXML);
	
	Иначе
	
		Ошибки = ВалидацияXMLПоСхеме.ПроверитьXMLПоНБКИ(ПутьКXML, АдресXML);
	
	КонецЕсли;
	
	Если ПустаяСтрока(Ошибки) Тогда
	
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Проверка успешно пройдена";
		Сообщение.Сообщить();
	
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ВыполнитьПроверкуНаСервере(Знач АдресXML, Знач ПолноеИмя)

	Возврат ВалидацияXMLПоСхеме.ПроверитьXMLПоНБКИ(ПолноеИмя, АдресXML);

КонецФункции // ()

#КонецОбласти


&НаСервере
Функция СформироватьОтчетНаСервереЕдиный()
		
	ДокОбъект = РеквизитФормыВЗначение("Объект");
	ТекстСообщенияФормирования = "";
	ДокументXML = ДокОбъект.СформироватьФайлКорректировкиXML(ТекстСообщенияФормирования);
	Сообщить(ТекстСообщенияФормирования);  
	Возврат ДокументXML;
КонецФункции

&НаКлиенте
Процедура ПутьКФайлуОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(ПутьКФайлу) Тогда
		НачатьЗапускПриложения(Новый ОписаниеОповещения("ПутьКФайлуОткрытиеЗавершение", ЭтаФорма), ПутьКФайлу);
	КонецЕсли;  
	
КонецПроцедуры  

&НаКлиенте
Процедура ПутьКФайлуОткрытиеЗавершение(КодВозврата, ДополнительныеПараметры) Экспорт
	//Асинхронное преобразование попросило создать пустое Описание оповещения
КонецПроцедуры



