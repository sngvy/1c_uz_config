
#Область ПрограммныйИнтерфейс

Функция УбратьЛишниеСимволы(Знач Текст, Знач ЧитаемыеСимволы, НомерМожетСодержатьТочку) Экспорт

	зСтрока = БазовоеФорматирование(Текст);
	ПодходящиеСимволы = ПодготовитьСимволыДляПроверки(ЧитаемыеСимволы);
	ТипЧисло  = Новый ОписаниеТипов("Число");
	
	НоваяСтрока = "";

	Для Сч = 1 По СтрДлина(зСтрока) Цикл
	
		Если ОставляемСимвол(зСтрока, Сч, НомерМожетСодержатьТочку, ПодходящиеСимволы, ТипЧисло) Тогда
		
			Символ = Сред(зСтрока, Сч, 1);
			НоваяСтрока = НоваяСтрока + Символ;
		
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат НоваяСтрока;
	
КонецФункции

// Убирает не значащие для поиска символы
//
// Параметры:
//  Текст  - Строка - Текст из которого требуется убрать символы
//
// Возвращаемое значение:
//   Строка   - Результирующий текст
//
Функция УбратьСимволыНеУчавствующиеВПоиске(Знач Текст) Экспорт

	Возврат МинимальноНеобходимоеФорматирование(Текст);

КонецФункции // ()

// Приводит ФИО к поисковому виду
//
// Параметры:
//  ФИО  - Строка - ФИО из которое требуется отформатировать
//
// Возвращаемое значение:
//   Строка   - ФИО для поиска
//
Функция ФорматироватьФИОДляПоиска(Знач ФИО) Экспорт

	ФИО = НРег(ФИО);
	ФИО = УбратьСимволыНеУчавствующиеВПоиске(ФИО);
	ФИО = УбратьСпецифичныеДляЛаколизацийКомпонентыФИО(ФИО);
	ФИО = ПоставитьЗаглавныеБуквы(ФИО);
	
	Возврат ФИО;

КонецФункции // ()

#КонецОбласти

#Область ТехническийПодготовкаТекста

Функция МинимальноНеобходимоеФорматирование(Знач Текст)

	НовыйТекст = Текст;
	НовыйТекст = СтрЗаменить(НовыйТекст, Символы.ПС, " ");
	НовыйТекст = СтрЗаменить(НовыйТекст, Символ(182), " "); // "¶"
	НовыйТекст = СтрЗаменить(НовыйТекст, "ё", "е");
	
	Возврат НовыйТекст;

КонецФункции // ()

Функция КомпонентыФИО()

	Элементы = Новый Массив;
	
	// Тюркские отчества
	Элементы.Добавить(" оглы");
	Элементы.Добавить(" кызы");
	Элементы.Добавить(" улы");
	Элементы.Добавить(" оглу");
	Элементы.Добавить(" уулу");
	Элементы.Добавить(" гызы");
	
	Возврат Элементы;

КонецФункции // ()

Функция УбратьСпецифичныеДляЛаколизацийКомпонентыФИО(Знач ФИО)

	Элементы = КомпонентыФИО();
	Для каждого Элемент Из Элементы Цикл
	
		ФИО = СтрЗаменить(ФИО, Элемент, "");
	
	КонецЦикла;
	
	Возврат ФИО;

КонецФункции // ()

Функция ПоставитьЗаглавныеБуквы(Знач Текст)

	Элементы = СтрРазделить(Текст, " ");
	НовыеЭлементы = Новый Массив;
	Для каждого Элемент Из Элементы Цикл
	
		ЭлементСЗаглавной = ВРег(Лев(Элемент, 1)) + Прав(Элемент, СтрДлина(Элемент) - 1);
		НовыеЭлементы.Добавить(ЭлементСЗаглавной);
	
	КонецЦикла;
	
	Возврат СтрСоединить(НовыеЭлементы, " ");

КонецФункции // ()

Функция БазовоеФорматирование(Знач Текст)
	
	НовыйТекст = МинимальноНеобходимоеФорматирование(Текст);
	
	НовыйТекст = СтрЗаменить(НовыйТекст, "_", "");
	НовыйТекст = СтрЗаменить(НовыйТекст, "—", "-");
	НовыйТекст = СтрЗаменить(НовыйТекст, "ЛИСТ2", "ЛИСТ 2-");  
	НовыйТекст = СтрЗаменить(НовыйТекст, "ЛИСТ 2", "ЛИСТ 2-");
	НовыйТекст = СтрЗаменить(НовыйТекст, "№2", "№ 2-");
	НовыйТекст = СтрЗаменить(НовыйТекст, "№ 2", "№ 2-");
	НовыйТекст = СтрЗаменить(НовыйТекст, " -", "-");
	НовыйТекст = СтрЗаменить(НовыйТекст, "- ", "-"); 
	НовыйТекст = СтрЗаменить(НовыйТекст, "---", "-");
	НовыйТекст = СтрЗаменить(НовыйТекст, "--", "-");  
	НовыйТекст = СтрЗаменить(НовыйТекст, "2-.", "2.");
	
	НовыйТекст = СтрЗаменить(НовыйТекст, "конейками", "копейками");
	НовыйТекст = СтрЗаменить(НовыйТекст, "конейкам", "копейкам");
	НовыйТекст = СтрЗаменить(НовыйТекст, "конейках", "копейках");
	НовыйТекст = СтрЗаменить(НовыйТекст, "конейкой", "копейкой");
	НовыйТекст = СтрЗаменить(НовыйТекст, "конейка", "копейка");
	НовыйТекст = СтрЗаменить(НовыйТекст, "конейки", "копейки");
	НовыйТекст = СтрЗаменить(НовыйТекст, "конейке", "копейке");
	НовыйТекст = СтрЗаменить(НовыйТекст, "конейку", "копейку");
	НовыйТекст = СтрЗаменить(НовыйТекст, "конеек", "копеек");
	НовыйТекст = СтрЗаменить(НовыйТекст, "кон.", "коп.");			
	
	Возврат НовыйТекст + " ";

КонецФункции // ()

Функция ПодготовитьСимволыДляПроверки(ЧитаемыеСимволы)

	Данные = Новый Соответствие;
	
	Для Номер = 1 По СтрДлина(ЧитаемыеСимволы) Цикл
	
		Символ = Сред(ЧитаемыеСимволы, Номер, 1);
		Данные.Вставить(Символ, Истина);
	
	КонецЦикла;
	
	Возврат Данные;

КонецФункции // ()

Функция ОставляемСимвол(Текст, Номер, НомерМожетСодержатьТочку, ЧитаемыеСимволы, ТипЧисло)

	Символ = Сред(Текст, Номер, 1);
	
	Если Символ = " " Тогда
	
		Возврат Истина;
	
	КонецЕсли;
	
	Если ЧитаемыеСимволы.Получить(Символ) <> Неопределено Тогда
	
		Возврат Истина;
	
	КонецЕсли;

	// если точка и предущий символ число , то предпологаем что это дата через точку
	Если Номер > 1 И Символ = "."
		И СтрокаВЧисло(Сред(Текст, Номер - 1, 1), ТипЧисло) <> Неопределено Тогда
		
		Возврат Истина;
	
	КонецЕсли;
	
	//если точка и далее пробел, то предпологаем это конец строки 
	Если Символ = "." И НомерМожетСодержатьТочку
		И Номер < СтрДлина(Текст) И Сред(Текст, Номер + 1, 1) = " " Тогда
		
		Возврат Ложь;
	
	КонецЕсли;
	
	Если Символ = "." И НомерМожетСодержатьТочку Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции // ()

// Преобразует исходную строку в число без вызова исключений.
//
// Параметры:
//   Значение - Строка - Строка, которую необходимо привести к числу.
//                       Например, "10", "+10", "010", вернет 10;
//                                 "(10)", "-10",вернет -10;
//                                 "10,2", "10.2",вернет 10.2;
//                                 "000", " ", "",вернет 0;
//                                 "10текст", вернет Неопределено.
//
// Возвращаемое значение:
//   Число, Неопределено - Полученное число, либо Неопределено, если строка не является числом.
//
Функция СтрокаВЧисло(Знач Значение, ТипЧисло)
	
	Значение  = СтрЗаменить(Значение, " ", "");
	Если СтрНачинаетсяС(Значение, "(") Тогда
		Значение = СтрЗаменить(Значение, "(", "-");
		Значение = СтрЗаменить(Значение, ")", "");
	КонецЕсли;
	
	СтрокаБезНулей = СтрЗаменить(Значение, "0", "");
	Если ПустаяСтрока(СтрокаБезНулей) Или СтрокаБезНулей = "-" Тогда
		Возврат 0;
	КонецЕсли;
	
	Результат = ТипЧисло.ПривестиЗначение(Значение);
	
	Возврат ?(Результат <> 0 И Не ПустаяСтрока(СтрокаБезНулей), Результат, Неопределено);
	
КонецФункции

#КонецОбласти