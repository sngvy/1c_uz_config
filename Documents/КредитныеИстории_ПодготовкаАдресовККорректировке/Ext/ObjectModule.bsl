
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.НБКИДокументКорректировкаRUTDF") Тогда
		
		Для Каждого ТекСтрокатчКорректировка Из ДанныеЗаполнения.тчКорректировка Цикл
			НоваяСтрока = ДолжникиДляКорректировкиАдресов.Добавить();
			НоваяСтрока.ОбъектДолжник = ТекСтрокатчКорректировка.Должник;
		КонецЦикла;  
		
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим) 
	
	Если Не ЗначениеЗаполнено(ВидАдреса) Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Вид Адреса обязателен для заполнения!";
		Сообщение.Сообщить();
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;	
	
	Движения.КредитныеИсторииСтруктураАдресаНаКорректировку.Записывать = Истина;
			
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КредитныеИстории_ПодготовкаАдресовККорректировкеДолжникиДляКорректировкиАдресов.ОбъектДолжник КАК ОбъектДолжник
		|ПОМЕСТИТЬ ТЧДолжники
		|ИЗ
		|	Документ.КредитныеИстории_ПодготовкаАдресовККорректировке.ДолжникиДляКорректировкиАдресов КАК КредитныеИстории_ПодготовкаАдресовККорректировкеДолжникиДляКорректировкиАдресов
		|ГДЕ
		|	КредитныеИстории_ПодготовкаАдресовККорректировкеДолжникиДляКорректировкиАдресов.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОбъектДолжник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТЧДолжники.ОбъектДолжник КАК Должник,
		|	КредитныеИсторииСтруктураАдреса.ВидАдреса КАК ВидАдреса,
		|	КредитныеИсторииСтруктураАдреса.Индекс КАК Индекс,
		|	КредитныеИсторииСтруктураАдреса.КодСтраныОКСМ КАК КодСтраныОКСМ,
		|	КредитныеИсторииСтруктураАдреса.НаименованиеИнойСтраны КАК НаименованиеИнойСтраны,
		|	КредитныеИсторииСтруктураАдреса.НомерАдресаФИАС КАК НомерАдресаФИАС,
		|	КредитныеИсторииСтруктураАдреса.НаселенныйПунктОКАТО КАК НаселенныйПунктОКАТО,
		|	КредитныеИсторииСтруктураАдреса.НаселенныйПунктНазвание КАК НаселенныйПунктНазвание,
		|	КредитныеИсторииСтруктураАдреса.Улица КАК Улица,
		|	КредитныеИсторииСтруктураАдреса.Дом КАК Дом,
		|	КредитныеИсторииСтруктураАдреса.Владение КАК Владение,
		|	КредитныеИсторииСтруктураАдреса.Корпус КАК Корпус,
		|	КредитныеИсторииСтруктураАдреса.Строение КАК Строение,
		|	КредитныеИсторииСтруктураАдреса.КвартираПомещениеОфис КАК КвартираПомещениеОфис
		|ИЗ
		|	ТЧДолжники КАК ТЧДолжники
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КредитныеИсторииСтруктураАдреса КАК КредитныеИсторииСтруктураАдреса
		|		ПО ТЧДолжники.ОбъектДолжник = КредитныеИсторииСтруктураАдреса.Должник
		|ГДЕ
		|	КредитныеИсторииСтруктураАдреса.ВидАдреса = &ВидАдреса";
	
	Запрос.УстановитьПараметр("ВидАдреса", ВидАдреса);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
		Движение = Движения.КредитныеИсторииСтруктураАдресаНаКорректировку.Добавить(); 
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
		Движение.ВидАдреса = ВидАдреса; 
		Движение.Период = Дата;
		
	КонецЦикла;

КонецПроцедуры
