
#Область ПрограммныйИнтерфейс

Функция ПоискКонтрагентаПоФИО(ФИО, Инициалы, Условия) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СклонениеФИО.Контрагент КАК Контрагент
	|ИЗ
	|	РегистрСведений.СклонениеФИО КАК СклонениеФИО
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ЭтоПолноеФИО = ИСТИНА
	|				ТОГДА СклонениеФИО.Именительный = &ФИО
	|						ИЛИ СклонениеФИО.Родительный = &ФИО
	|						ИЛИ СклонениеФИО.Творительный = &ФИО
	|						ИЛИ СклонениеФИО.Предолжный = &ФИО
	|						ИЛИ СклонениеФИО.Дательный = &ФИО
	|			ИНАЧЕ СклонениеФИО.Именительный ПОДОБНО ""%"" + &ФИО + ""%""
	|						И СклонениеФИО.Инициалы = &Инициалы
	|					ИЛИ СклонениеФИО.Родительный ПОДОБНО ""%"" + &ФИО + ""%""
	|						И СклонениеФИО.Инициалы = &Инициалы
	|					ИЛИ СклонениеФИО.Творительный ПОДОБНО ""%"" + &ФИО + ""%""
	|						И СклонениеФИО.Инициалы = &Инициалы
	|					ИЛИ СклонениеФИО.Предолжный ПОДОБНО ""%"" + &ФИО + ""%""
	|						И СклонениеФИО.Инициалы = &Инициалы
	|					ИЛИ СклонениеФИО.Дательный ПОДОБНО ""%"" + &ФИО + ""%""
	|						И СклонениеФИО.Инициалы = &Инициалы
	|		КОНЕЦ";
	
	Запрос.УстановитьПараметр("ЭтоПолноеФИО", Условия["ЭтоПолноеФИО"]);
	БазовоеФИО = ФункцииРазбораТекста.ФорматироватьФИОДляПоиска(ФИО);
	Запрос.УстановитьПараметр("ФИО", БазовоеФИО);
	Запрос.УстановитьПараметр("Инициалы", Инициалы);

	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
	
		Контрагент = Выборка.Контрагент;
		
		Если Условия["КАОдин"] И Выборка.Количество() = 1 Тогда
			Возврат Контрагент;	
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Условия["ДопРеквДата"]) Тогда
			ДатаРождения = ОбъектыСервер.ПолучитьЗначениеСвойства(
				Контрагент,
				Условия["ДопРеквДата"].Код
			);
			Для Каждого Стр Из Условия["мДаты"] Цикл
				Если ДатаРождения = Стр Тогда
					Возврат Контрагент;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Условия["ДопИннСнилс"]) Тогда
			ИннСнилс = ОбъектыСервер.ПолучитьЗначениеСвойства(
				Контрагент,
				Условия["ДопИннСнилс"].Код
			);
			Для Каждого Стр Из Условия["мСлова"] Цикл
				Если ИннСнилс = Стр Тогда
					Возврат Контрагент;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Условия["ДопРеквДата"])
			И НЕ ЗначениеЗаполнено(Условия["ДопИннСнилс"]) Тогда
			Возврат Контрагент;
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат Неопределено;

КонецФункции // ()

Процедура ЗаполнитьСклонения(Контрагент, ВсеЗаписиСклонений = Ложь) Экспорт
	ТзФИО = Контрагент.ФИО.Выгрузить();
	Для Каждого Стр Из ТзФИО Цикл
		Стр.ФИО = СтрЗаменить(Стр.ФИО, "ё", "е");
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
					|	ТзФИО.ФИО КАК ФИО
					|ПОМЕСТИТЬ ТзФИО
					|ИЗ
					|	&ТзФИО КАК ТзФИО
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	ТзФИО.ФИО КАК ФИО
					|ИЗ
					|	ТзФИО КАК ТзФИО
					|
					|СГРУППИРОВАТЬ ПО
					|	ТзФИО.ФИО";
	Запрос.УстановитьПараметр("ТзФИО", ТзФИО);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Контрагент.Установить(Контрагент);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ФИОДляПоиска = ПривестиКПоисковомуВиду(Выборка.ФИО);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Контрагент = Контрагент;
		НоваяЗапись.Именительный = СклонятьВПадеж(ФИОДляПоиска, "Именительный");
		
		ВыборкаРучная = ИзмененоВРучную(Контрагент, НоваяЗапись.Именительный);
		Если Не ВсеЗаписиСклонений И ВыборкаРучная <> Неопределено Тогда
		
			НоваяЗапись.Инициалы = ВыборкаРучная.Инициалы;
			НоваяЗапись.Родительный = ВыборкаРучная.Родительный;
			НоваяЗапись.Творительный = ВыборкаРучная.Творительный;
			НоваяЗапись.Предолжный = ВыборкаРучная.Предолжный;
			НоваяЗапись.Дательный = ВыборкаРучная.Дательный;
			НоваяЗапись.ИзмененоВРучную = ВыборкаРучная.ИзмененоВРучную;
			Продолжить;
		
		КонецЕсли;

		НоваяЗапись.Родительный = СклонятьВПадеж(ФИОДляПоиска, "Родительный");
		НоваяЗапись.Творительный = СклонятьВПадеж(ФИОДляПоиска, "Творительный");
		НоваяЗапись.Предолжный = СклонятьВПадеж(ФИОДляПоиска, "Предложный");
		НоваяЗапись.Дательный = СклонятьВПадеж(ФИОДляПоиска, "Дательный");

		НоваяЗапись.Инициалы = Инициалы(ФИОДляПоиска);
	КонецЦикла;
	
	НаборЗаписей.Записать();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИзмененоВРучную(Контрагент, Именительный)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СклонениеФИО.Именительный КАК Именительный,
		|	СклонениеФИО.Родительный КАК Родительный,
		|	СклонениеФИО.Творительный КАК Творительный,
		|	СклонениеФИО.Предолжный КАК Предолжный,
		|	СклонениеФИО.Дательный КАК Дательный,
		|	СклонениеФИО.Инициалы КАК Инициалы,
		|	СклонениеФИО.Контрагент КАК Контрагент,
		|	СклонениеФИО.ИзмененоВРучную КАК ИзмененоВРучную
		|ИЗ
		|	РегистрСведений.СклонениеФИО КАК СклонениеФИО
		|ГДЕ
		|	СклонениеФИО.Контрагент = &Контрагент
		|	И СклонениеФИО.Именительный = &Именительный
		|	И СклонениеФИО.ИзмененоВРучную = ИСТИНА";
	
	Запрос.УстановитьПараметр("Именительный", Именительный);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
	
		Возврат Неопределено;
	
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выгрузить();

	Возврат ВыборкаДетальныеЗаписи[0];

КонецФункции // ()

Функция СклонятьВПадеж(Текст, Падеж)

	Результат = ПолучитьСклоненияСтроки(ТРег(Текст), , "ПД=" + Падеж);
	Если Результат.Количество() > 0 Тогда
	
		Возврат Результат[0];
	
	КонецЕсли;
	
	Возврат Текст;

КонецФункции // ()

Функция Инициалы(СтрФИО)
	
	сФио = ФизическиеЛицаКлиентСервер.ЧастиИмени(СтрФИО);
	Если сФио.Свойство("Имя") И сФио.Свойство("Отчество") Тогда
		Инициалы = Лев(сФио.Имя,1) + " " +  Лев(сФио.Отчество,1);
		Возврат Инициалы;
	КонецЕсли;
	
	Возврат "";
КонецФункции

Функция ПривестиКПоисковомуВиду(ФИО)

	Возврат ФункцииРазбораТекста.ФорматироватьФИОДляПоиска(ФИО);

КонецФункции // ()

#КонецОбласти
