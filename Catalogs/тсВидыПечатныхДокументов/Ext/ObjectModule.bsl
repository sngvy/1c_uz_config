
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если УниверсальныйТипИспользуетсяВместеСДругимиТипамиШаблонов() Тогда
	
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Универсальный шаблон не может использоваться с другими типами шаблонов!";
		Сообщение.Сообщить();
	
	КонецЕсли;
	
КонецПроцедуры

#Область ВспомогательнаяПроверкаЗаполнения

Функция УниверсальныйТипИспользуетсяВместеСДругимиТипамиШаблонов()

	Шаблоны = ПолучитьТекущиеШаблоны();
	
	ПроверочнаяПеременная = Неопределено;
	Если Шаблоны.Количество() > 0 Тогда
	
		ПроверочнаяПеременная = ЭтоУниверсальныйШаблон(Шаблоны[0].ТипДокумента);
	
	КонецЕсли;
	
	Для каждого Шаблон Из Шаблоны Цикл
	
		Если ПроверочнаяПеременная <> ЭтоУниверсальныйШаблон(Шаблон.ТипДокумента) Тогда
		
			Возврат Истина;
		
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат Ложь;

КонецФункции // ()

Функция ЭтоУниверсальныйШаблон(ТипДокумента)

	Если ТипДокумента = Перечисления.ТипыЭлДок.Универсальный Тогда
	
		Возврат Истина;
	
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции // ()


Функция ПолучитьТекущиеШаблоны()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	тсВидыПечатныхДокументовШаблоныПечатныхФорм.ШаблонПечатнойФормы КАК Шаблон
		|ПОМЕСТИТЬ втШаблоныПечатныхФорм
		|ИЗ
		|	&ШаблоныПечатныхФорм КАК тсВидыПечатныхДокументовШаблоныПечатныхФорм
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(тсВидыПечатныхДокументовШаблоныПечатныхФорм.Шаблон КАК Справочник.тсШаблоныПечатныхДокументов) КАК Шаблон,
		|	тсВидыПечатныхДокументовШаблоныПечатныхФорм.Шаблон.ТипДокумента КАК ТипДокумента
		|ИЗ
		|	втШаблоныПечатныхФорм КАК тсВидыПечатныхДокументовШаблоныПечатныхФорм
		|ГДЕ
		|	тсВидыПечатныхДокументовШаблоныПечатныхФорм.Шаблон ССЫЛКА Справочник.тсШаблоныПечатныхДокументов";
	
	Шаблоны = ЭтотОбъект.ШаблоныПечатныхФорм.Выгрузить();
	Запрос.УстановитьПараметр("ШаблоныПечатныхФорм", Шаблоны);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выгрузить();
	
	Возврат ВыборкаДетальныеЗаписи;

КонецФункции


#КонецОбласти

