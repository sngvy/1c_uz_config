
&НаКлиенте
Перем обКонтроллерАТС;

&НаКлиенте
Перем СоответствиеНомеровХоста;

&НаКлиенте
Перем кэшКонтактов;


&НаКлиенте
Процедура УстановитьКонтроллерАТС(ссылкаКонтроллерАТС) ЭКСПОРТ
	обКонтроллерАТС = ссылкаКонтроллерАТС;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСоответствиеНомеров(СоответствиеВнутреннихНомеров, СоответствиеКонтактов) ЭКСПОРТ
	СоответствиеНомеровХоста = СоответствиеВнутреннихНомеров;
	кэшКонтактов = СоответствиеКонтактов;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТранк(ХмлКаналы)
	
	Попытка
		чтение = Новый ЧтениеXML();
		чтение.УстановитьСтроку(ХмлКаналы);
		
		дом = Новый ПостроительDOM();
		докХмл = дом.Прочитать(чтение);
		
		Разговоры.Очистить();
		
		КолвоЗвонковТранка = 0;
		
		списокТранковПир = докХмл.ПолучитьЭлементыПоИмени("/ChannelsInfo/Trunks", ПирТранка);
		Если списокТранковПир.Количество() > 0 Тогда
			обТранк = списокТранковПир[0];
			списокРазговоровТранка = обТранк.ПолучитьЭлементыПоИмени("ExternalCallInfo");
			КолвоЗвонковТранка = списокРазговоровТранка.Количество();
			Для й = 0 По (КолвоЗвонковТранка-1) Цикл
				обРазговор = списокРазговоровТранка[й];
				ЗвонокНаТранке				= обРазговор.ПолучитьАтрибут("Connected");
				НаправлениеВходящНаТранке	= Булево(Число(обРазговор.ПолучитьАтрибут("Incoming")));
				ДлительностьНаТранке		= обРазговор.ПолучитьАтрибут("Duration");
				ВнешнийАбонентНаТранке		= обРазговор.ПолучитьАтрибут("ExternalAbonent");
				КонтрагентСсылка = Неопределено;
				КонтактноеЛицоСсылка = Неопределено;
				бит_АТСКлиент.СократитьНомерНайтиКонтрагента(кэшКонтактов, СоответствиеНомеровХоста, ВнешнийАбонентНаТранке, КонтрагентСсылка, КонтактноеЛицоСсылка);
				строкаЗвонок = Разговоры.Добавить();
				строкаЗвонок.Разговор		= бит_АТСКлиент.ПолучитьИмяПоВнутреннемуНомеру(СоответствиеНомеровХоста, ЗвонокНаТранке);
				строкаЗвонок.Направление	= ? (НаправлениеВходящНаТранке,  "<-" , "->");
				строкаЗвонок.Длительность	= ДлительностьНаТранке;
				ВнешнийАбонентНаТранке = бит_ТелефонияКлиентСервер.ОчиститьНомерТолькоЦифры(ВнешнийАбонентНаТранке);
				строкаЗвонок.ВнешнийАбонент	= бит_ТелефонияКлиентСервер.СократитьНомерДляПоиска(ВнешнийАбонентНаТранке);
				строкаЗвонок.Контрагент		= КонтрагентСсылка;
				строкаЗвонок.КонтактноеЛицо	= КонтактноеЛицоСсылка;
			КонецЦикла;
		КонецЕсли;
		
		ВсегоРазговоров = КолвоЗвонковТранка;

	Исключение
		бит_ТелефонияКлиент.ВывестиСообщение( ОписаниеОшибки() );
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)
	Если Источник = ИсточникВнешнихСобытий И Событие = "ОбновлениеНомеров" Тогда	
		ЗаполнитьТранк(Данные);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если обКонтроллерАТС <> Неопределено Тогда
		обКонтроллерАТС.GetExtensionsStatusesAndChannels();
	КонецЕсли;
КонецПроцедуры

