
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ОбновитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ОбменБИТАТС_РегистрацияИзменений" Тогда
		Если Параметр.Результат = КодВозвратаДиалога.Да Тогда
			РегистрацияИзмененийУзлаОбменаСервер();
			бит_ТелефонияКлиент.ВывестиСообщение("Все контрагенты и контактные лица помечены как измененные для узла обмена '" + Объект.Ссылка + "'", ЭтаФорма);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПроверитьСоединение(Команда)
	
	// подключение к БД
	стрОшибка = "";
	соединение = бит_АТСВыгрузкаИзменений.ПроверитьСоединениеКБазеДанных(Объект.Ссылка, стрОшибка);
	Если соединение Тогда
		бит_ТелефонияКлиентПереопределяемый.ПоказПредупреждение("Соединение успешно.");
	Иначе
		стрОшибка = стрОшибка + Символы.ПС + "Не удалось подключиться к базе данных.";
		бит_ТелефонияКлиентПереопределяемый.ПоказПредупреждение(стрОшибка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрацияИзмененийУзлаОбмена(Команда)
	
	АдминистраторАТС = бит_АТССервер.ПроверитьПраваАдминистрировать();
	Если НЕ АдминистраторАТС Тогда
		бит_ТелефонияКлиентПереопределяемый.ПоказПредупреждение("Недостаточно прав для регистрации изменений узла обмена");
		Возврат;
	КонецЕсли;
	
	бит_ТелефонияКлиентПереопределяемый.ПоказВопрос("Регистрация всех объектов измененными для узла обмена может занять длительное время. Продолжить?",
		РежимДиалогаВопрос.ДаНет, , , , "ОбменБИТАТС_РегистрацияИзменений");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура РегистрацияИзмененийУзлаОбменаСервер()
	
	метаданныеКонтрагенты = Метаданные.НайтиПоПолномуИмени("Справочник." + бит_ТелефонияСерверПереопределяемый.ПолучитьИмяСправочникаКонтрагентов());
	ПланыОбмена.ЗарегистрироватьИзменения(Объект.Ссылка, метаданныеКонтрагенты);

	метаданныеКонтактныеЛица = Метаданные.НайтиПоПолномуИмени("Справочник." + бит_ТелефонияСерверПереопределяемый.ПолучитьИмяСправочникаКонтактныхЛиц());
	ПланыОбмена.ЗарегистрироватьИзменения(Объект.Ссылка, метаданныеКонтактныеЛица);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДоступность()
	флагОбъектСохранен = ЗначениеЗаполнено(Объект.Ссылка);
	Элементы.ПроверитьСоединение.Доступность = флагОбъектСохранен;
	Элементы.РегистрацияИзмененийУзлаОбмена.Доступность = флагОбъектСохранен;
КонецПроцедуры

#КонецОбласти
