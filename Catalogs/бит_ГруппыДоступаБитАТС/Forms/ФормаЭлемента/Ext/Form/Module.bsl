
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПользователиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Пользователи") Тогда
		ссылкаПользователь = ВыбранноеЗначение;
		массПольз = Новый Массив();
		Если ПроверкаПользовательГруппа(ссылкаПользователь) Тогда
			массПольз = ПолучитьПользователейГруппыСервер(ссылкаПользователь);
		Иначе
			массПольз.Добавить(ссылкаПользователь);
		КонецЕсли;
		ДобавитьПользователейИзМассива(массПольз);
		Модифицированность = Истина;
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		ДобавитьПользователейИзМассива(ВыбранноеЗначение);
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подобрать(Команда)
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	
	ФормаПодбора = ПолучитьФорму("Справочник.Пользователи.ФормаВыбора", ПараметрыФормы, Элементы.Пользователи);
	ФормаПодбора.Открыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьПользователейГруппыСервер(ГруппаПользователейСсылка)
	ЗапросПользователей = Новый Запрос;
	ЗапросПользователей.Текст = "ВЫБРАТЬ
	                            |	Пользователи.Ссылка
	                            |ИЗ
	                            |	Справочник.Пользователи КАК Пользователи
	                            |ГДЕ
	                            |	Пользователи.ЭтоГруппа = ЛОЖЬ
	                            |	И Пользователи.ПометкаУдаления = ЛОЖЬ
	                            |	И Пользователи.Родитель В ИЕРАРХИИ(&Родитель)";
	ЗапросПользователей.УстановитьПараметр("Родитель", ГруппаПользователейСсылка);
	массивПользователей = ЗапросПользователей.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	Возврат массивПользователей;
КонецФункции

&НаСервере
Функция ПроверкаПользовательГруппа(ПользовательСсылка)
	Возврат ПользовательСсылка.ЭтоГруппа;
КонецФункции

&НаКлиенте
Процедура ДобавитьПользователейИзМассива(массивПользователей)
	Для Каждого элемПольз Из массивПользователей Цикл
		ПараметрыОтбора = Новый Структура();
		ПараметрыОтбора.Вставить("Пользователь", элемПольз);
		массСущПольз = Объект.Пользователи.НайтиСтроки(ПараметрыОтбора);
		Если массСущПольз.Количество() = 0 Тогда
			строкаПольз = Объект.Пользователи.Добавить();
			строкаПольз.Пользователь = элемПольз;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

