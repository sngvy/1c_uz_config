
Процедура ОбработкаПроведенияОбщая(Отказ, Проведение, ЭтотОбъект) Экспорт	
	
	ОбработкаПроведения(Отказ, Проведение, ЭтотОбъект);
	СнятьПометкуУдаления(Отказ, Проведение, ЭтотОбъект);
	
КонецПроцедуры

// Восстановление помеченных на удаление
Процедура СнятьПометкуУдаления(Отказ, Проведение, ЭтотОбъект) 
	Если ЭтотОбъект.Схема.Пустая() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|			БизнесПроцессы.Ссылка КАК Ссылка,
	|			БизнесПроцессы.Объект КАК Объект,
	|			БизнесПроцессы.ПометкаУдаления КАК ПометкаУдаления
	|		ИЗ
	|			БизнесПроцесс.БизнесПроцессы КАК БизнесПроцессы
	|		ГДЕ
	|			БизнесПроцессы.Автор = &Ссылка И ПометкаУдаления = Истина");
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	Результат = Запрос.Выполнить().Выбрать();
	
	Пока Результат.Следующий() Цикл
		// При проведении документа необходимо убирать пометку удаления 
		// у существующего документа "БизнесПроцессы" если она есть.
		// INFO нужна в случае распроведения и повторного проведения документа, так как бп помечаются на удаление
		Результат.Ссылка.ПолучитьОбъект().УстановитьПометкуУдаления(Ложь); 	
	КонецЦикла;	
КонецПроцедуры

// Восстановление удаленных и создание новых
Процедура ОбработкаПроведения(Отказ, Проведение, ЭтотОбъект) 
	Если ЭтотОбъект.Схема.Пустая() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |ТЗ.Объект КАК Объект
	                      |ПОМЕСТИТЬ ТаблицаЗапускБП
	                      |ИЗ
	                      |&ТЗ КАК ТЗ
	                      |
	                      |ИНДЕКСИРОВАТЬ ПО
	                      |	Объект
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	БизнесПроцессы.Ссылка КАК Ссылка,
	                      |	БизнесПроцессы.Объект КАК Объект,
	                      |	БизнесПроцессы.ПометкаУдаления КАК ПометкаУдаления
	                      |ПОМЕСТИТЬ ТаблицаБП
	                      |ИЗ
	                      |	БизнесПроцесс.БизнесПроцессы КАК БизнесПроцессы
	                      |ГДЕ
	                      |	БизнесПроцессы.Автор = &Ссылка
	                      |
	                      |ИНДЕКСИРОВАТЬ ПО
	                      |	Объект
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ЕСТЬNULL(ТаблицаБП.Объект, ЗНАЧЕНИЕ(Справочник.ДолговыеОбязательства.ПустаяСсылка)) КАК Объект,
	                      |	ТаблицаЗапускБП.Объект КАК Объект1
	                      |ПОМЕСТИТЬ БПиЗБП
	                      |ИЗ
	                      |	ТаблицаЗапускБП КАК ТаблицаЗапускБП
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаБП КАК ТаблицаБП
	                      |		ПО (ТаблицаБП.Объект = ТаблицаЗапускБП.Объект)
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |	БПиЗБП.Объект КАК ОбъектБП,
	                      |	БПиЗБП.Объект1 КАК ОбъектЗП
	                      |ИЗ
	                      |	БПиЗБП КАК БПиЗБП
	                      |ГДЕ
	                      |	БПиЗБП.Объект = ЗНАЧЕНИЕ(Справочник.ДолговыеОбязательства.ПустаяСсылка)");
	
	Запрос.УстановитьПараметр ("ТЗ", ЭтотОбъект.Объекты);
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	Результат = Запрос.Выполнить().Выбрать();
	МассивДанных = ЭтотОбъект.ТочкаСтарта.Получить();
	Пока Результат.Следующий() Цикл
		Если Результат.ОбъектБП = Справочники.ДолговыеОбязательства.ПустаяСсылка() Тогда
			НовыйБП = БизнесПроцессы.БизнесПроцессы.СоздатьБизнесПроцесс();
			НовыйБП.Дата = ТекущаяДатаСеанса();
			НовыйБП.Организация = ЭтотОбъект.Организация;
			НовыйБП.Подразделение = ЭтотОбъект.Подразделение;
			НовыйБП.Схема = ЭтотОбъект.Схема;
			НовыйБП.Операция = ЭтотОбъект.Операция;
			НовыйБП.Автор = ЭтотОбъект.Ссылка;
			НовыйБП.Комментарий = "Документ ""Запуск БП""";
			НовыйБП.Объект = Результат.ОбъектЗП;
			НовыйБП.БПРодитель = ЭтотОбъект.Ссылка;
			
			Попытка
				НовыйБП.Записать();
			Исключение
				Отказ = Истина;
				Возврат;
			КонецПопытки;
			
			Если Не ЭтотОбъект.ТССтартоватьСВыбраннойСтадии Тогда
				НовыйБП.ЗапуститьБизнесПроцесс(Отказ);
			Иначе
				НовыйБП.ЗапуститьБизнесПроцессСТочки(Отказ, МассивДанных.ТСТочка, МассивДанных.ТСОбъектРодитель, МассивДанных.ТССхема, 
				МассивДанных.ТСТипМероприятия, МассивДанных.ТСМассив, МассивДанных.ТСИсполнитель);
			КонецЕсли;
			
			Если Отказ Тогда
				Возврат;
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ, ЭтотОбъект) Экспорт
	
	// Запрос проверяет, созданы ли БизнесПроцессы для каждого ДО, которые есть в табличной части документа "ЗапускБП".
	// Полное соед. даёт возможность отследить:
	// 1) какие БизнесПроцессы были удалены для ДО, а в документе "ЗапускБП" они есть,
	// 2) какие строки ДО из ТЧ "ЗапускБП" были удалены, а БизнесПроцессы не удалены.
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|			БизнесПроцессы.Ссылка КАК БПСсылка,
	|			БизнесПроцессы.Объект КАК Объект,
	|			БизнесПроцессы.ПометкаУдаления КАК ПометкаУдаления
	|		ИЗ
	|			БизнесПроцесс.БизнесПроцессы КАК БизнесПроцессы
	|		ГДЕ
	|			БизнесПроцессы.Автор = &Ссылка И ПометкаУдаления = Ложь");
	
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		// Если Результат.Объект = Null, значит строка из ТЧ документа "ЗапускБП"" была удалена или
		// Если Результат.БПСсылка = Null, значит документ "БизнесПроцессы" был удален, т.е выполнять
		// выполнять отмену проведения у документа "БизнесПроцессы" не требуется.
		Результат.БПСсылка.ПолучитьОбъект().УстановитьПометкуУдаления(Истина);
	КонецЦикла;
КонецПроцедуры
