
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	стрИмяСправочникаКонтрагентов = бит_ТелефонияСерверПереопределяемый.ПолучитьИмяСправочникаКонтрагентов();
	стрИмяТаблицыКонтрагентов = "Справочник." + стрИмяСправочникаКонтрагентов;
	//
	Контрагенты.ПроизвольныйЗапрос = Истина;
	Контрагенты.ДинамическоеСчитываниеДанных = Истина;
	Контрагенты.ОсновнаяТаблица = стрИмяТаблицыКонтрагентов;
	Контрагенты.ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                           |	Контрагенты.Наименование
	                           |ИЗ
	                           |	" + стрИмяТаблицыКонтрагентов + " КАК Контрагенты";
	//
	КолонкаНаименованиеКонтрагента = Элементы.Добавить("Наименование", Тип("ПолеФормы"), Элементы.Контрагенты);    
	КолонкаНаименованиеКонтрагента.ПутьКДанным = "Контрагенты.Наименование";	// тут название реквизита формы - динамического списка, а не таблицы БД
	
	Элементы.ГруппаНомераБитАТС.Видимость = ЗначениеЗаполнено(Параметры.СсылкаАТС);
	бит_ТелефонияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(НомераБитАТС, "Владелец", Параметры.СсылкаАТС);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ЗаполнитьСписокКонтактныхЛицКонтрагента(ВыбраннаяСтрока);
	ЗаполнитьСписокТелефоновКонтрагента(ВыбраннаяСтрока);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыПриАктивизацииЯчейки(Элемент)
	контрагентСсылка = Элементы.Контрагенты.ТекущаяСтрока;
	ЗаполнитьСписокКонтактныхЛицКонтрагента(контрагентСсылка);
	ЗаполнитьСписокТелефоновКонтрагента(контрагентСсылка);
КонецПроцедуры

&НаКлиенте
Процедура КонтактныеЛицаКонтрагентаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	текущиеДанные = Элементы.КонтактныеЛицаКонтрагента.ТекущиеДанные;
	Если текущиеДанные <> Неопределено Тогда
		контактноеЛицоСсылка = текущиеДанные.Наименование;
		ЗаполнитьСписокТелефоновКонтактногоЛица(контактноеЛицоСсылка);
	КонецЕсли;
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ТелефоныВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ВыбратьНомерВТаблицеТелефонов();
КонецПроцедуры

&НаКлиенте
Процедура ТелефоныПриАктивизацииЯчейки(Элемент)
	ВыбратьНомерВТаблицеТелефонов();
КонецПроцедуры

&НаКлиенте
Процедура НомераБитАТСВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура НомераБитАТСПриАктивизацииЯчейки(Элемент)
	текущиеДанные = Элементы.НомераБитАТС.ТекущиеДанные;
	Если текущиеДанные <> Неопределено Тогда
		ВыбранныйНомер = текущиеДанные.Номер;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьДляЗвонка(Команда)
	ЗакрыватьПриВыборе = Истина;
	ОповеститьОВыборе(ВыбранныйНомер);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокКонтактныхЛицКонтрагента(КонтрагентСсылка)
	Если НЕ ЗначениеЗаполнено(КонтрагентСсылка) Тогда
		Возврат;
	КонецЕсли;
	КонтактныеЛицаКонтрагента.Очистить();
	массКонтактныеЛицаКонтрагента = бит_ТелефонияСерверПереопределяемый.НайтиКонтактныеЛицаКонтрагента(КонтрагентСсылка);
	Для Каждого элКонтактноеЛицо Из массКонтактныеЛицаКонтрагента Цикл
		контактноеЛицо = КонтактныеЛицаКонтрагента.Добавить();
		контактноеЛицо.Наименование = элКонтактноеЛицо;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокТелефоновКонтрагента(КонтрагентСсылка)
	Если НЕ ЗначениеЗаполнено(КонтрагентСсылка) Тогда
		Возврат;
	КонецЕсли;
	Телефоны.Очистить();
	массивНомераТелефонов = бит_ТелефонияСерверПереопределяемый.НайтиНомераКонтрагента(КонтрагентСсылка);
	Для Каждого тел Из массивНомераТелефонов Цикл
		строкаТелефон = Телефоны.Добавить();
		строкаТелефон.Номер = тел;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокТелефоновКонтактногоЛица(КонтактноеЛицоСсылка)
	Если НЕ ЗначениеЗаполнено(КонтактноеЛицоСсылка) Тогда
		Возврат;
	КонецЕсли;
	Телефоны.Очистить();
	массивНомераТелефонов = бит_ТелефонияСерверПереопределяемый.НайтиНомераКонтактногоЛица(КонтактноеЛицоСсылка);
	Для Каждого тел Из массивНомераТелефонов Цикл
		строкаТелефон = Телефоны.Добавить();
		строкаТелефон.Номер = тел;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьНомерВТаблицеТелефонов()
	текущиеДанные = Элементы.Телефоны.ТекущиеДанные;
	Если текущиеДанные <> Неопределено Тогда
		ВыбранныйНомер = текущиеДанные.Номер;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


