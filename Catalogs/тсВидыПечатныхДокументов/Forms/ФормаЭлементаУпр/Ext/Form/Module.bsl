
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Не Объект.Назначение = "" Тогда
		Назначение = УЭДСервер.ПолучитьТипИзСтроки(Объект.Назначение);
		мсТекущийТип = Новый Массив;
		мсТекущийТип.Добавить(Назначение);
		ДопустимыеТипы = Новый ОписаниеТипов(мсТекущийТип);
	КонецЕсли;
	АвторегистрацияПечатныхФорм = ОбъектыСервер.ПолучитьЗначениеКонстанты("АвторегистрацияПечатныхФорм");
	Элементы.ШаблоныПечатныхФормИсходящийНомер.Видимость = АвторегистрацияПечатныхФорм И Не Объект.СклеиватьВОдинФайл;
	Элементы.ШаблоныПечатныхФормПрикреплять.Видимость = Не Объект.СклеиватьВОдинФайл;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Представление = ПолучитьПредставлениеНазначение(ДопустимыеТипы);
	Если Представление = Неопределено Тогда
	
		Отказ = Истина;
		Возврат;
	
	КонецЕсли;
	
	Объект.Назначение = Представление;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПредставлениеНазначение(Знач ВыбранныйТип)

	ТипНазначения = ТипИзОписанияТипаВНазначении(ВыбранныйТип);
	Если ТипНазначения = Неопределено Тогда
	
		Возврат Неопределено;
	
	КонецЕсли;
	ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипНазначения);
	Если ОбъектМетаданных = Неопределено Тогда
	
		Возврат Неопределено;
	
	КонецЕсли;
	
	Возврат Строка(ОбъектМетаданных);

КонецФункции // ()

&НаСервереБезКонтекста
Функция ТипИзОписанияТипаВНазначении(ВыбранныйТип)

	Типы = ВыбранныйТип.Типы();
	Если Типы.Количество() = 0 Тогда
	
		Возврат Неопределено;
	
	КонецЕсли;
	
	Возврат Типы[0];

КонецФункции // ()

&НаКлиенте
Процедура ШаблоныПечатныхФормПрикреплятьПриИзменении(Элемент)
	Если ТипЗнч(Элементы.ШаблоныПечатныхФорм.ТекущиеДанные.ШаблонПечатнойФормы) = Тип("СправочникСсылка.тсШаблоныПечатныхДокументов") Тогда
		Если Не Элементы.ШаблоныПечатныхФорм.ТекущиеДанные.Прикреплять Тогда
			Элементы.ШаблоныПечатныхФорм.ТекущиеДанные.ИсходящийНомер = Ложь;
		КонецЕсли;
	Иначе
		Элементы.ШаблоныПечатныхФорм.ТекущиеДанные.Прикреплять = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ШаблоныПечатныхФормИсходящийНомерПриИзменении(Элемент)
	Если ТипЗнч(Элементы.ШаблоныПечатныхФорм.ТекущиеДанные.ШаблонПечатнойФормы) = Тип("СправочникСсылка.тсШаблоныПечатныхДокументов") Тогда
		Если Элементы.ШаблоныПечатныхФорм.ТекущиеДанные.ИсходящийНомер Тогда
			Элементы.ШаблоныПечатныхФорм.ТекущиеДанные.Прикреплять = Истина;
		КонецЕсли;
	Иначе
		Элементы.ШаблоныПечатныхФорм.ТекущиеДанные.ИсходящийНомер = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ШаблоныПечатныхФормШаблонПечатнойФормыПриИзменении(Элемент)
	Если ТипЗнч(Элементы.ШаблоныПечатныхФорм.ТекущиеДанные.ШаблонПечатнойФормы) <> Тип("СправочникСсылка.тсШаблоныПечатныхДокументов") Тогда
		Элементы.ШаблоныПечатныхФорм.ТекущиеДанные.ИсходящийНомер = Ложь;
		Элементы.ШаблоныПечатныхФорм.ТекущиеДанные.Прикреплять = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СклеиватьВОдинФайлПриИзменении(Элемент)
	Для Каждого Эл Из Объект.ШаблоныПечатныхФорм Цикл
		Эл.Прикреплять = Ложь;
		Эл.ИсходящийНомер = Ложь;
	КонецЦикла;
	АвторегистрацияПечатныхФорм = ОбъектыСервер.ПолучитьЗначениеКонстанты("АвторегистрацияПечатныхФорм");
	Элементы.ШаблоныПечатныхФормИсходящийНомер.Видимость = АвторегистрацияПечатныхФорм И Не Объект.СклеиватьВОдинФайл;
	Элементы.ШаблоныПечатныхФормПрикреплять.Видимость = Не Объект.СклеиватьВОдинФайл;
КонецПроцедуры
