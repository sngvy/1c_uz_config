Процедура КонструкторФорматаОткрытие(ФормаВладелец, ПолеТекстовогоДокумента = "ПолеТекстовогоДокумента", ТолькоФормат = Ложь) Экспорт
	
	Конструктор = Новый КонструкторФорматнойСтроки();
	
	ИсходныйТекстФормата = "";
	ИсходныйТекст = ФормаВладелец.Элементы[ПолеТекстовогоДокумента].ВыделенныйТекст;
	Если НЕ ИсходныйТекст = "" И СтрНайти(ИсходныйТекст, "Формат") > 0 Тогда
		
		Кавычка = Символ(34);
		ПерваяКовычка = СтрНайти(ИсходныйТекст, Кавычка);
		ВтораяКовычка = СтрНайти(Сред(ИсходныйТекст, ПерваяКовычка + 1), Кавычка);
		ИсходныйТекстФормата = Сред(ИсходныйТекст, ПерваяКовычка + 1, ВтораяКовычка - 1);

	КонецЕсли;
	
	СтарыйТекст = Ложь;
	Если ИсходныйТекстФормата <> "" Тогда
	
		Конструктор.Текст = ИсходныйТекстФормата;
		СтарыйТекст = Истина;
	
	КонецЕсли;

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("СтарыйТекст", СтарыйТекст);
	ДополнительныеПараметры.Вставить("ФормаВладелец", ФормаВладелец);
	ДополнительныеПараметры.Вставить("ПолеТекстовогоДокумента", ПолеТекстовогоДокумента);
	ДополнительныеПараметры.Вставить("ТолькоФормат", ТолькоФормат);
	
	Конструктор.Показать(Новый ОписаниеОповещения(
		"КонструкторФорматаЗавершение",
		КонструкторыКлиент,
		ДополнительныеПараметры));

КонецПроцедуры

&НаКлиенте
Процедура КонструкторФорматаЗавершение(ФорматнаяСтрока, ДополнительныеПараметры) Экспорт
	
	Если ФорматнаяСтрока = Неопределено Или ФорматнаяСтрока = "" Тогда
		Возврат;
	КонецЕсли; 
	
	//ВспомогательнаяЧасть = ДополнительныеПараметры["Объект"].Функция;
	//Если ЗначениеЗаполнено(ВспомогательнаяЧасть) Тогда
	//	ВспомогательнаяЧасть = ВспомогательнаяЧасть + Символы.ПС;
	//КонецЕсли;
	Если ДополнительныеПараметры["ТолькоФормат"] Тогда
		ДобавленныйТекст = ФорматнаяСтрока;
	ИначеЕсли ДополнительныеПараметры["СтарыйТекст"] Тогда
		ДобавленныйТекст = "Формат(Значение, """ + ФорматнаяСтрока + """);";
	Иначе

		ДобавленныйТекст = "Значение = Формат(Значение, """ + ФорматнаяСтрока + """);";

	КонецЕсли;
	
	ФормаВладелец = ДополнительныеПараметры["ФормаВладелец"];
	ПолеТекстовогоДокумента = ФормаВладелец.Элементы[ДополнительныеПараметры["ПолеТекстовогоДокумента"]];
	ВыделенныйТекст = ПолеТекстовогоДокумента.ВыделенныйТекст;
	Если ВыделенныйТекст <> "" Тогда
		ВыделенныйТекст = ДобавленныйТекст;
	Иначе
		ВыделенныйТекст = ВыделенныйТекст + ДобавленныйТекст;
	КонецЕсли;
	ПолеТекстовогоДокумента.ВыделенныйТекст = ВыделенныйТекст;

	//Объект.Функция = ПолеТекстовогоДокумента.ПолучитьТекст();

КонецПроцедуры