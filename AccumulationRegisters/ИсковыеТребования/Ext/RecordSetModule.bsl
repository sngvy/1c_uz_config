
Процедура ПриЗаписи(Отказ, Замещение)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
	
		ОбновитьГоспошлинуДляКлиентБанка();
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьГоспошлинуДляКлиентБанка()

	Если НЕ ЗаписываемИзСудебногоДела() Тогда
	
		Возврат;
	
	КонецЕсли;
	
	СудебноеДело = ЭтотОбъект.Отбор.Регистратор.Значение;

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	&Регистратор КАК Регистратор,
		|	СУММА(ИсковыеТребованияОбороты.СуммаГоспошлинаПриход) КАК СуммаГоспошлинаПриход
		|ПОМЕСТИТЬ втИсковаяГП
		|ИЗ
		|	РегистрНакопления.ИсковыеТребования.Обороты(, , Регистратор, Займ = &Займ) КАК ИсковыеТребованияОбороты
		|ГДЕ
		|	ИсковыеТребованияОбороты.Регистратор = &Регистратор
		|
		|СГРУППИРОВАТЬ ПО
		|	ИсковыеТребованияОбороты.Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&Регистратор КАК Регистратор,
		|	ГоспошлинаДляКлиентБанкаСрезПоследних.Госпошлина КАК Госпошлина,
		|	ГоспошлинаДляКлиентБанкаСрезПоследних.Оплачена КАК Оплачена
		|ПОМЕСТИТЬ втГоспошлинаКлиентБанк
		|ИЗ
		|	РегистрСведений.ГоспошлинаДляКлиентБанка.СрезПоследних(, Основание = &Регистратор) КАК ГоспошлинаДляКлиентБанкаСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(втИсковаяГП.СуммаГоспошлинаПриход, 0) - ЕСТЬNULL(втГоспошлинаКлиентБанк.Госпошлина, 0) КАК Дельта,
		|	ЕСТЬNULL(втИсковаяГП.СуммаГоспошлинаПриход, 0) КАК ТекущаяГоспошлина,
		|	ЕСТЬNULL(втГоспошлинаКлиентБанк.Оплачена, Ложь) КАК БылаОплачена
		|ИЗ
		|	втИсковаяГП КАК втИсковаяГП
		|		Полное СОЕДИНЕНИЕ втГоспошлинаКлиентБанк КАК втГоспошлинаКлиентБанк
		|		ПО втИсковаяГП.Регистратор = втГоспошлинаКлиентБанк.Регистратор";
	
	Запрос.УстановитьПараметр("Займ", СудебноеДело.Займ);
	Запрос.УстановитьПараметр("Регистратор", СудебноеДело);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ЗаполненаВыборка = ВыборкаДетальныеЗаписи.Следующий();
	Если НЕ ЗаполненаВыборка Тогда
	
		Возврат;
	
	КонецЕсли;
	НужноОбновитьГП = НЕ ВыборкаДетальныеЗаписи.БылаОплачена И ВыборкаДетальныеЗаписи.Дельта <> 0;
	Если НЕ НужноОбновитьГП Тогда
	
		Возврат;
	
	КонецЕсли;
	
	Запись = РегистрыСведений.ГоспошлинаДляКлиентБанка.СоздатьМенеджерЗаписи();
	Запись.Период = ТекущаяДатаСеанса();
	Запись.Основание = СудебноеДело;
	Запись.Госпошлина = ВыборкаДетальныеЗаписи.ТекущаяГоспошлина;
	
	Запись.Записать();

КонецПроцедуры

Функция ЗаписываемИзСудебногоДела()

	Возврат ЭтотОбъект.Отбор.Количество() > 0
		И ЭтотОбъект.Отбор.Найти("Регистратор") <> Неопределено
		И ТипЗнч(ЭтотОбъект.Отбор.Регистратор.Значение) = Тип("ДокументСсылка.СудебноеДело");

КонецФункции // ()


Процедура ОбновитьОстатокИска(Отказ)

	ОбъектыОтбора = ОбъектыСервер.ОтборОбъектовПоРегистратору(ЭтотОбъект);
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ИсковыеТребованияОстатки.Займ КАК Займ,
	|	ИсковыеТребованияОстатки.ЦенаИскаОстаток КАК ЦенаИскаОстаток
	|ПОМЕСТИТЬ ТекЗадолженность
	|ИЗ
	|	РегистрНакопления.ИсковыеТребования.Остатки(, Займ В (&МассивОбъектов)) КАК ИсковыеТребованияОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбъектыВРаботеОстатки.Объект КАК ОбъектВРаботе
	|ПОМЕСТИТЬ ОбъектыВРаботе
	|ИЗ
	|	РегистрНакопления.ОбъектыВРаботе.Остатки(, Объект В (&Объекты)) КАК ОбъектыВРаботеОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбъектыВРаботе.ОбъектВРаботе КАК ОбъектВРаботе,
	|	ЕстьNull(ТекЗадолженность.ЦенаИскаОстаток, 0) КАК ЦенаИскаОстаток
	|ИЗ
	|	ОбъектыВРаботе КАК ОбъектыВРаботе
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТекЗадолженность КАК ТекЗадолженность
	|		ПО ОбъектыВРаботе.ОбъектВРаботе = ТекЗадолженность.Займ"); 
	
	Запрос.УстановитьПараметр("МассивОбъектов", ОбъектыОтбора);  
	Запрос.УстановитьПараметр("Объекты", ОбъектыОтбора);
	
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		Набор = РегистрыСведений.ЗадолженностьПоОбъектамОстатки.СоздатьНаборЗаписей();
		Набор.Отбор.Объект.Установить(Результат.ОбъектВРаботе);
		
		Если ЗначениеЗаполнено(Результат.ОбъектВРаботе) Тогда
			
			Если Результат.ЦенаИскаОстаток <> 0 Тогда
				Нов = Набор.Добавить();
				Нов.Объект = Результат.ОбъектвРаботе;
				Нов.ТипЗадолженности = ПланыВидовХарактеристик.ТипыЗадолженности.Сумма;
				Нов.СуммаДО = Результат.ЦенаИскаОстаток;
				Нов.СуммаРегл = Результат.ЦенаИскаОстаток;
			КонецЕсли;
			
		КонецЕсли;
		
		Попытка
			Набор.Записать();
		Исключение
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ОписаниеОшибки();
			Сообщение.Сообщить();
			Отказ = Истина;
			Возврат;
		КонецПопытки;
	КонецЦикла;

КонецПроцедуры
