
Функция НастройкиПарсинга(Настройка) Экспорт

	Значения = ЗначенияНастройки(Настройка);
	
	Данные = Новый Структура;
	
	НомераСтолбцов = Новый Структура;
	НомераСтолбцов.Вставить("Объект", Значения.Объект);
	НомераСтолбцов.Вставить("ДатаВыполнения", Значения.ДатаВыполнения);
	НомераСтолбцов.Вставить("ТипМероприятия", Значения.ТипМероприятия);
	НомераСтолбцов.Вставить("Результат", Значения.Результат);
	НомераСтолбцов.Вставить("Комментарий", Значения.Комментарий);

	Данные.Вставить("Столбцы", НомераСтолбцов);
	
	ЗначенияСвойств = ЗначенияНастройкиДопРеквизитов(Настройка);
	Данные.Вставить("СтолбцыСвойств", ЗначенияСвойств);
	
	МодульОбъекта = МодульПоОбъектуУчета(Значения.ОбъектУчета);
	Данные.Вставить("Модуль", МодульОбъекта);
	
	ДанныеПоУмолчанию = Новый Структура;
	ДанныеПоУмолчанию.Вставить("ТипМероприятия", Значения.ТипМероприятияПоУмолчанию);
	ДанныеПоУмолчанию.Вставить("Результат", Значения.РезультатПоУмолчанию);
	Данные.Вставить("ДанныеПоУмолчанию", ДанныеПоУмолчанию);
	
	ДанныеИзРеестра = Новый Структура;
	ДанныеИзРеестра.Вставить("ТипМероприятияИзРеестра", Значения.СоздатьМероприятияИзРеестра); 
	ДанныеИзРеестра.Вставить("РезультатМероприятийИзРеестра", Значения.СоздатьРезультатыИзРеестра);
	Данные.Вставить("ДанныеИзРеестра", ДанныеИзРеестра);
	
	Возврат Данные;

КонецФункции // ()

Функция ДоступныеОбъектыУчета() Экспорт

	Данные = Новый Массив;
	Данные.Добавить(Перечисления.ОбъектыУчета.ДолговыеОбязательства);
	Данные.Добавить(Перечисления.ОбъектыУчета.Контрагенты);
	
	Возврат Данные;
	
КонецФункции // ()

Функция ЗначенияНастройки(Настройка)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиЗагрузкиМероприятий.ОбъектСтолбец КАК Объект,
		|	НастройкиЗагрузкиМероприятий.ОбъектДата КАК ДатаВыполнения,
		|	НастройкиЗагрузкиМероприятий.ОбъектКомментарий КАК Комментарий,
		|	НастройкиЗагрузкиМероприятий.ОбъектТипМероприятия КАК ТипМероприятия,
		|	НастройкиЗагрузкиМероприятий.РезультатСтолбец КАК Результат,
		|	НастройкиЗагрузкиМероприятий.ТипМероприятия КАК ТипМероприятияПоУмолчанию,
		|	НастройкиЗагрузкиМероприятий.РезультатПоУмолчанию КАК РезультатПоУмолчанию,
		|	НастройкиЗагрузкиМероприятий.ОбъектУчета КАК ОбъектУчета,
		|	НастройкиЗагрузкиМероприятий.СоздатьМероприятияИзРеестра КАК СоздатьМероприятияИзРеестра,
		|	НастройкиЗагрузкиМероприятий.СоздатьРезультатыИзРеестра КАК СоздатьРезультатыИзРеестра
		|ИЗ
		|	Справочник.НастройкиЗагрузкиМероприятий КАК НастройкиЗагрузкиМероприятий
		|ГДЕ
		|	НастройкиЗагрузкиМероприятий.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Настройка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
	
		Возврат Неопределено;
	
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выгрузить();
	
	Возврат ВыборкаДетальныеЗаписи[0];

КонецФункции // ()

Функция ЗначенияНастройкиДопРеквизитов(Настройка)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиЗагрузкиМероприятийДополнительныеРеквизиты.НомерСтолбца КАК НомерСтолбца,
		|	НастройкиЗагрузкиМероприятийДополнительныеРеквизиты.Свойство КАК Свойство,
		|	НастройкиЗагрузкиМероприятийДополнительныеРеквизиты.Свойство.ТипЗначения КАК ТипЗначения,
		|	НастройкиЗагрузкиМероприятийДополнительныеРеквизиты.Свойство.Код КАК Код
		|ИЗ
		|	Справочник.НастройкиЗагрузкиМероприятий.ДополнительныеРеквизиты КАК НастройкиЗагрузкиМероприятийДополнительныеРеквизиты
		|ГДЕ
		|	НастройкиЗагрузкиМероприятийДополнительныеРеквизиты.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Настройка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
	
		Возврат Новый ТаблицаЗначений;
	
	КонецЕсли;
	
	Возврат РезультатЗапроса.Выгрузить();

КонецФункции // ()

Функция МодульПоОбъектуУчета(ОбъектУчета)

	МодульМенеджера = Перечисления.ОбъектыУчета.ПолучитьМодульСправочника(
		ОбъектУчета
	);
	
	Если МодульМенеджера = Неопределено Тогда
	
		ВызватьИсключение "Не удалось найти связи по выбранному объекту учета";
	
	КонецЕсли;
	
	Возврат МодульМенеджера;

КонецФункции // ()
