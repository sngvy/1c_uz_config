
#Область ОписаниеПеременных

&НаКлиенте
Перем обКонтроллерАТС;

&НаКлиенте
Перем СоответствиеНомеровХоста;

&НаКлиенте
Перем кэшКонтактов;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если обКонтроллерАТС <> Неопределено Тогда
		обКонтроллерАТС.GetExtensionsStatusesAndChannels();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если Источник = ИсточникВнешнихСобытий И ИмяСобытия = "ОбновлениеНомеров" Тогда	
		ЗаполнитьТранк(Параметр);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьКонтроллерАТС(ссылкаКонтроллерАТС) ЭКСПОРТ
	обКонтроллерАТС = ссылкаКонтроллерАТС;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСоответствиеНомеров(СоответствиеВнутреннихНомеров, СоответствиеКонтактов) ЭКСПОРТ
	СоответствиеНомеровХоста = СоответствиеВнутреннихНомеров;
	кэшКонтактов = СоответствиеКонтактов;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТранк(соотвХмл)
	
	Попытка
		
		соотвИнфо = соотвХмл.Получить("ChannelsInfo");
		
		списокУзловТранки = соотвИнфо.Получить("Trunks");
		
		Разговоры.Очистить();
		
		КолвоЗвонковТранка = 0;
		
		обТранк = списокУзловТранки.Получить(ПирТранка);
		Если обТранк <> Неопределено Тогда
			списокРазговоровТранка = обТранк.Получить("ExternalCalls");
			КолвоЗвонковТранка = списокРазговоровТранка.Количество();
			Для й = 0 По (КолвоЗвонковТранка-1) Цикл
				обРазговор = списокРазговоровТранка[й];
				ЗвонокНаТранке				= обРазговор.Получить("Connected");
				НаправлениеВходящНаТранке	= Булево(Число(обРазговор.Получить("Incoming")));
				ДлительностьНаТранке		= обРазговор.Получить("Duration");
				ВнешнийАбонентНаТранке		= обРазговор.Получить("ExternalAbonent");
				КонтрагентСсылка = Неопределено;
				КонтактноеЛицоСсылка = Неопределено;
				строкаЗвонок = Разговоры.Добавить();
				строкаЗвонок.Разговор		= бит_АТСКлиент.ПолучитьИмяПоВнутреннемуНомеру(СоответствиеНомеровХоста, ЗвонокНаТранке);
				строкаЗвонок.Направление	= ? (НаправлениеВходящНаТранке,  "<-" , "->");
				строкаЗвонок.Длительность	= ДлительностьНаТранке;
				бит_АТСКлиент.СократитьНомерНайтиКонтрагента(кэшКонтактов, СоответствиеНомеровХоста, ВнешнийАбонентНаТранке, КонтрагентСсылка, КонтактноеЛицоСсылка);
				строкаЗвонок.ВнешнийАбонент	= ВнешнийАбонентНаТранке;
				строкаЗвонок.Контрагент		= КонтрагентСсылка;
				строкаЗвонок.КонтактноеЛицо	= КонтактноеЛицоСсылка;
			КонецЦикла;
		КонецЕсли;
		
		ВсегоРазговоров = КолвоЗвонковТранка;

	Исключение
		бит_ТелефонияКлиент.ВывестиСообщение( ОписаниеОшибки() );
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти
