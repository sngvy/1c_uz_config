
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Обработка = РеквизитФормыВЗначение("Объект");
	Объект.ИмяКомпьютера = Обработка.ПолучитьИмяКомпьютера();
	Объект.ЦелеваяИнформационнаяБаза = Обработка.ПолучитьИмяЦелевойИБ();
	
	ОбновитьСостояниеВОбработке();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДвоичныеДанныеФайлаЗапроса()
	
	Обработка = РеквизитФормыВЗначение("Объект");
	Возврат Обработка.СформироватьФайлЗапроса();	
	
КонецФункции

&НаСервере
Процедура ЗагрузитьФайлОтветаНаСервере(ДвоичныеДанныеФайла)
	
	Обработка = РеквизитФормыВЗначение("Объект");	
	Обработка.ЗагрузитьФайлОтвета(ДвоичныеДанныеФайла);
	
	ОбновитьСостояниеВОбработке();
	
КонецПроцедуры

&НаСервере
Процедура ОтключитьАвтономныйРежимНаСервере()
	
	Обработка = РеквизитФормыВЗначение("Объект");	
	Обработка.ОтключитьАвтономныйРежим();
	
	ОбновитьСостояниеВОбработке();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСостояниеВОбработке()

	Обработка = РеквизитФормыВЗначение("Объект");
	Состояние = Обработка.ПолучитьСостояниеАвтономногоРежима();
	
	Элементы.ГруппаАвтономныйРежим.Видимость = Состояние.АвтономныеДанныеУстановлены;
	Элементы.ГруппаДейтвия.Видимость = Не Состояние.АвтономныеДанныеУстановлены;
	
	Если Состояние.АвтономныйРежимАктивен Тогда
		Элементы.ДекорацияАвтономныйРежимСостояние.Заголовок = "Состояние: Активен";
	Иначе
		Элементы.ДекорацияАвтономныйРежимСостояние.Заголовок = "Состояние: Не активен";		
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Функция ВыбратьФайл(Фильтр = "(файл ответа *.lsmr)|*.lsmr", Заголовок = "Выберите файл")
	
	ВыбранныйФайл = Неопределено;
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогОткрытияФайла.Фильтр = Фильтр;
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	ДиалогОткрытияФайла.Заголовок = Заголовок;
	
	Если ДиалогОткрытияФайла.Выбрать() Тогда		
		МассивФайлов = ДиалогОткрытияФайла.ВыбранныеФайлы;
		Если МассивФайлов.Количество() > 0 Тогда 			
			ВыбранныйФайл = МассивФайлов[0];			
		КонецЕсли;
	КонецЕсли;
	
	Возврат ВыбранныйФайл;
	
КонецФункции

&НаКлиенте
Функция ВыбратьПутьСохраненияФайла(Фильтр = "(файл запроса *.lsmq)|*.lsmq", ИмяПоУмолчанию = "LicenseModeRequest.lsmq", Заголовок = "Сохранение файла")
	
	ВыбранныйФайл = Неопределено;
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогОткрытияФайла.ПолноеИмяФайла = ИмяПоУмолчанию;
	ДиалогОткрытияФайла.Фильтр = Фильтр;
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	ДиалогОткрытияФайла.Заголовок = Заголовок;
	
	Если ДиалогОткрытияФайла.Выбрать() Тогда		
		МассивФайлов = ДиалогОткрытияФайла.ВыбранныеФайлы;
		Если МассивФайлов.Количество() > 0 Тогда			
			ВыбранныйФайл = МассивФайлов[0];			
		КонецЕсли;		
	КонецЕсли;
	
	Возврат ВыбранныйФайл;
	
КонецФункции

&НаКлиенте
Процедура СохранитьФайлЗапроса(Команда)
	
	ПутьСохранения = ВыбратьПутьСохраненияФайла();
	
	Если ПутьСохранения <> Неопределено Тогда		
		ФайлЗапросаДвоичныеДанные = ПолучитьДвоичныеДанныеФайлаЗапроса();
		ФайлЗапросаДвоичныеДанные.Записать(ПутьСохранения);		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФайлОтвета(Команда)
	
	ВыбранныйФайл = ВыбратьФайл();
	
	Если ЗначениеЗаполнено(ВыбранныйФайл) Тогда
		
		ДвоичныеДанныеФайла = Новый ДвоичныеДанные(ВыбранныйФайл);
		ЗагрузитьФайлОтветаНаСервере(ДвоичныеДанныеФайла);
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьАвтономныйРежим(Команда)
	
	ОтключитьАвтономныйРежимНаСервере();
	
КонецПроцедуры
